using Nitra;
using Nitra.Runtime;
using Nemerle;
using Nemerle.Collections;
using System.Collections.Generic;
using Whitespaces;

[Splicable]
syntax module CalcGrammar
{
  using PrettyPrint;
  using TokenNames;
  using StandardSpanClasses;
  using IncGrammar;
  using NumParser;

  [StartRule]
  syntax start = (expr; ','; ?)+ !Any { [Cached] Value() : list[double] = Exprs.Item1.MapToList(_.Value()); }

  [StartRule]
  syntax start2 = expr !Any { [Cached] Value() : double = Expr.Value(); }

  // We can redefine names for fields in Any time.
  literal Operator = "+", "-", "??";

  alias Condition = expr ^ 301;

  [StartRule]
  syntax expr
  {
    Value() : double;
    missing Value = double.NaN;

    | [SpanClass(Number)]
      num        = number                             { override Value = Number.Value(); }
    | rounds     = '(' expr ')'                       { override Value = Expr.Value(); }
    | add        = expr sm '+' sm expr precedence 10  { override Value = Expr1.Value() + Expr2.Value(); }
    | sub        = expr sm '-' sm expr precedence 10  { override Value = Expr1.Value() - Expr2.Value(); }
    | mul        = expr sm '*' sm expr precedence 20  { override Value = Expr1.Value() * Expr2.Value(); }
    | div        = expr sm '/' sm expr precedence 20  { override Value = Expr1.Value() / Expr2.Value(); }
    | mod        = expr sm '%' sm expr precedence 20  { override Value = Expr1.Value() % Expr2.Value(); }
    | pow        = expr sm '^' sm expr precedence 30 right-associative
                                                      { override Value = System.Math.Pow(Expr1.Value(), Expr2.Value()); }
    | neg        = '-' expr            precedence 100 { override Value = -Expr.Value(); }
    | prefixDec  = "--" expr           precedence 200 { override Value = Expr.Value() - 1.0; }
    | postfixDec = expr "--"           precedence 200 { override Value = Expr.Value() - 1.0; }

//    | cond       = Condition sm '?' sm  expr sm  Colon=':' sm expr
//      { override Value = if (Condition.Value() != 0.0) Expr1.Value() else Expr2.Value(); }
//    | coalescing = expr ^ 401 sm "??" sm expr ^ 400
//      { override Value = if (Expr1.Value() != 0.0) Expr1.Value() else Expr2.Value(); }
//    | test       = "test" number "test" number { override Value = 42; }
  }
}

syntax module IncGrammar
{
  using cp = CalcGrammar;
  using CalcGrammar;

  extend syntax cp.expr
  {
    | plus       = '+'  cp.expr ^ 100  { override Value = Expr.Value(); }
    | prefixInc  = "++" cp.expr ^ 200  { override Value = Expr.Value() + 1; }
    | postfixInc = cp.expr ^ 200 "++"  { override Value = Expr.Value() + 1; }
  }
}
