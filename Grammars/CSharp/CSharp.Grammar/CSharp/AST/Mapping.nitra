using Nitra;
using Nitra.Declarations;

using DotNet;

using Nemerle.Collections;

namespace CSharp
{
  map syntax Main.CompilationUnit -> CompilationUnit
  {
    NamespaceMemberDeclarations -> Members;
  }

  map syntax Types.TypeParameter -> TypeParameterAlias
  {
    Name -> Name;
    Variance = 
      if (VarianceAnnotationOpt.HasValue)
        ParsedValue(VarianceAnnotationOpt.Value.Annotation, if (VarianceAnnotationOpt.Value.Annotation.Length == 2) Variance.Covariant else Variance.Contravariant)
      else ParsedValue(VarianceAnnotationOpt.Span, Variance.None);
    Attributes -> Attributes;
  }

  map syntax Types.TypeParameters -> TypeParameterAlias*
  {
    TypeParameters.Item1
  }

  map syntax Types.TypeParameterConstraint -> TypeParameterConstraint
  {
    | Ctor   {}
    | Class  {}
    | Struct {}
    | Base { AnyType -> Type; }
  }

  map syntax Types.TypeParameterConstraints -> TypeParameterConstraint*
  {
    TypeParameterConstraints.Item1
  }

  map syntax Types.TypeParameterConstraintsClause -> TypeParameterConstraintsClause
  {
    Reference                -> ParameterName;
    TypeParameterConstraints -> Constraints;
  }

  map syntax Types.TypeParameterConstraintsClauses -> TypeParameterConstraintsClause*
  {
    TypeParameterConstraintsClauses
  }

  map syntax TopDeclarations.TypeDeclaration -> NamespaceMember
  {
    | Class -> TopClass
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Struct -> TopStruct
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Interface -> TopInterface
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Enum -> TopEnum
      {
        Name        -> Name;
        EnumBaseOpt -> EnumBase;
        EnumBody.EnumMemberDeclarations -> Members;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Delegate -> TopDelegate
      {
        Name             -> Name;
        AnyType          -> ReturnType;
        FormalParameters.Item1 -> Parameters;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
  }

  map syntax TopDeclarations.TypeDeclaration(_tag : NestedTypeTag) -> TypeMember
  {
    | Class -> NestedClass
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Struct -> NestedStruct
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Interface -> NestedInterface
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Enum -> NestedEnum
      {
        Name        -> Name;
        EnumBaseOpt -> EnumBase;
        EnumBody.EnumMemberDeclarations -> Members;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Delegate -> NestedDelegate
      {
        Name             -> Name;
        AnyType          -> ReturnType;
        FormalParameters.Item1 -> Parameters;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
  }

  map syntax TopDeclarations.TypeBase -> QualifiedReference*
  {
    AnyTypes.Item1
  }

  map syntax TopDeclarations.TypeMemberDeclaration -> TypeMember*
  {
    | NestedType      -> [TypeDeclaration(NestedTypeTag {})]
    | Constructor     -> [Member.Constructor
      {
        Name                      -> Name;
        FormalParameters.Item1    -> Parameters;
        Attributes                -> Attributes;
        Modifiers                 -> Modifiers;
        ConstructorInitializerOpt -> Initializer;
      }]
    | Destructor      -> [Member.Destructor
      {
        Name             -> Name;
        FormalParameters.Item1 -> Parameters;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | Method          -> [Member.Method
      {
        MethodHeader.Name             -> Name;
        MethodHeader.InterfaceTypeOpt -> InterfaceType;
        match(MethodHeader.TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        MethodHeader.TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        MethodHeader.AnyType          -> ReturnType;
        MethodHeader.FormalParameters.Item1 -> Parameters;
        Attributes                    -> Attributes;
        Modifiers                     -> Modifiers;
      }]
    | Property        -> [Member.Property
      {
        Name             -> Name;
        AnyType          -> Type;
        InterfaceTypeOpt -> InterfaceType;
        PropertyBody.AccessorDeclarations -> Accessors;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | Event           -> [Member.Event
      {
        Name             -> Name;
        InterfaceTypeOpt -> InterfaceType;
        EventHeader { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; } -> Header;
        EventAccessorDeclarations -> Accessors;
      }]
    | Indexer         -> [Member.Indexer
      {
        Name = Name(this.File, this.KwThis, "#Item");
        FormalParameters.Item1 -> Parameters;
        AnyType          -> Type;
        InterfaceTypeOpt -> InterfaceType;
        PropertyBody.AccessorDeclarations -> Accessors;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | SimpleEvent     -> VariableDeclarators       .Item1(EventHeader           { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | Field           -> VariableDeclarators       .Item1(FieldHeader           { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | Constant        -> ConstantDeclarators       .Item1(ConstantHeader        { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | FixedSizeBuffer -> FixedSizeBufferDeclarators.Item1(FixedSizeBufferHeader { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | OperatorUnary   -> [Member.OperatorUnary
      {
        Name = Name(this.File, this.OverloadableUnaryOperator, this.GetText(OverloadableUnaryOperator));
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
    | OperatorBinary  -> [Member.OperatorBinary
      {
        Name = Name(this.File, this.OverloadableBinaryOperator, this.GetText(OverloadableBinaryOperator));
        FormalParameter1 -> Parameter1;
        FormalParameter2 -> Parameter2;
        AnyType          -> ReturnType;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | ImplicitOperator   -> [Member.OperatorCnv
      {
        Name = Name(this.File, this.KwImplicit, "#Implicit");
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Kind = ParsedValue(this.KwImplicit, OperatorKind.Implicit);
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
    | ExplicitOperator   -> [Member.OperatorCnv
      {
        Name = Name(this.File, this.KwExplicit, "#Explicit");
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Kind = ParsedValue(this.KwExplicit, OperatorKind.Explicit);
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
  }

  map syntax Statements.VariableDeclarator(header : FieldHeader) -> Member.Field
  {
    Name   -> Name;
    header -> Header;
  }

  map syntax Statements.VariableDeclarator(header : EventHeader) -> Member.Event
  {
    Name   -> Name;
    header -> Header;
    None() -> InterfaceType;
    []     -> Accessors;
  }

  map syntax TopDeclarations.EnumBase -> QualifiedReference
  {
    AnyType
  }

  map syntax TopDeclarations.EnumMemberDeclaration -> EnumMember
  {
    Name       -> Name;
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.EnumMemberDeclarations -> EnumMember*
  {
    EnumMemberDeclarations.Item1
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration -> NamespaceMember
  {
    | Namespace ->
      ExplicitNamespace
      {
        NamespaceName.Name          -> Name;
        NamespaceName.Path(ImplicitNamespaceTag{}) -> Path;
        //ExternAliasDirectives       -> ExternAlias;
        //UsingDirectives             -> UsingDirectives;
        NamespaceMemberDeclarations -> Members;
      }

    | Type -> TypeDeclaration
    | ExternAliasDirective -> ExternAliasDirective
      {
        Name -> Name;
      }
    | Alias -> UsingAliasDirective
      {
        Name          -> Name;
        QualifiedName -> NamespaceOrTypeName;
      }

    | Open -> UsingOpenDirective
      {
        QualifiedName -> NamespaceOrTypeName;
      }

    | GlobalAttributeSection -> GlobalAttributes
      {
        GlobalAttributeSection -> Attributes;
      }
  }

  ast UsingSyntax : NamespaceMember
  {
  }

  map syntax Extensible.NamespaceMemberDeclaration.UsingSyntax -> NamespaceMember
  {
    UsingSyntax
    {
    }
  }

  ast ImplicitNamespaceTag { }

  map syntax CsIdentifiers.Name(_tag : ImplicitNamespaceTag) -> ImplicitNamespace
  {
    this -> Name;
  }

  map syntax TopDeclarations.ConstructorInitializer -> ConstructorInitializer
  {
    | This {}
    | Base {}
  }

  map syntax TopDeclarations.FormalParameter -> FormalParameter
  {
    Name       -> Name;
    AnyType    -> Type;
    Attributes -> Attributes;
    Modifier =
      if (this.ParameterModifierOpt.HasValue)
      {
        def result =
          match (this.GetText(this.ParameterModifierOpt.Value))
          {
            | "ref"    => ParameterModifier.Ref
            | "out"    => ParameterModifier.Out
            | "this"   => ParameterModifier.This
            | "params" => ParameterModifier.Params
            | _        => assert2(false, "Unknown parameter modifier value"); ParameterModifier.None
          };
        ParsedValue(this.ParameterModifierOpt.Value, result)
      }
      else
        ParsedValue(this.ParameterModifierOpt.Span, ParameterModifier.None);
  }

  map syntax Statements.ConstantDeclarator(header : ConstantHeader) -> Member.Constant
  {
    Name -> Name;
    header -> Header;
  }

  map syntax TopDeclarations.FixedSizeBufferDeclarator(header : FixedSizeBufferHeader) -> Member.FixedSizeBuffer
  {
    Name   -> Name;
    header -> Header;
  }

  map syntax TopDeclarations.GetAccessorDeclaration -> PropertyAccessor.Get
  {
    Name = Name(this.File, this.KwGet, "#Get");
    Attributes -> Attributes;
    Modifiers  -> Modifiers;
  }

  map syntax TopDeclarations.SetAccessorDeclaration -> PropertyAccessor.Set
  {
    Name = Name(this.File, this.KwSet, "#Set");
    Attributes -> Attributes;
    Modifiers  -> Modifiers;
  }

  map syntax TopDeclarations.AccessorDeclarations -> PropertyAccessor*
  {
    | GetSet -> match (this) { (g, Some(s)) -> [g, s] | (g, None) -> [g] }
    | SetGet -> match (this) { (s, Some(g)) -> [s, g] | (s, None) -> [s] }
    | None   -> []
  }

  map syntax TopDeclarations.AddAccessorDeclaration -> EventAccessor.Add
  {
    Name = Name(this.File, this.KwAdd, "#Add");
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.RemoveAccessorDeclaration -> EventAccessor.Remove
  {
    Name = Name(this.File, this.KwRemove, "#Remove");
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.EventAccessorDeclarations -> EventAccessor*
  {
    | AddRemove -> [AddAccessorDeclaration, RemoveAccessorDeclaration]
    | RemoveAdd -> [RemoveAccessorDeclaration, AddAccessorDeclaration]
  }

  map syntax TopDeclarations.Modifiers -> Modifier*
  {
    Modifiers
  }

  map syntax TopDeclarations.Modifier -> Modifier
  {
    | New       {}
    | Public    {}
    | Protected {}
    | Internal  {}
    | Private   {}
    | Virtual   {}
    | Volatile  {}
    | Static    {}
    | Readonly  {}
    | Sealed    {}
    | Override  {}
    | Abstract  {}
    | Extern    {}
    | Unsafe    {}
    | AsyncMod -> Async {}
  }

  map syntax CsIdentifiers.QualifiedIdentifier -> QualifiedReference
  {
    match (this)
    {
      | (None,    (first :: other, _)) -> fold (item in other; acc = first();    acc = item(acc))
      | (Some(a), (first :: other, _)) -> fold (item in other; acc = first(a()); acc = item(acc))
      | (_,       ([],             _)) = AstUtils.CreateMissingQualifiedReference(QualifiedIdentifierParts.File, QualifiedIdentifierParts.Span);
    }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPartTypeArguments -> QualifiedReference*
  {
    | TypeArgumentList -> AnyTypes.Item1
    | OpenTypeArguments =
      {
        def args = array(RankSeparators.Count + 1);
        args[0] = AstUtils.CreateSimpleName(this.File, NSpan(OpenBrace.EndPos), "#");
        foreach (rs in RankSeparators with index)
          args[index + 1] = AstUtils.CreateSimpleName(this.File, NSpan(rs.EndPos), "#");
        QualifiedReference.AstList(this, args)
      }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPart -> QualifiedReference
  {
    let qid : QualifiedReference = Simple { Reference -> Name; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPart(aliasAst : Nitra.Declarations.Reference) -> QualifiedReference
  {
    let qid : QualifiedReference = Aliased { Reference -> Name; aliasAst -> Alias; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPart(qualifier : QualifiedReference) -> QualifiedReference
  {
    let qid : QualifiedReference = Qualified { Reference -> Name; qualifier -> Qualifier; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.InterfaceType -> QualifiedReference
  {
    match (this)
    {
      | (None,    (first, _) :: other) -> fold (item in other; acc = first();    acc = item(acc))
      | (Some(a), (first, _) :: other) -> fold (item in other; acc = first(a()); acc = item(acc))
      | (_,       []                 ) = AstUtils.CreateMissingQualifiedReference(InterfaceTypeParts.File, InterfaceTypeParts.Span);
    }
  }

  map syntax CsIdentifiers.InterfaceTypePart -> QualifiedReference
  {
    let qid : QualifiedReference = Simple { Reference -> Name; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.InterfaceTypePart(aliasAst : Nitra.Declarations.Reference) -> QualifiedReference
  {
    let qid : QualifiedReference = Aliased { Reference -> Name; aliasAst -> Alias; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.InterfaceTypePart(qualifier : QualifiedReference) -> QualifiedReference
  {
    let qid : QualifiedReference = Qualified { Reference -> Name; qualifier -> Qualifier; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax Types.TypeName -> QualifiedReference
  {
    | QualifiedName  -> QualifiedIdentifier
    | PredefinedType -> Simple { Name = Reference(this.File, this.Span, GetText(PredefinedType)); }
  }

  map syntax Types.AnyNonArrayType -> QualifiedReference
  {
    match (this)
    {
      | (typeName, None) -> typeName
      | (typeName, Some) -> Generic
        {
          Qualified
          {
            Simple { Name = Reference(this.File, this.Span, "System"); } -> Qualifier;
            Name = Reference(this.File, this.Span, "Nullable");
          } -> QualifiedName;
          [ typeName ] -> Arguments;
        }
    }
  }

  map syntax Types.TypeSpecifier(qualifiedName : QualifiedReference) -> QualifiedReference
  {
    | PointerSpecifier -> Generic
      {
        Simple { Name = Reference(this.File, this.Span, "#Pointer"); } -> QualifiedName;
        [qualifiedName] -> Arguments;
      }
    | RankSpecifier -> Generic
      {
        Simple { Name = Reference(this.File, this.Span, "#Array" + (RankSpecifier.RankSeparators.Count + 1).ToString()); } -> QualifiedName;
        [qualifiedName] -> Arguments;
      }
  }

  map syntax Types.AnyType -> QualifiedReference
  {
    fold (ts in TypeSpecifiers; acc = AnyNonArrayType; acc = ts(acc))
  }

  // Reference is a predefined declaration
  map syntax CsIdentifiers.QualifiedAlias -> Reference { Alias }

  map syntax Attributes.Attribute(targetOpt : CustomAttributeTarget?) -> CustomAttributeReference
  {
    targetOpt     -> Target;
    QualifiedName -> Name;
  }

  map syntax Attributes.AttributeList(targetOpt : CustomAttributeTarget?) -> CustomAttributeReference*
  {
    Attributes.Item1(targetOpt)
  }

  map syntax Attributes.AttributeSection -> CustomAttributeReference*
  {
    AttributeList(AttributeTargetSpecifierOpt)
  }

  map syntax Attributes.GlobalAttributeSection -> CustomAttributeReference*
  {
    AttributeList(Some(GlobalAttributeTargetSpecifier))
  }

  map syntax Attributes.AttributeTargetSpecifier -> CustomAttributeTarget =
  {
    match (this.GetText(this.AttributeTarget))
    {
      | "field"    => CustomAttributeTarget.Field(this.File, this.AttributeTarget)
      | "event"    => CustomAttributeTarget.Event(this.File, this.AttributeTarget)
      | "method"   => CustomAttributeTarget.Method(this.File, this.AttributeTarget)
      | "param"    => CustomAttributeTarget.Param(this.File, this.AttributeTarget)
      | "property" => CustomAttributeTarget.Property(this.File, this.AttributeTarget)
      | "return"   => CustomAttributeTarget.Return(this.File, this.AttributeTarget)
      | "type"     => CustomAttributeTarget.Type(this.File, this.AttributeTarget)
      | "typevar"  => CustomAttributeTarget.Typevar(this.File, this.AttributeTarget)
      | _          => CustomAttributeTarget.AmbiguousOrMissing(this.File, this.AttributeTarget)
    }
  }

  map syntax Attributes.GlobalAttributeTargetSpecifier -> CustomAttributeTarget =
  {
    match (this.GetText(this.GlobalAttributeTarget))
    {
      | "assembly" => CustomAttributeTarget.Assembly(this.File, this.GlobalAttributeTarget)
      | "module"   => CustomAttributeTarget.Module(this.File, this.GlobalAttributeTarget)
      | _          => CustomAttributeTarget.AmbiguousOrMissing(this.File, this.GlobalAttributeTarget)
    }
  }
}
