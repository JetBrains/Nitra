using Nitra;
using Nitra.Runtime.Binding;
using DotNet;

namespace CSharp
{
  abstract declaration Member : TypeMember
  {
    symbol
    {
      in Flags : ModifierSet;
      FullName = Parent.FullName + "." + this.Name;
    }

    | Constructor
      {
        symbol
        {
          Kind      = "constructor";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Attributes.NameScope       = Scope;

        Parameters  : FormalParameter*;
        Attributes  : Attribute*;
        Modifiers   : Modifier*;
        Initializer : ConstructorInitializer?;
      }
    | Destructor
      {
        symbol
        {
          Kind      = "destructor";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Attributes.NameScope       = Scope;

        Parameters : FormalParameter*;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | Method : GenericEntity
      {
        symbol
        {
          Kind      = "method";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        InterfaceType.Scope        = Scope;
        ReturnType.Scope           = TypeParameterConstraints.Scope;
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = TypeParameterConstraints.Scope;
        Symbol.ReturnType          = ReturnTypeRef.Symbol;
        Attributes.NameScope       = Scope;

        InterfaceType            : QualifiedReference?;
        IsPartial                : bool;
        Parameters               : FormalParameter*;
        ReturnType               : QualifiedReference;
        Attributes               : Attribute*;
        Modifiers                : Modifier*;
      }
    | Property
      {
        symbol
        {
          Kind      = "property";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;

          in Type : TypeSymbol;
        }

      stage 1:
        out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

        Symbol.Flags        |= Modifiers.Flags;
        Modifiers.Flags      = ModifierSet(context);
        InterfaceType.Scope  = Scope;
        Type.Scope           = Scope;
        Symbol.Type          = TypeRef.Symbol;
        Accessors.Scope      = Scope;
        Attributes.NameScope = Scope;

        InterfaceType : QualifiedReference?;
        Type          : QualifiedReference;
        Accessors     : PropertyAccessor*;
        Attributes    : Attribute*;
        Modifiers     : Modifier*;
      }
    | Event
      {
        symbol
        {
          Kind      = "event";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;

          in Type : TypeSymbol;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        InterfaceType.Scope         = Scope;
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.TypeRef.Symbol;
        Accessors.Scope             = Scope;
        Header.Attributes.NameScope = Scope;

        InterfaceType : QualifiedReference?;
        Header        : EventHeader;
        Accessors     : EventAccessor*;
      }
    | Indexer
      {
        symbol
        {
          Kind      = "indexer";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
          in  Type           : TypeSymbol;
        }

      stage 1:
        out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        InterfaceType.Scope        = Scope;
        Type.Scope                 = Scope;
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Symbol.Type                = TypeRef.Symbol;
        Accessors.Scope            = Scope;
        Attributes.NameScope       = Scope;

        Parameters    : FormalParameter*;
        Type          : QualifiedReference;
        InterfaceType : QualifiedReference?;
        Accessors     : PropertyAccessor*;
        Attributes    : Attribute*;
        Modifiers     : Modifier*;
      }
    | Constant
      {
        symbol
        {
          Kind      = "constant";
          SpanClass = NitraCSharpLanguage.ConstantSpanClass;

          in Type : TypeSymbol;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.TypeRef.Symbol;
        Header.Attributes.NameScope = Scope;

        Header : ConstantHeader;
      }
    | Field
      {
        symbol
        {
          Kind      = "field";
          SpanClass = NitraCSharpLanguage.FieldSpanClass;

          in Type : TypeSymbol;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.TypeRef.Symbol;
        Header.Attributes.NameScope = Scope;

        Header : FieldHeader;
      }
    | FixedSizeBuffer
      {
        symbol
        {
          Kind      = "fixed size buffer";
          SpanClass = NitraCSharpLanguage.FieldSpanClass;

          in Type : TypeSymbol;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.TypeRef.Symbol;
        Header.Attributes.NameScope = Scope;

        Header : FixedSizeBufferHeader;
      }
    | OperatorUnary
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        Symbol.Flags             |= Modifiers.Flags;
        Modifiers.Flags           = ModifierSet(context);
        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.CurrentScope    = Scope;
        Symbol.ReturnType         = ReturnTypeRef.Symbol;
        Attributes.NameScope      = Scope;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | OperatorBinary
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        ReturnType.Scope           = Scope;
        Parameter1.ContainingTable = Symbol.ParameterScope;
        Parameter1.CurrentScope    = Scope;
        Parameter2.ContainingTable = Symbol.ParameterScope;
        Parameter2.CurrentScope    = Scope;
        Symbol.ReturnType          = ReturnTypeRef.Symbol;
        Attributes.NameScope       = Scope;

        ReturnType : QualifiedReference;
        Parameter1 : FormalParameter;
        Parameter2 : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | OperatorCnv
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          out ParameterScope : TableScope = TableScope("parameters", this);
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        Symbol.Flags             |= Modifiers.Flags;
        Modifiers.Flags           = ModifierSet(context);
        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.CurrentScope    = Scope;
        Symbol.ReturnType         = ReturnTypeRef.Symbol;
        Attributes.NameScope      = Scope;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
        Kind       : OperatorKind;
      }
  }

  abstract ast ConstructorInitializer
  {
    // TODO: Map arguments
    | This
    | Base
  }

  ast FieldHeader
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast ConstantHeader
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast EventHeader
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast FixedSizeBufferHeader
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  abstract declaration PropertyAccessor
  {
    in Scope : Scope;

    Attributes.NameScope = Scope;

    Attributes : Attribute*;
    Modifiers  : Modifier*;
    // TODO: Map body
    | Get
    | Set
  }

  abstract declaration EventAccessor
  {
    in Scope : Scope;

    Attributes.NameScope = Scope;

    Attributes : Attribute*;
    // TODO: Map body
    | Add
    | Remove
  }
}
