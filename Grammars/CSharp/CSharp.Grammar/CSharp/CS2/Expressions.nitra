namespace CSharp
{
  syntax module Expressions
  {
    using Nitra.Core;

    using CSharp.TokenNames;
    using CSharp.Core;
    using Literals;
    using CSharp.CsIdentifiers;
    using Types;
    using Statements;

    keyword regex ['A'..'Z', 'a'..'z', '_'..'_']+ rule S;

    alias QualifiedName       = QualifiedIdentifier;

    ////////////////////////////////////////////////////////////////////////////////////
    // Arguments

    regex ArgumentModfier = "ref" | "out";
    syntax ArgumentName    = Reference !"::" ":" sm;
    syntax Argument
    {
      | Ref    = ArgumentName? Modfier="ref" sm Expression;
      | Out    = ArgumentName? Modfier="out" sm Expression;
      | Simple = ArgumentName?                  Expression;
    }

    syntax ArgumentList    = (Argument; ","sm)*;

    //
    ////////////////////////////////////////////////////////////////////////////////////

    ////////////////////////////////////////////////////////////////////////////////////
    //  Initializers: array-initializer, variable-initializer

    syntax ArrayInitializer = "{" sm (VariableInitializer; "," sm; ?)* sm "}";

    syntax VariableInitializer
    {
      | Expression
      | ArrayInitializer
    }

    //
    ////////////////////////////////////////////////////////////////////////////////////

    extend syntax Expression
    {
      // B.1.8 Literals
      | BooleanLiteral;
      | RegularStringLiteral;
      | VerbatimStringLiteral;
      | RealLiteral;
      | IntegerLiteral;
      | CharacterLiteral;
      | NullLiteral            = "null";

      | QualifiedName; // simple-name || member-access
      | Parenthesized          = "(" Expression ")"; // parenthesized-expression
      | Cast1                  = "(" !Expression AnyType ")" Expression;
      | Cast2                  = "(" &Expression AnyType ")" &CastPredicate Expression;
      | ThisAccess             = "this";
      | BaseAccessMember       = "base" "." QualifiedName=QualifiedIdentifierContinuation;
      | BaseAccessIndexer      = "base" "[" ArgumentList "]";
      //BaseError              = "base" = Error;

      // delegate-creation-expression || object-creation-expression || anonymous-object-creation-expression
      | NewObject_1            = "new" sm AnyNonArrayType "(" ArgumentList ")";             // delegate-creation-expression || object-creation-expression (1)
      | NewArray_3             = "new" sm AnyNonArrayType RankSpecifier+ sm ArrayInitializer;                           // array-creation-expression  (2)
      | NewArray_4             = "new" sm RankSpecifier sm ArrayInitializer;                                            // array-creation-expression  (3)

      | TypeOf                 =  "typeof"     "(" AnyType    ")";
      | Checked                =  "checked"    "(" Expression ")";
      | Unchecked              =  "unchecked"  "(" Expression ")";
      | Default                =  "default"    "(" AnyType    ")";
      | Sizeof                 =  "sizeof"     "(" AnyType    ")";

      precedence AssignmentOrAnonymousFunction:
      | Assignment             = Expression sm "="  sm Expression right-associative;
      | MulAssignment          = Expression sm "*=" sm Expression right-associative;
      | DivAssignment          = Expression sm "/=" sm Expression right-associative;
      | RemainderAssignment    = Expression sm "%=" sm Expression right-associative;
      | AddAssignment          = Expression sm "+=" sm Expression right-associative;
      | SubAssignment          = Expression sm "-=" sm Expression right-associative;
      | ShiftLeftAssignment    = Expression sm "<<="sm Expression right-associative;
      | ShiftRightAssignment   = Expression sm ">>="sm Expression right-associative;
      | AndAssignment          = Expression sm "&=" sm Expression right-associative;
      | XorAssignment          = Expression sm "^=" sm Expression right-associative;
      | OrAssignment           = Expression sm "|=" sm Expression right-associative;

      precedence Conditional:
      | Conditional            = Condition = Expression ^ Conditional sm "?" sm Expr1 = Expression sm ":" sm Expr2 = Expression;

      precedence NullCoalescing:
      | NullCoalescing         = Expr1 = Expression sm "??" sm Expr2 = Expression;

      precedence ConditionalOR:
      | ConditionalOr          = Expr1 = Expression sm "||" sm Expr2 = Expression;

      precedence ConditionalAND:
      | ConditionalAnd         = Expr1 = Expression sm "&&" sm Expr2 = Expression;

      precedence LogicalOR:
      | LogicalOr              = Expr1 = Expression sm "|"  sm Expr2 = Expression;

      precedence LogicalXOR:
      | LogicalXor             = Expr1 = Expression sm "^"  sm Expr2 = Expression;

      precedence LogicalAND:
      | LogicalAnd             = Expr1 = Expression sm "&"  sm Expr2 = Expression;

      precedence Equality:
      | Equal                  = Expr1 = Expression sm "==" sm Expr2 = Expression;
      | NotEqual               = Expr1 = Expression sm "!=" sm Expr2 = Expression;

      precedence RelationalAndTypeTesting:
      | LessThan               = Expr1 = Expression sm "<"  sm Expr2 = Expression;
      | GreaterThan            = Expr1 = Expression sm ">"  sm Expr2 = Expression;
      | LessThanOrEqual        = Expr1 = Expression sm "<=" sm Expr2 = Expression;
      | GreaterThanOrEqual     = Expr1 = Expression sm ">=" sm Expr2 = Expression;
      | Is                     = Expr1 = Expression ^ RelationalAndTypeTesting sm "is" sm Expr2 = AnyTypeNullableHack;
      | As                     = Expr1 = Expression ^ RelationalAndTypeTesting sm "as" sm Expr2 = AnyTypeNullableHack;

      precedence Shift:
      | ShiftLeft              = Expr1 = Expression sm "<<" sm Expr2 = Expression;
      | ShiftRight             = Expr1 = Expression sm ">>" sm Expr2 = Expression;

      precedence Additive:
      | Add                    = Expr1 = Expression sm "+"  sm Expr2 = Expression;
      | Sub                    = Expr1 = Expression sm "-"  sm Expr2 = Expression;

      precedence Multiplicative:
      | Mul                    = Expr1 = Expression sm "*"  sm Expr2 = Expression; 
      | Div                    = Expr1 = Expression sm "/"  sm Expr2 = Expression;
      | Remainder              = Expr1 = Expression sm "%"  sm Expr2 = Expression;

      precedence PointerIndirection:
      | PointerIndirection     = "*"  Expr1 = Expression;// pointer-indirection-expression

      precedence Unary:
      | Identity               = "+"  Expr1 = Expression;
      | Negation               = "-"  Expr1 = Expression;
      | LogicalNegation        = "!"  Expr1 = Expression;
      | BitwiseNegation        = "~"  Expr1 = Expression;
      | PreIncrement           = "++" Expr1 = Expression;
      | PreDecrement           = "--" Expr1 = Expression;
      //ExplicitlyCast         = (T) Expression ;

      precedence Primary:
      | MemberAccess1          =  Expr1 = Expression ^ Primary "." QualifiedName=QualifiedIdentifierContinuation; // member-access
      | MemberAccess2          =  PredefinedType "." QualifiedName=QualifiedIdentifierContinuation; // member-access
      | Invocation             =  Expression ^ Primary "(" ArgumentList ")";
      | IndexerAccess          =  Expression ^ Primary "[" ArgumentList "]";
      | PostIncrement          =  Expression "++";
      | PostDecrement          =  Expression "--";
    }
  }
}
