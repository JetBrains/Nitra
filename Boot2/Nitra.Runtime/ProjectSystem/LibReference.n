using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.ProjectSystem
{
  public abstract class LibReference
  {
    public abstract Kind : string { get; }
    public abstract Path : string { get; }
    public virtual Serialize() : string { Kind + ":" + this }
    public static Deserialize(data : string) : LibReference
    {
      def index1 = data.IndexOf(':');

      when  (index1 <= 0)
        return null;
      
      def value = data.Substring(index1 + 1, data.Length - index1 - 1);
      def kind  = data.Substring(0, index1);

      match (kind)
      {
        | "File"     => FileLibReference(value)
        | "FullName" => FullNameLibReference(value)
        | "Project"  =>
          def index2 = value.IndexOf(':');
          assert(index2 != 0);
          if (index2 < 0)
            FileLibReference(value)
          else
          {
            def projectId = int.Parse(value.Substring(index2 + 1, value.Length - index2 - 1));
            def path  = value.Substring(0, index2);
            ProjectReference(projectId, path)
          }
          
        | _ => null
      }
    }
  }

  [Record]
  [StructuralEquality]
  public class ProjectReference : LibReference
  {
    public this(path : string) { this(-1, path); }
    
    public          ProjectId : int;
    public override Path      : string { get; }
    public override Kind      : string { get { "Project" } }

    public override ToString() : string { ProjectId + ":" + Path }
  }

  [Record]
  [StructuralEquality]
  public class FileLibReference : LibReference
  {
    public override Path : string { get; }
    public override Kind : string { get { "File" } }

    public override ToString() : string { Path }
  }

  [Record]
  [StructuralEquality]
  public class FullNameLibReference : LibReference
  {
    public override Kind : string { get { "FullName" } }
    
    [EqualsIgnore]
    [RecordIgnore]
    private mutable _path : string;
    
    public FullName : string { get; }

    [EqualsIgnore]
    [RecordIgnore]
    public Exception : Exception { get; protected set; }
    
    public override Path : string
    {
      get
      {
        when (_path == null)
        {
          _path = "";
          try
          {
            def asm = Assembly.ReflectionOnlyLoad(FullName); // TODO: This aproch is platform dependent! Replace with backand call!
            _path = asm.Location;
          }
          catch { | e => this.Exception = e; }
        }
          
        _path
      }
    }
    
    public override ToString() : string { FullName }
  } // class
} // namespace
