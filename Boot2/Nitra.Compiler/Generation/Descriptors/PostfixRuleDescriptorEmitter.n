using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  internal sealed class PostfixRuleDescriptorEmitter : RuleDescriptorEmitter[ExtensionRuleSymbol]
  {
    protected override BaseType  : PExpr  { get <[ Nitra.PostfixRuleDescriptor ]> }
    protected override ClassName : string { get "_N_PostfixRuleDescriptor_#" }

    public override DefineDescriptor() : void
    {
      base.DefineDescriptor();
      DefineNewParserMethod(<[ Nitra.Internal.PostfixRuleParser ]>, [<[parameter: parserData : Nitra.Internal.ExtensibleRuleParserData ]>]);

      def baseRuleExpr = MakeRuleSelector(ruleSymbol => ruleSymbol.BaseRule.GetValue(Owner.Environment.Project), _.GetRuleDescriptorRef());
      DescriptorTb.DefineConditional(<[decl: public override BaseRule : Nitra.ExtensibleRuleDescriptor { get { $baseRuleExpr } } ]>);

      def bindingPowerExpr = MakeRuleSelector(ruleSymbol => <[ $((ruleSymbol.ExtensionType :> ExtensionRuleSymbol.ExtensionRuleType.Postfix).BindingPower.Name : string) ]>);
      DescriptorTb.DefineConditional(<[decl: public override BindingPower : string { get { $bindingPowerExpr } } ]>);

      def createParseTreeExpr1 = MakeRuleSelectorForParseTree(ruleSymbol =>
        <[
          $(Owner.Environment.GetParseTreeType(ruleSymbol) : typed).__Parsed__Node(
            prefix :> $(Owner.Environment.GetParseTreeType(ruleSymbol.BaseRule.GetValue(Owner.Environment.Project)) : typed),
            prefixStartPos,
            ref pos,
            rawTreePtr,
            parseResult)
        ]>);
      DescriptorTb.DefineConditional(<[ decl:
        public override CreateParseTree(prefix : Nitra.ParseTree, prefixStartPos : int, pos : ref int, rawTreePtr : int, parseResult : Nitra.ParseResult) : Nitra.ParseTree
        {
          IgnoreParams();
          $createParseTreeExpr1
        }
      ]>);

      def createParseTreeExpr2 = MakeRuleSelectorForParseTree(ruleSymbol =>
        <[
          $(Owner.Environment.GetParseTreeType(ruleSymbol) : typed).__Parsed__Node(
            prefix :> $(Owner.Environment.GetParseTreeType(ruleSymbol.BaseRule.GetValue(Owner.Environment.Project)) : typed),
            prefixStartPos,
            startPos,
            endPos,
            recoveredSubrulesIndex,
            parseResult)
        ]>);
      DescriptorTb.DefineConditional(<[ decl:
        public override CreateParseTree(prefix : Nitra.ParseTree, prefixStartPos : int, startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : Nitra.ParseResult) : Nitra.ParseTree
        {
          IgnoreParams();
          $createParseTreeExpr2
        }
      ]>);
    }
  }
}
