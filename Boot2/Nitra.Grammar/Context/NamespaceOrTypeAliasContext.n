using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Compiler;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  public sealed class NamespaceOrTypeAliasContext : Context, INemerleEnvironmentContext
  {
    public Name  : G.Name                     { get; }
    public Path  : G.QualifiedReference       { get; }
    public Alias : NamespaceOrTypeAliasSymbol { get; }

    public this(parent : Context, name : G.Name, path : G.QualifiedReference)
    {
      base(parent);
      Name  = name;
      Path  = path;
      def p = this.GetParent.[EnteredNamespaceContext]();
      Alias = NamespaceOrTypeAliasSymbol(name, p.TryBind(path));
    }

    [Memoize]
    GetNemerleEnvironment() : GlobalEnv implements INemerleEnvironmentContext.GetNemerleEnvironment
    {
      def parentEnv = Parent.GetNemerleEnvironment();
      parentEnv.AddNamespaceAlias(Name.Value(), Path.Parts(), Path.NLocation())
    }

    public override ToString() : string
    {
      "using " + Alias.Name + " = " + Alias.RealSymbol.Map(sym => sym.FullName)
    }
  }
}
