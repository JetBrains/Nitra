using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Model;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol]
  public abstract class SpanClassSymbol : SingleDefSymbol
  {
    public override Description       : string                 { get "span class" }
    public abstract DeclaringLanguage : LanguageSymbol         { get; }
    public abstract RegularRule       : option[RegularRule]    { get; }
    public abstract Style             : SymbolRef[StyleSymbol] { get; }
    public          Matcher           : string -> bool
    {
      [Memoize] get
      {
        assert(Node != null);
        match (this.RegularRule)
        {
          | Some(rr) => rr.MakeMatcher(Node.Project)
          | None     => nullMatcher
        }
      }
    }

    private static nullMatcher : string -> bool = _ => false;
  }

  public sealed class ParsedSpanClassSymbol : SpanClassSymbol
  {
    private _node : G.LanguageMember.SpanClass;

    public this(node : G.LanguageMember.SpanClass, language : LanguageSymbol)
    {
      base(node.Name);
      _node             = node;
      DeclaringLanguage = language;
    }

    public override DeclaringLanguage : LanguageSymbol         { get; }
    public override RegularRule       : option[RegularRule]    { get { _node.RegularRule() } }
    public override Style             : SymbolRef[StyleSymbol] { get { _node.StyleRef() } }
  }
}
