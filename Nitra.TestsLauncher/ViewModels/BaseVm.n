using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nitra.ClientServer.Client;
using System;
using System.Collections.Generic;
using System.ComponentModel;

namespace Nitra.ViewModels
{
  public abstract class BaseVm : INotifyPropertyChanged
  {
    public          FullPath : string  { get; private set }
    public          Suite    : SuiteVm { get; private set }
    public abstract Name     : string  { get }
    public abstract Hint     : string  { get }

    mutable _testState  : TestState;
    mutable _isSelected : bool;
    mutable _isExpanded : bool;

    private this() { }

    protected this(suite : SuiteVm, fullPath : string)
    {
      this();
      Suite = suite;
      FullPath = fullPath;
      _isExpanded = true;

      when (Suite != null && Suite.TestState == TestState.Ignored)
        TestState = TestState.Ignored;
    }

    public IsSelected : bool
    {
      get { _isSelected }
      set
      {
        _isSelected = value;
        OnPropertyChanged("IsSelected")
      }
    }

    public IsExpanded : bool
    {
      get { _isExpanded }
      set
      {
        _isExpanded = value;
        OnPropertyChanged("IsExpanded")
      }
    }

    public TestState : TestState
    {
      get { _testState }
      protected set
      {
        unless (value == _testState)
        {
          _testState = value;
          OnPropertyChanged("TestState");
          OnPropertyChanged("DispayImage")
        }
      }
    }

    public DispayImage : string
    {
      get
      {
        match (TestState)
        {
          | TestState.Failure => @"Images/TreeIcons/failure.png"
          | TestState.Ignored => @"Images/TreeIcons/ignored.png"
          | TestState.Inconclusive => @"Images/TreeIcons/inconclusive.png"
          | TestState.Skipped => @"Images/TreeIcons/skipped.png"
          | TestState.Success => @"Images/TreeIcons/success.png"
          | _ => throw ArgumentOutOfRangeException()
        }
      }
    }

    public event PropertyChanged : PropertyChangedEventHandler;

    //[NotifyPropertyChangedInvocator]
    protected virtual OnPropertyChanged(propertyName : string): void
    {
      def h = PropertyChanged;
      unless (h == null)
        h(this, PropertyChangedEventArgs(propertyName));
    }
    
    public abstract GetAllTests() : Seq[FileVm];
  }
}