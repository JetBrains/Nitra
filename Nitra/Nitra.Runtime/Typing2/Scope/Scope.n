using Nitra.Declarations;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Runtime.Binding
{
  public abstract class Scope
  {
    public abstract BindMany[TSymbol](reference : Reference, results : ref LightList[TSymbol]) : void where TSymbol : DeclarationSymbol;
    public abstract MakeCompletionList(prefix : string) : Seq[DeclarationSymbol];

    public Bind[TSymbol](reference : Reference) : Ref[TSymbol]
      where TSymbol : DeclarationSymbol
    {
      def sym = TryBind(reference);
      SymbolUtils.ReportError(sym);
      sym
    }

    /// Если не может связать возвращает AmbiguousSymbol или UnresolvedSymbol.
    public TryBind[TSymbol](reference : Reference) : Ref[TSymbol]
      where TSymbol : DeclarationSymbol
    {
      mutable result = LightList();
      BindMany(reference, ref result);
      match (result.Count)
      {
        | 0 => Ref.Unresolved(reference.File, reference.Span, reference.Text, ResolutionSource.Bind(this))
        | 1 => Ref.Some(reference.File, reference.Span, result[0])
        | _ => Ref.Ambiguous(reference.File, reference.Span, result.ToNList(), ResolutionSource.Bind(this))
      }
    }

    public HideWith(scope : Scope) : Scope
    {
      if (scope == null)
        this
      else if (this : object == EmptyScope.Instance)
        scope
      else if (scope : object == EmptyScope.Instance)
        this
      else
        HidingScope(this, scope)
    }

    public UnionWith(scope : Scope) : Scope
    {
      match (this, scope)
      {
        | (_, null) => this
        | (EmptyScope, s)
        | (s, EmptyScope) => s
        | (UnionScope(Scopes = scopes1), UnionScope(Scopes = scopes2)) => UnionScope(scopes2.RevAppend(scopes1))
        | (UnionScope(Scopes = scopes), s)
        | (s, UnionScope(Scopes = scopes)) => UnionScope(s :: scopes)
        | _ => UnionScope([this, scope])
      }
    }
    
    public UnionWith(scopes : list[Scope]) : Scope
    {
      | null | [] => this
      | [one] => UnionWith(one)
      | _     => 
        match (this)
        {
          | EmptyScope      => UnionScope(scopes)
          | UnionScope as s => UnionScope(scopes.RevAppend(s.Scopes))
          | _               => UnionScope(this :: scopes)
        }
    }

    public static Union(scopes : list[Scope]) : Scope
    {
      | null | [] => EmptyScope.Instance
      | _ => UnionScope(scopes)
    }

    public FilterWith([NotNull] predicate : Func[DeclarationSymbol, bool]) : Scope
    {
      if (this : object == EmptyScope.Instance)
        this
      else
        FilteringScope(this, predicate)
    }
  }
}
