using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Runtime
{
  public struct FakeVoid
  {
    public static Value : FakeVoid = FakeVoid();
  }

  public abstract class AstWalkerBase[TContext]
  {
    public virtual OnEmpty(ruleInfo : SubruleInfo.Empty, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      IgnoreParams();
    }

    public virtual OnMarker(ruleInfo : SubruleInfo.Marker, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      IgnoreParams();
    }

    public virtual OnPrefixMissed(ruleParser : ExtensibleRuleParser, pos : int, parseResult : ParseResult, context : TContext) : void
    {
      IgnoreParams();
    }

    public virtual OnRegularCall(ruleInfo : SubruleInfo.RegularCall, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      IgnoreParams();
    }

    public virtual OnTokenString(ruleInfo : SubruleInfo.TokenString, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      IgnoreParams();
    }

    public virtual OnSimpleCall(ruleInfo : SubruleInfo.SimpleCall, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      mutable curTextPos = startPos;
      if (endPos < 0)
        _ = WalkSimple(curTextPos, ruleInfo.RuleParser, parseResult, context)
      else when (startPos != endPos)
      {
        curTextPos = WalkSimple(curTextPos, ruleInfo.RuleParser, parseResult, context);
        assert3(curTextPos == endPos);
      }
    }

    public virtual OnExtensibleCall(ruleInfo : SubruleInfo.ExtensibleCall, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      mutable curTextPos = startPos;
      if (endPos < 0)
        _ = WalkExtension(parseResult.TryGetAst(curTextPos, ruleInfo.RuleParser.PrefixId), curTextPos, ruleInfo.RuleParser, parseResult, context)
      else if (startPos == endPos)
        OnPrefixMissed(ruleInfo.RuleParser, startPos, parseResult, context)
      else
      {
        curTextPos = WalkExtension(parseResult.GetAst(curTextPos, ruleInfo.RuleParser.PrefixId), curTextPos, ruleInfo.RuleParser, parseResult, context);
        while (curTextPos < endPos)
          curTextPos = WalkExtension(parseResult.GetAst(curTextPos, ruleInfo.RuleParser.PostfixId), curTextPos, ruleInfo.RuleParser, parseResult, context);
        assert3(curTextPos == endPos);
      }
    }

    public virtual OnOption(ruleInfo : SubruleInfo.Option, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      mutable curTextPos = startPos;
      if (endPos < 0)
         _ = WalkSequence(parseResult.TryGetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context)
      else when (startPos != endPos)
      {
        curTextPos = WalkSequence(parseResult.GetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context);
        assert3(curTextPos == endPos);
      }
    }

    public virtual OnList(ruleInfo : SubruleInfo.List, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      mutable curTextPos = startPos;
      if (endPos < 0)
         _ = WalkSequence(parseResult.TryGetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context)
      else when (startPos != endPos && !ruleInfo.IsOptimized)
      {
        while (curTextPos < endPos)
          curTextPos = WalkSequence(parseResult.GetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context);
        assert3(curTextPos == endPos);
      }
    }

    public virtual OnListWithSeparator(ruleInfo : SubruleInfo.ListWithSeparator, startPos : int, endPos : int, parseResult : ParseResult, context : TContext) : void
    {
      mutable curTextPos = startPos;
      if (endPos < 0)
         _ = WalkSequence(parseResult.TryGetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context)
      else when (startPos != endPos)
      {
        while (true)
        {
          when (curTextPos >= endPos) break;
          curTextPos = WalkSequence(parseResult.GetAst(curTextPos, ruleInfo.Rule.Id), curTextPos, parseResult, context);

          when (curTextPos >= endPos) break;
          curTextPos = WalkSequence(parseResult.GetAst(curTextPos, ruleInfo.Separator.Id), curTextPos, parseResult, context);
        }
        assert3(curTextPos == endPos);
      }
    }

    public Walk(parseResult : ParseResult, context : TContext) : int
    {
      match (parseResult.RuleParser)
      {
        | ExtensibleRuleParser as ruleParser => WalkExtensible(parseResult.StartPos, ruleParser, parseResult, context)
        | SimpleRuleParser     as ruleParser => WalkSimple(parseResult.StartPos, ruleParser, parseResult, context)
        | _                                  => assert3(false)
      }
    }

    public virtual WalkSimple(curTextPos : int, ruleParser : SimpleRuleParser, parseResult : ParseResult, context : TContext) : int
    {
      WalkSequence(parseResult.TryGetAst(curTextPos, ruleParser.RuleId), curTextPos, parseResult, context)
    }

    public WalkExtensible(mutable curTextPos : int, ruleParser : ExtensibleRuleParser, parseResult : ParseResult, context : TContext) : int
    {
      curTextPos = WalkExtension(parseResult.TryGetAst(curTextPos, ruleParser.PrefixId), curTextPos, ruleParser, parseResult, context);
      if (curTextPos >= 0)
      {
        mutable endPos;
        do
        {
          endPos = curTextPos;
          curTextPos = WalkExtension(parseResult.TryGetAst(curTextPos, ruleParser.PostfixId), curTextPos, ruleParser, parseResult, context);
        }
        while (curTextPos >= 0);
        endPos
      }
      else
        -1
    }

    public WalkExtension(astPtr : int, mutable curTextPos : int, ruleParser : ExtensibleRuleParser, parseResult : ParseResult, context : TContext) : int
    {
      when (astPtr <= 0)
        return -1;

      def id = parseResult.ast[astPtr] & ExtensibleRuleParser.AstMask.Id;
      if (id == ruleParser.PrefixId)
      {
        mutable prefixAst = astPtr;
        def bestResult = parseResult.TryGetPrefix(ref curTextPos, ref prefixAst);

        if (bestResult > 0)
        {
          def bestPrefixAstPtr = ruleParser.BestPrefix(bestResult, parseResult);
          WalkPrefix(bestPrefixAstPtr, curTextPos, parseResult, context)
        }
        else
        {
          OnPrefixMissed(ruleParser, curTextPos, parseResult, context);
          curTextPos
        }
      }
      else
      {
        mutable postfixAst = astPtr;
        def astPtr = ruleParser.BestPostfix(parseResult.TryGetPostfix(ref curTextPos, ref postfixAst), parseResult);
        WalkPostfix(astPtr, curTextPos, parseResult, context)
      }
    }

    public virtual WalkPrefix(astPtr : int, curTextPos : int, parseResult : ParseResult, context : TContext) : int
    {
      WalkSequence(astPtr, curTextPos, parseResult, context)
    }

    public virtual WalkPostfix(astPtr : int, curTextPos : int, parseResult : ParseResult, context : TContext) : int
    {
      WalkSequence(astPtr, curTextPos, parseResult, context)
    }

    protected WalkSequence(astPtr : int, mutable curTextPos : int, parseResult : ParseResult, context : TContext) : int
    {
      when (astPtr <= 0)
        return -1;

      def state = parseResult.ast[astPtr + ExtensibleRuleParser.AstOfs.State];

      foreach (ruleInfo in parseResult.GetSequenceInfo(astPtr).SubrulesWithMarkers)
      {
        if (state >= 0 && ruleInfo.State >= state)
        {//обработка состояния облома
          match (ruleInfo)
          {
            | Marker            as ruleInfo => OnMarker           (ruleInfo, curTextPos, -1, parseResult, context)
            | RegularCall       as ruleInfo => OnRegularCall      (ruleInfo, curTextPos, -1, parseResult, context)
            | TokenString       as ruleInfo => OnTokenString      (ruleInfo, curTextPos, -1, parseResult, context)
            | SimpleCall        as ruleInfo => OnSimpleCall       (ruleInfo, curTextPos, -1, parseResult, context)
            | ExtensibleCall    as ruleInfo => OnExtensibleCall   (ruleInfo, curTextPos, -1, parseResult, context)
            | Option            as ruleInfo => OnOption           (ruleInfo, curTextPos, -1, parseResult, context)
            | List              as ruleInfo => OnList             (ruleInfo, curTextPos, -1, parseResult, context)
            | ListWithSeparator as ruleInfo => OnListWithSeparator(ruleInfo, curTextPos, -1, parseResult, context)
            | Empty             as ruleInfo => OnEmpty            (ruleInfo, curTextPos, -1, parseResult, context)
          }
          return -1;
        }
        else if (ruleInfo is Marker as ruleInfo)
          OnMarker(ruleInfo, curTextPos, curTextPos, parseResult, context);
        else
        {
          def sizePtr = astPtr + ruleInfo.Offset;
          def skipAndSize = parseResult.GetSkipAndSize2(sizePtr);
          def size = 
            if (skipAndSize >= 0)
              skipAndSize
            else
            {
              def skip = parseResult.GetSkip(sizePtr);
              curTextPos += skip;
              (~skipAndSize) - skip
            };
          def endPos = curTextPos + size;
          match (ruleInfo)
          {
            | Marker                        => assert3(false)
            | RegularCall       as ruleInfo => OnRegularCall      (ruleInfo, curTextPos, endPos, parseResult, context)
            | TokenString       as ruleInfo => OnTokenString      (ruleInfo, curTextPos, endPos, parseResult, context)
            | SimpleCall        as ruleInfo => OnSimpleCall       (ruleInfo, curTextPos, endPos, parseResult, context)
            | ExtensibleCall    as ruleInfo => OnExtensibleCall   (ruleInfo, curTextPos, endPos, parseResult, context)
            | Option            as ruleInfo => OnOption           (ruleInfo, curTextPos, endPos, parseResult, context)
            | List              as ruleInfo => OnList             (ruleInfo, curTextPos, endPos, parseResult, context)
            | ListWithSeparator as ruleInfo => OnListWithSeparator(ruleInfo, curTextPos, endPos, parseResult, context)
            | Empty             as ruleInfo => OnEmpty            (ruleInfo, curTextPos, endPos, parseResult, context)
          }
          curTextPos = endPos;
        }
      }
      curTextPos
    }
  }
}
