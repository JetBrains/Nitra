using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Declarations;
using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;
using Nitra.Runtime.Reflection;

using System;
using System.Collections.Generic;
using System.Linq;

using SCG = System.Collections.Generic;
using Symbol = Nitra.Runtime.Binding.Symbol2;

namespace Nitra
{
  public class CollectSymbolErrorsVisitor : IAstVisitor
  {
    public CompilerMessages : CompilerMessageList = CompilerMessageList();

    public Visit(parseTree : IAst) : void
    {
      parseTree.Accept(this);
    }

    public Visit(reference : Reference) : void implements IAstVisitor.Visit
    {
      when (reference.IsSymbolEvaluated)
      {
        match (reference.Symbol)
        {
          | symbol when symbol.IsResolved => ()
          | ambiguousList is IAmbiguousSymbol =>
            using (err = CompilerMessages.RootError(reference, $<#Symbol '$reference' is ambiguous.#>))
              foreach (ambiguous when ambiguous : object != ambiguousList in ambiguousList.Ambiguous with i)
                when (ambiguous.Declarations is head :: _)
                  err.Hint(head.Name, $<#Declaration $i#>);
          
          | _ is UnresolvedSymbol => CompilerMessages.Error(reference, $"Unresolved reference '$reference'");
          | _ => ()
        }
      }
    }
    
    public Visit(_ : Name) : void implements IAstVisitor.Visit
    {
    }
  }
}
