using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra
{
  [Record]
  public variant OptionAst[T] : Ast where T : IAst
  {
    | Some
      {
        private static applyItem : bool = CanApplyVisitor.[T]();

        Item : T;

        public override IsSome : bool { get true }

        public override ToNOption() : option[T] { Some(Item) }

        public override Apply(visitor : AstVisitor) : Ast
        {
          visitor.Enter(this);
          mutable result = this;
          mutable isDirty;
          when (applyItem)
          {
            def newItem = visitor.Visit(Item :> Ast) :> T;
            unless (ReferenceEquals(newItem, Item))
            {
              isDirty = true;
              result = OptionAst.Some(this.Location, newItem)
            }
          }
          visitor.Leave(this, result);
          result
        }
      }

    | None
      {
        public override IsSome : bool { get false }

        public override ToNOption() : option[T] { None() }
      }

    public override Location : Nitra.Location { get }

    public abstract IsSome : bool { get; }

    public abstract ToNOption() : option[T];
  }
}
