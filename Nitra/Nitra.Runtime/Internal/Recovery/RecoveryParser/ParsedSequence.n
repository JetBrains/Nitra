using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Extensions;

using Nitra.Runtime.Reflection;

using System;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nitra.Internal.Recovery
{
  [Record, StructuralEquality]
  public partial class ParsedSequence
  {
    [EqualsIgnore]               public         RecoveryParser  : RecoveryParser;
                                 public         StartPos        : int;
                                 public         ParsingSequence : ParsingSequence;

    [RecordIgnore, EqualsIgnore] public         Callers         : SCG.HashSet[ParseRecord]     = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public         ParsedSubrules  : SCG.HashSet[ParsedSubrule]   = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public         Ends            : Hashtable[int, TokenChanges] = Hashtable();
    [RecordIgnore, EqualsIgnore] public mutable Iteration       : int = -1;

    public IsToken : bool { get { this.ParsingSequence.IsToken }}
    
    public RuleName : string { get { this.ParsingSequence.RuleName } }

    public Add(subrule : ParsedSubrule) : void
    {
      _ = this.ParsedSubrules.Add(subrule);
    }
    
    public IsSubruleVoid(subrule : ParsedSubrule) : bool
    {
      match (ParsingSequence.States[subrule.State])
      {
        | Simple           as state => state.RuleParser.IsVoid
        | Extensible       as state => state.RuleParser.IsVoid
        | ExtensionPrefix  as state => state.RuleParser.IsVoid
        | ExtensionPostfix as state => state.RuleParser.IsVoid
        | Subsequence      as state => state.SequenceInfo.IsVoid
        | Scan                      => false
        | Predicate                 => true
      }
    }
    
    public SubruleMandatoryTokenCount(subrule : ParsedSubrule) : int
    {
      match (ParsingSequence.States[subrule.State])
      {
        | Simple           as state => state.RuleParser.SequenceInfo.MandatoryTokenCount
        | Extensible       as state => state.RuleParser.MandatoryTokenCount
        | ExtensionPrefix  as state => state.RuleParser.MandatoryTokenCount
        | ExtensionPostfix          => 0
        | Subsequence      as state => state.SequenceInfo.MandatoryTokenCount
        | Scan             as state => state.Subrule.MandatoryTokenCount
        | Predicate                 => 0
      }
    }

    public GetSequencesForSubrule(parsedSubrule : ParsedSubrule) : Seq[ParsedSequence]
    {
      def begin = parsedSubrule.Begin;
      def end   = parsedSubrule.End;
      mutable sequence;

      match (ParsingSequence.States[parsedSubrule.State])
      {
        | Simple           as state =>
          when (RecoveryParser.Sequences.TryGetValue((begin, state.RuleParser.ParsingSequence), out sequence))
          when (sequence.Ends.Contains(end))
            yield sequence;

        | Extensible       as state =>
          when (RecoveryParser.Sequences.TryGetValue((begin, state.RuleParser.ParsingSequence), out sequence))
          when (sequence.Ends.Contains(end))
            yield sequence;

        | ExtensionPrefix  as state =>
          foreach (prefixRule in state.RuleParser.PrefixRules)
            when (RecoveryParser.Sequences.TryGetValue((begin, prefixRule.ParsingSequence), out sequence))
            when (sequence.Ends.Contains(end))
              yield sequence;

        | ExtensionPostfix as state =>
          foreach (postfixRule when state.RuleParser.FirstPostfixRuleId <= postfixRule.RuleId in state.RuleParser.PostfixRules)
            when (RecoveryParser.Sequences.TryGetValue((begin, postfixRule.ParsingSequence), out sequence))
            when (sequence.Ends.Contains(end))
              yield sequence;

        | Subsequence      as state =>
          when (RecoveryParser.Sequences.TryGetValue((begin, state.Sequence), out sequence))
          when (sequence.Ends.Contains(end))
            yield sequence;

        | Scan => ()
        | Predicate => ()
      }
    }
    
    public AddCaller(caller : ParseRecord) : void
    {
      when (Callers.Add(caller))
        foreach (end in Ends.Keys)
          RecoveryParser.RecordsToComplete.Push(end, caller, this);
    }

    public AddEnd(end : int) : void
    {
      when (!Ends.Contains(end))
      {
        Ends.Add(end, TokenChanges.Fail);
        foreach (caller in Callers)
          RecoveryParser.RecordsToComplete.Push(end, caller, this);
      }
    }

    public GetNextSubrules(subrule : ParsedSubrule) : Seq[ParsedSubrule]
    {
      GetNextSubrules(subrule, ParsedSubrules)
    }

    public GetNextSubrules(subrule : ParsedSubrule, parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      def state = ParsingSequence.States[subrule.State];
      parsedSubrules.Where(next => subrule.End == next.Begin && state.IsNext(next.State) && next != subrule)
    }

    public GetPrevSubrules(subrule : ParsedSubrule) : Seq[ParsedSubrule]
    {
      GetPrevSubrules(subrule, ParsedSubrules)
    }

    public GetPrevSubrules(subrule : ParsedSubrule, parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      def state = ParsingSequence.States[subrule.State];
      parsedSubrules.Where(prev => prev.End == subrule.Begin && state.IsPrev(prev.State) && prev != subrule)
    }

    public GetFirstSubrules() : Seq[ParsedSubrule]
    {
      GetFirstSubrules(ParsedSubrules)
    }

    public GetFirstSubrules(parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => p.Begin == StartPos && IsStartSubrule(p))
    }
    
    IsStartSubrule(subrule : ParsedSubrule) : bool
    {
      when (subrule.Begin!= StartPos)
        return false;
        
      ParsingSequence.IsStartState(subrule.State)
    }
    
    public GetLastSubrules() : Seq[ParsedSubrule]
    {
      GetLastSubrules(ParsedSubrules)
    }

    public GetLastSubrules(parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => Ends.Contains(p.End) && IsEndSubrule(p))
    }

    public GetLastSubrules(end : int) : Seq[ParsedSubrule]
    {
      GetLastSubrules(ParsedSubrules, end)
    }

    public GetLastSubrules(parsedSubrules : Seq[ParsedSubrule], end : int) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => p.End == end && IsEndSubrule(p))
    }
    
    private IsEndSubrule(subrule : ParsedSubrule) : bool
    {
      ParsingSequence.States[subrule.State].Next.Exists(_ == -1)
    }

    public GetValidSubrules() : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(), ParsedSubrules)
    }

    public GetValidSubrules(end : int) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrules(ParsedSubrules, end)
    }

    public GetValidSubrules(parsedSubrules : Seq[ParsedSubrule]) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(parsedSubrules), parsedSubrules)
    }

    public GetValidSubrules(parsedSubrules : Seq[ParsedSubrule], end : int) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(parsedSubrules, end), parsedSubrules)
    }

    private GetValidSubrulesImpl(ends : Seq[ParsedSubrule], parsedSubrules : Seq[ParsedSubrule]) : SCG.SortedSet[ParsedSubrule]
    {
      def cmp  = ParsedSubrule.ComparerEndBeginIndex();
      def res  = SCG.SortedSet(cmp);
      def ends = SCG.SortedSet(ends, cmp);
      while (ends.Count > 0)
      {
        def end = ends.Max;
        _ = ends.Remove(end);
        when (res.Add(end))
        {
          foreach (prev in GetPrevSubrules(end, parsedSubrules))
            _ = ends.Add(prev);
          otherwise
          {
            assert3(true);
          }
        }
      }
      res
    }

    public override ToString() : string
    {
      def invalidState = -100;
      def invalidCurPos = -1;
      def sb = Text.StringBuilder();
      ToString(sb, invalidState, invalidCurPos);
      sb.ToString()
    }
    
    public ToString(sb : Text.StringBuilder, beforeState : int, curPos : int) : void
    {
      def startPos = this.StartPos;
      
      ParsingSequence.ToString(sb, beforeState);
        
      _ = sb.Append(", StartPos=");
        
      _ = sb.Append(startPos);
      _ = sb.Append(")");

      //when (IsSpeculative)
      //  _ = sb.Append(" !!! ");
        
      def text = this.RecoveryParser.ParseResult.Text;
          
      when (startPos <= text.Length)
      {
        _ = sb.Append("   «");
        def showLen = 30;
            
        when (startPos - showLen > 0)
          _ = sb.Append("…");

        _ = sb.Append(text.SafeLeftSubstring(startPos, showLen));
            
        _ = sb.Append("■");
            
        def middle = if (curPos >= 0) curPos - startPos else showLen;
        
        _ = sb.Append(text.SafeRightSubstring(startPos, middle));
        
        when (curPos >= 0)
          _ = sb.Append("▸");
        
        when (middle < showLen)
        _ = sb.Append(text.SafeRightSubstring(startPos + middle, showLen - middle));
              
        when (startPos + showLen <= text.Length)
          _ = sb.Append("…");
              
        _ = sb.Append("»");
      }
    }
  }
}
