using Nitra.Internal.Recovery;
using Nitra.Runtime.Internal;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Imperative.GoTo;

using System;
using System.Collections.Generic;

namespace Nitra.Internal
{
  public sealed partial class ExtensibleRuleParser
  {
    public BestPrefix(mutable bestResult : int, parseResult : ParseResult) : int
    {
      while (bestResult > 0 && (parseResult.ast[bestResult] & RawTreeMask.Flags) != RawTreeFlags.Best)
        bestResult = parseResult.ast[bestResult + RawTreeOfs.Next];
      bestResult;
    }

    public ParsePrefix(mutable curTextPos : int, text : string, parseResult : ParseResult) : int
    {
      unchecked
      {
        mutable prefixAst;
        mutable state = ExtensionRuleParserState();
        state.parseResult = parseResult;
        state.curTextPos = curTextPos;

        prefixAst = parseResult.TryGetRawTree(curTextPos, PrefixId);
        when (prefixAst > 0)
        {
          def maxFailPos = parseResult.ast[prefixAst + PrefixOfs.MaxFailPos];
          when (parseResult.MaxFailPos < maxFailPos)
            parseResult.MaxFailPos = maxFailPos;
          state.bestResult = BestPrefix(parseResult.TryGetPrefix(ref curTextPos, ref prefixAst), parseResult);
          if (state.bestResult > 0)
          {
            def astState = parseResult.ast[state.bestResult + RawTreeOfs.State];
            if (astState == ParseResult.AstParsedState)
              return curTextPos + GetPrefixSize(state.bestResult, parseResult);
            else if (astState < 0)
              assert3(false)
            else
            {
              def prefixRule = PrefixRuleParser(state.bestResult, parseResult);
              def maxFailPos = parseResult.ast[state.bestResult + RawTreeOfs.Sizes + prefixRule.SubrulesCount - 1];
              when (maxFailPos > parseResult.MaxFailPos)
                parseResult.MaxFailPos = maxFailPos;
              return -1;
            }
          }
          else if (state.bestResult < 0)
            return curTextPos;
          else
          {
            when (curTextPos > parseResult.MaxFailPos)
              parseResult.MaxFailPos = curTextPos;
            return -1; // облом разбора
          }
        }

        //нет мемоизации префикса
        prefixAst = parseResult.Allocate(PrefixOfs.NodeSize, PrefixId);
        parseResult.ast[prefixAst + PrefixOfs.Next] = parseResult.memoize[curTextPos];
        parseResult.memoize[curTextPos] = prefixAst;
        state.bestResult = 0;
        state.bestEndPos = -1;
        state.lastResult = 0;
        if (curTextPos < text.Length)
        {
          def c = text[curTextPos]; // временная переменная для отсечения правил по первой букве
          for (mutable i = PrefixRules.Length - 1; i >= 0; --i)
          {
            def prefixRule = PrefixRules[i];
            when (prefixRule.LowerBound <= c && c <= prefixRule.UpperBound)
            {
              state.newResult = -1;
              state.newEndPos = prefixRule.Parse(curTextPos, text, ref state.newResult, parseResult);
              when (state.newResult > 0)
                state.Append();
            }
          }
        }
        else
        {
          when (!CanParseEmptyString)
            return -1;
          for (mutable i = PrefixRules.Length - 1; i >= 0; --i)
          {
            def prefixRule = PrefixRules[i];
            state.newResult = -1;
            state.newEndPos = prefixRule.Parse(curTextPos, text, ref state.newResult, parseResult);
            when (state.newResult > 0)
              state.Append();
          }
        }

        parseResult.ast[prefixAst + PrefixOfs.List] = state.lastResult;

        when (state.bestResult <= 0 || state.bestEndPos < 0)// не смогли разобрать префикс
        {
          when (curTextPos > parseResult.MaxFailPos)
            parseResult.MaxFailPos = curTextPos;
          parseResult.ast[prefixAst + PrefixOfs.MaxFailPos] = parseResult.MaxFailPos;
          return -1;
        }
        parseResult.ast[prefixAst + PrefixOfs.MaxFailPos] = parseResult.MaxFailPos;
        state.bestEndPos;
      }
    }
  }
}
