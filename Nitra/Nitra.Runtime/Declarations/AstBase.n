using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;
using Nitra.Internal;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public abstract class AstBase : IAst
  {
    protected this() { }

    public          File        : File      { get; protected set; }
    public          Span        : NSpan     { get; protected set; }
    public virtual  IsAmbiguous : bool      { get { false } }
    public virtual  IsMissing   : bool      { get { false } }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      _ = context;
    }

    public virtual ResetProperties() : void
    {
    }

    public virtual IsAllPropertiesEvaluated : bool { get { true } }
    public virtual PropertiesEvalState      : int  { get { 0 } }

    public virtual ToXaml() : string
    {
      def type    = GetType();
      def color   = if (this.IsAmbiguous) "red" else "gray";
      def str     = AstUtils.XmlEscape(if (type.IsNested) type.DeclaringType.Name + "." + type.Name else type.Name);
      def typeStr = $"<Span Foreground = '$color'>$str</Span>";

      if (this : object is Declaration as decl)
      {
        def text = decl.Name.Text;
        if (!string.IsNullOrWhiteSpace(text))
          typeStr + ": " + AstUtils.XmlEscape(text)
        else
          typeStr
      }
      else
        typeStr
    }

    public virtual Accept(visitor : IAstVisitor) : void { _ = visitor }

    public override ToString() : string
    {
      match (this)
      {
        | decl is Declaration => decl.Name?.Text + ": " + this.GetType().FullName
        | _ => base.ToString()
      }
    }

    protected UpdateSpan(isSpanSet : ref bool, span : NSpan) : void
    {
      if (isSpanSet)
        Span += span
      else
      {
        Span = span;
        isSpanSet = true;
      }
    }
  }
}
