using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra
{
  public abstract class OptionParseTree[T] : ParseTree where T : IParseTree
  {
    public abstract HasValue : bool { get; }
    public abstract Value    : T    { get; }

    [Record]
    public sealed class Some : OptionParseTree[T]
    {
      private static applyItem : bool = CanApplyVisitor.[T]();

      public override Location : Nitra.Location { get }
      public override HasValue : bool           { get true }
      public override Value    : T              { get }

      public override Apply(visitor : ParseTreeVisitor) : ParseTree
      {
        visitor.Enter(this);
        mutable result = this;
        mutable isDirty;
        when (applyItem)
        {
          def newItem = visitor.Visit(Value :> ParseTree) :> T;
          unless (ReferenceEquals(newItem, Value))
          {
            isDirty = true;
            result = OptionParseTree.Some(this.Location, newItem)
          }
        }
        visitor.Leave(this, result);
        result
      }
    }

    [Record]
    public sealed class None : OptionParseTree[T]
    {
      public override Location : Nitra.Location { get }
      public override HasValue : bool           { get false }
      public override Value    : T              { get throw NotSupportedException("Option is None") }
    }
  }
}
