using Nemerle;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  internal abstract class RecoveryModeParseTreeReader[TContext, TParseTree]
  {
    public Read(parseResult : ParseResult, parsingSequence : ParsingSequence, startPos : int, endPos : int, context : TContext) : TParseTree
    {
      if (startPos == endPos)
        MakeEmptyParseTree(parseResult, startPos, context)
      else
        match (parseResult.TryGetRecoveredSequence(startPos, endPos, parsingSequence))
        {
          | null                               => ReadParseTreeInStandardMode(parseResult, startPos, endPos, context)
          | Unambiguous(recoveredSubruleIndex) => ReadParseTree(parseResult, startPos, endPos, recoveredSubruleIndex, context)
          | Ambiguous(indexes) =>
            def ambiguities = array(indexes.Length);
            foreach (recoveredSubruleIndex in indexes with i)
              ambiguities[i] = ReadParseTree(parseResult, startPos, endPos, recoveredSubruleIndex, context);
            MakeAmbiguousParseTree(ambiguities, context)
        }
    }

    protected abstract MakeEmptyParseTree(parseResult : ParseResult, pos : int, context : TContext) : TParseTree;
    protected abstract MakeAmbiguousParseTree(ambiguities : array[TParseTree], context : TContext) : TParseTree;
    protected abstract ReadParseTreeInStandardMode(parseResult : ParseResult, startPos : int, endPos : int, context : TContext) : TParseTree;
    protected abstract ReadParseTree(parseResult : ParseResult, startPos : int, endPos : int, recoveredSubruleIndex : int, context : TContext) : TParseTree;
  }
}
