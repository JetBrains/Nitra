using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Typing;

using System;
using System.IO;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Serialization
{
  public sealed class ExternalSymbolConstructorRegistry
  {
    private _readers : Dictionary[string, Action[IMetadataReader, BinaryReader]] = Dictionary();

    public Register(assembly : Assembly) : void
    {
      foreach (attr in assembly.GetCustomAttributes(typeof(ExportableSymbolsAttribute), false) :> array[ExportableSymbolsAttribute])
        foreach (symbolType in attr.SymbolTypes)
          Register(symbolType);
    }

    Register(symbolType : Type) : void
    {
      def symbolTypeId = symbolType.FullName;
      def externalSymbolTypeName = if (string.IsNullOrEmpty(symbolType.Namespace)) "External" + symbolType.Name else symbolType.Namespace + ".External" + symbolType.Name;
      def externalSymbolType     = symbolType.Assembly.GetType(externalSymbolTypeName, throwOnError = true);
      def readSymbolsMethod      = externalSymbolType.GetMethod("CreateSymbols", BindingFlags.Public | BindingFlags.Static);
      def reader = Delegate.CreateDelegate(typeof(Action[IMetadataReader, BinaryReader]), readSymbolsMethod) :> Action[IMetadataReader, BinaryReader];
      _readers.Add(symbolTypeId, reader);
    }

    public Resolve(symbolTypeId : string) : Action[IMetadataReader, BinaryReader]
    {
      _readers[symbolTypeId]
    }

    public static CreateFromAppDomainAssemblies() : ExternalSymbolConstructorRegistry
    {
      def registry = ExternalSymbolConstructorRegistry();
      foreach (assembly in AppDomain.CurrentDomain.GetAssemblies())
        registry.Register(assembly);
      registry
    }
  }
}
