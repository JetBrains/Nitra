using Nemerle;
using Nitra.Runtime;

namespace Nitra.Typing
{
  internal interface ISymbolRef
  {
    HasValue   : bool   { get; }
    Value      : Symbol { get; }
  }

  [Record]
  public variant SymbolRef[T] : Located, ISymbolRef
    where T : Symbol
  {
    | Some { Symbol : T }
    | None

    public override Location : Location { get; }
    public IsGenerated       : bool     { get; }
    public IsSome            : bool     { get { this is Some } }
    public IsNone            : bool     { get { this is None } }
    public HasValue          : bool     { get { this is Some } }
    public Value             : T
    {
      get
      {
        match (this)
        {
          | Some(symbol) => symbol
          | None         => Project.CurrentProject.FatalError(this.Location, $"Unresolved symbol $(this.GetText())", 0); //assert2(false); assert(false);
        }
      }
    }

    TypeErasedValue : Symbol implements ISymbolRef.Value { get { Value } }

    public static Create(located : Located, value : option[T], isGenerated : bool = false) : SymbolRef[T]
    {
     Create(located.Location, value, isGenerated)
    }

    public static Create(location : Location, value : option[T], isGenerated : bool = false) : SymbolRef[T]
    {
      match (value)
      {
        | Some(symbol) => SymbolRef.Some(location, isGenerated, symbol)
        | None         => SymbolRef.None(location, isGenerated)
      }
    }

    public static Create(located : Located, value : ValueOption[T], isGenerated : bool = false) : SymbolRef[T]
    {
      Create(located.Location, value, isGenerated)
    }

    public static Create(location : Location, value : ValueOption[T], isGenerated : bool = false) : SymbolRef[T]
    {
      match (value)
      {
        | VSome(symbol) => SymbolRef.Some(location, isGenerated, symbol)
        | VNone         => SymbolRef.None(location, isGenerated)
      }
    }

    public override ToString() : string
    {
      match (this)
      {
        | Some(r) => r.Name
        | None    => $"$(GetText()) (unresolved)"
      }
    }

    public Equals(other : SymbolRef[T]) : bool
    {
      match (this, other)
      {
        | (Some(thisSymbol), Some(otherSymbol)) => thisSymbol : object == otherSymbol : object
        | _ => false
      }
    }

    public override Equals(other : object) : bool
    {
      | other is SymbolRef[T] => this.Equals(other)
      | other is ISymbolRef =>
        match (this)
        {
          | Some(symbol) => other.HasValue && other.Value : object == symbol : object
          | None         => false
        }

      | _ => false
    }

    public static @==[K](self : SymbolRef[K], other : SymbolRef[T]) : bool
      where K : Symbol
    {
      match (self, other)
      {
        | (Some(selfSymbol), Some(otherSymbol)) => selfSymbol : object == otherSymbol : object
        | _ => false
      }
    }

    public override GetHashCode() : int
    {
      match (this)
      {
        | Some(symbol) => (symbol : object).GetHashCode()
        | None         => typeof(T).GetHashCode()
      }
    }

    public Map[K](mapper : T -> K) : option[K]
    {
      match (this)
      {
        | Some(symbol) => option.Some(mapper(symbol))
        | None         => option.None()
      }
    }

    public static @:(symbolRef : SymbolRef[T]) : option[T]
    {
      symbolRef.ToOption()
    }

    public ToOption() : option[T]
    {
      match (this)
      {
        | Some(symbol) => option.Some(symbol)
        | None         => option.None()
      }
    }
  }
}
