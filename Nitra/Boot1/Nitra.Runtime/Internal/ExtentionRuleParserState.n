using Nitra.Internal.Recovery;
using Nitra.Internal;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Imperative.GoTo;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Runtime.Internal
{
  public struct ExtensionRuleParserState
  {
    public mutable newResult : int;
    public mutable newEndPos : int;
    public mutable bestResult : int;
    public mutable bestEndPos : int;
    public mutable lastResult : int;
    public mutable curTextPos : int;
    public mutable parseResult : ParseResult;

    public Append() : void
    {
      assert3(newResult > 0);

      //очищаем флаги
      parseResult.rawTree[newResult] = parseResult.rawTree[newResult] & ExtensibleRuleParser.RawTreeMask.Id;

      //добавляем в список
      parseResult.rawTree[newResult + ExtensibleRuleParser.RawTreeOfs.Next] = lastResult;
      lastResult = newResult;

      // выбираем лучшее правило: побеждает то правило, у которого находится поле спарсившее больше текста
      // если оба правила имеют одинаковое кол-во полей, размеры которых идентичны, ситуация считается неоднозначностью
      when (bestResult <= 0) goto new_better;
      if   (bestEndPos < 0)  if   (newEndPos >= 0) goto new_better; else goto equal;
      else                   when (newEndPos < 0)  goto best_better;

      def newTokens  = parseResult.TokenEnumerator1.Start(newResult,  curTextPos);
      def bestTokens = parseResult.TokenEnumerator2.Start(bestResult, curTextPos);

      while (true)
      {
        def newSize = newTokens.NextTokenSize();
        def bestSize = bestTokens.NextTokenSize();
        when (newSize > bestSize) goto new_better;
        when (newSize < bestSize) goto best_better;
        when (newSize < 0)        goto equal;
      }

    label equal;//АСТ равен лучшему. Неоднозначность.
      parseResult.rawTree[newResult] = parseResult.rawTree[newResult] | ExtensibleRuleParser.RawTreeFlags.Equal;
      assert(bestEndPos == newEndPos);
      return;

    label new_better;//Новый АСТ лучше
      bestEndPos = newEndPos;
      bestResult = newResult;
      parseResult.rawTree[newResult] = parseResult.rawTree[newResult] | ExtensibleRuleParser.RawTreeFlags.Best;
      return;

    label best_better;
      return;
    }
  }
}
