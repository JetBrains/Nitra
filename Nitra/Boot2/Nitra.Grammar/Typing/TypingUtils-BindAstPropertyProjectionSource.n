using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal BindAstPropertyProjectionSource(propertyContainer : PropertyContainerSymbol, name : G.Reference, context : Context) : StructuralPropertySymbol * AstSymbol
    {
      match (TypingUtils.BindProperty(propertyContainer, name, context))
      {
        | SymbolRef.Some(StructuralPropertySymbol(Type = OptionList(Qualified as name)) as astProperty)
        | SymbolRef.Some(StructuralPropertySymbol(Type =       List(Qualified as name)) as astProperty) =>
          if (context.Bind.[AstSymbol](name) is SymbolRef.Some(baseType))
            (astProperty, baseType)
          else
            (astProperty, null)

        | SymbolRef.Some(StructuralPropertySymbol(Type = t) as astProperty) as loc =>
          context.Error(loc, $"Property '$(astProperty.Name)' has invalid type '$t'. Only list of declarations can be decomposed.");
          (astProperty, null)

        | SymbolRef.Some(_) as loc =>
          context.Error(loc, "Expected structural property in 'decompose' block.");
          (null, null)

        | SymbolRef.None => (null, null)
      }
    }
  }
}
