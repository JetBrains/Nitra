using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtendSyntaxSymbol : SingleDefSymbol, IRuleDeclarationSite, IPrecedenceSymbolOwner
  {
    public abstract DeclarationSite : IRuleDeclarationSite { get; }
    public abstract Extend          : ExtensibleRuleRef    { get; }
    public abstract IsToken         : bool                 { get; }
    public override Description     : string               { get { if (IsToken) "extend token" else "extend syntax" } }

    public GetDefaultRuleDefinitionOptions() : RuleDefinitionOptions
    {
      DeclarationSite.GetDefaultRuleDefinitionOptions()
    }
  }

  public sealed class ParsedExtendSyntaxSymbol : ExtendSyntaxSymbol
  {
    _node : G.SyntaxModuleMember.ExtendRule;

    public this(node : G.SyntaxModuleMember.ExtendRule, declarationSite : IRuleDeclarationSite, isToken : bool)
    {
      def nameLoc =
        if (node.NameOpt.HasValue)
          node.NameOpt.Value.Item1.Location
        else
          node.BaseName.LastIdentifier().Location;
      base(nameLoc);
      _node           = node;
      IsToken         = isToken;
      DeclarationSite = declarationSite;
    }

    public override Extend          : ExtensibleRuleRef    { get { _node.BaseRuleRef } }
    public override IsToken         : bool                 { get; }
    public override DeclarationSite : IRuleDeclarationSite { get; }
  }
}
