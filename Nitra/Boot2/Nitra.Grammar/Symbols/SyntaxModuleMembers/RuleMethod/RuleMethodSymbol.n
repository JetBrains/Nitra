using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing.RuleMethod
{
  [Record]
  public abstract class RuleMethodSymbol : SingleDefSymbol
  {
    public DeclaringRule : RuleSymbol
    {
      get
      {
        assert(Node != null);
        match (Node.Parent.GetSymbol.[RuleSymbol]())
        {
          | ruleSymbol :: _ => ruleSymbol
          | []              => assert(false)
        }
      }
    }
    public override Description   : string     { get { "rule method" } }

    public override ToString() : string
    {
      def cached    = match (this) { | x is ICached when x.IsCached => "[Cached] "            | _ => "" };
      def signature = match (this) { | x is ISignature              => x.Signature.ToString() | _ => "" };
      def body      = match (this) { | x is IBody                   => x.Body.ToString()      | _ => "" };
      def modifier  =
        match (this)
        {
          | RuleMethodMissingValueSymbol    => "missing "
          | OverrideSymbol   => "override "
          | _                     => ""
        };

      $"$cached$modifier$Name$signature$body"
    }
  }
}
