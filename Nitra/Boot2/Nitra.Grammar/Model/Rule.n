using Nitra.Typing;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Text;
using System.Text.RegularExpressions;

namespace Nitra.Model
{
  [Record]
  public variant Rule : Nitra.Located
  {
    public override Location  : Location { get; }
    public          Type      : RuleType;
    public          FieldName : string;

    | Sequence                { Rules     : list[Rule]; }
    | Call                    { RuleRef   : RuleRef; BindingPower : int; }
    | Repeat                  {
                                MinCount  : int;     MaxCount     : option[int]; Rule : Rule;
                                public IsSkipAnyPattern : bool { get { this.Type is RuleType.Chars } }
                              }
    | RepeatWithSeparator     { MinCount  : int;     MaxCount     : option[int]; Rule : Rule; Separator : Rule; HangingSeparator : bool; }
    //| Choice                  { rules    : list[RegularRule]; }
    //| Chars                   {
    //                            chars : list[RangeSet];
    //                            public IsString : bool { get { chars.ForAll(_.IsSingleChar) } }
    //                          }
    | String
      {
        Str       : string;
        [RecordIgnore] public Context : Context { get; }
        /// This token is important in a error recovery.
        [RecordIgnore] public IsKeyToken : bool { get; internal set; }
        /// This token can be parsed by the rule used to parse an identifier. All keywords IsKeyToken.
        [RecordIgnore] public IsKeyword : bool { get; internal set; }

        public this(loc : Nitra.Location, type : RuleType, fieldName : string, str : string, context : Context, isKeyword : bool)
        {
          this(loc, type, fieldName, str);
          this.Context    = context;
          this.IsKeyword  = isKeyword;
          this.IsKeyToken = isKeyword;
        }

        public SpanClassOpt : option[SpanClassSymbol]
        {
          [Memoize]
          get
          {
            match (Context)
            {
              | null    => None()
              | context => context.GetSpanClassForString(Location, Str)
            }
          }
        }
      }
    | Not                     { Rule      : Rule; }
    | And                     { Rule      : Rule; }

    public this(originalRule : Rule)
    {
      this.Location  = originalRule.Location;
      this.Type      = originalRule.Type;
      this.FieldName = originalRule.FieldName;
    }

    public UpdateFieldName(newFieldName : string) : Rule
    {
      if (FieldName == newFieldName)
        this
      else match (this)
      {
        | Sequence(rules)                  => Rule.Sequence(Location, Type, newFieldName, rules)
        | Call(ruleRef, bindingPower)      => Rule.Call(Location, Type, newFieldName, ruleRef, bindingPower)
        | Repeat(minCount, maxCount, rule) => Rule.Repeat(Location, Type, newFieldName, minCount, maxCount, rule)
        | RepeatWithSeparator(minCount, maxCount, rule, separator, hangingSeparator) => Rule.RepeatWithSeparator(Location, Type, newFieldName, minCount, maxCount, rule, separator, hangingSeparator)
        | String(Str = str, Context = context, IsKeyToken=isKeyToken) => Rule.String(Location, Type, newFieldName, str, context, isKeyToken)
        | Not(rule)     => Rule.Not(Location, Type, newFieldName, rule)
        | And(rule)     => Rule.And(Location, Type, newFieldName, rule)
        //| Choice(rules) => Rule.Choice(Location, Type, newFieldName, rules)
        //| Chars(chars)  => Rule.Chars(Location, Type, newFieldName, chars)
      }
    }

    public FirstCharBounds() : CharBounds
    {
      def firstCharBounds(rule : Rule, initialRange : RangeSet) : RangeSet
      {
        def loop(rules, range)
        {
          match (rules)
          {
            | [] => range
            | Sequence(rules) :: tail => loop(rules + tail, range);
            | Call(symbolRef, _) :: tail =>
              if (symbolRef is SymbolRef.Some(RuleDefSymbol as symbol))
              {
                def newRange = range.AddRange(symbol.FirstCharLowerBound, symbol.FirstCharUpperBound);
                if (symbol.CanParseEmptyString)
                  loop(tail, newRange)
                else
                  newRange
              }
              else
                loop(tail, range)

            | String(str) :: tail =>
              if (str.Length > 0)
                range.AddSingleCharRange(str[0])
              else
                loop(tail, range)

            | Repeat(minCount, _, rule)                    :: tail
            | RepeatWithSeparator(minCount, _, rule, _, _) :: tail =>
              def newRange = firstCharBounds(rule, range);
              if (minCount > 0)
                newRange
              else
                loop(tail, newRange)

            //| Choice(rules) :: tail => loop(tail, range)
            //| Chars(chars)  :: tail => loop(tail, range) // TODO: Тут надо как-то вынимать первые символы из регулярки
            | And :: tail
            | Not :: tail => loop(tail, range)
          }
        }
        loop(if (rule is Sequence(rules)) rules else [rule], initialRange)
      }
      match (firstCharBounds(this, RangeSet()).GetBounds())
      {
        | None => (char.MinValue, char.MaxValue)
        | Some(bounds) => bounds
      }
    }

    public Priority : int
    {
      get
      {
        match (this)
        {
          //| Chars                       => 5
          | String                      => 5
          | Call                        => 5
          | Repeat                      => 4
          | RepeatWithSeparator         => 4
          | Not                         => 3
          | And                         => 3
          | Sequence                    => 2
          //| Choice                      => 1
        }
      }
    }

    public override ToString() : string
    {
      def p(rule : Rule)
      {
        if (rule.Priority <= this.Priority)
          $"($rule)"
        else
          rule.ToString()
      }

      def makeCallText(name, fieldName)
      {
        if (string.IsNullOrEmpty(fieldName))
          name.ToString()
        else
          fieldName
      }
      match (this)
      {
        | Sequence(rules)                                      => $<#..$(rules; " "; p)#>
        | Call(name, 0)                                        => makeCallText(name, FieldName)
        | Call(name, bp)                                       => $<#$(makeCallText(name, FieldName)) : $bp#>
        | Repeat(0, None, rule)                                => $"$(p(rule))*"
        | Repeat(1, None, rule)                                => $"$(p(rule))+"
        | Repeat(min, None, rule)                              => $"$(p(rule)){$min,}"
        | Repeat(0, Some(1), rule)                             => $"$(p(rule))?"
        | Repeat(min, Some(max), rule) when min == max         => $"$(p(rule)){$min}"
        | Repeat(min, Some(max), rule)                         => $"$(p(rule)){$min, $max}"
        | RepeatWithSeparator(0, None, rule, separator, false) => $"($(p(rule)); $(p(separator)))*"
        | RepeatWithSeparator(0, None, rule, separator, true)  => $"($(p(rule)); $(p(separator)); ?)*"
        | RepeatWithSeparator(1, None, rule, separator, false) => $"($(p(rule)); $(p(separator)))+"
        | RepeatWithSeparator(1, None, rule, separator, true)  => $"($(p(rule)); $(p(separator)); ?)+"
        | RepeatWithSeparator                                  => assert(false);
        | String(str)                                          => $<#'$str'#>
        | Not(rule)                                            => $"!$(p(rule))"
        | And(rule)                                            => $"&$(p(rule))"
        //| Choice(rules)                                        => $<#..$(rules; " | ")#>
        //| Chars as stringRule when stringRule.IsString         =>
        //  def buffer = StringBuilder();
        //  _ = buffer.Append("''");
        //  foreach (rangeSet in stringRule.chars)
        //    _ = buffer.Append(Utils.EscapeStringChar(rangeSet.Ranges.Head.from));
        //  _ = buffer.Append("''");
        //  buffer.ToString()
        //
        //| Chars([rangeSet])                                    => $"[$rangeSet]"
        //| Chars(rangeSets)                                     =>
        //  def buffer = StringBuilder();
        //  _ = buffer.Append('(');
        //  foreach (rangeSet in rangeSets with index)
        //  {
        //    when (index > 0)
        //      _ = buffer.Append(" | ");
        //    _ = buffer.AppendRangeSet(rangeSet)
        //  }
        //  _ = buffer.Append(')');
        //  buffer.ToString()
      }
    }

    public SubRules : list[Rule]
    {
      get
      {
        match (this)
        {
          | Rule.Sequence(rules) => rules
          | _ => [this]
        }
      }
    }
  }
}
