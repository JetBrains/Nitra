using Nemerle;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nitra.Serialization
{
  public class DefaultTypeSerializationFactory : TypeSerializationFactory
  {
    public enum CollectionType
    {
      | Array
      | NemerleList
      | IList
      | ICollection
      | IEnumerable
      | ImmutableArray
    }

    public override CreateBuilder(compiler : SerializationCompiler, type : FixedType) : TypeSerializationBuilder
    {
      def typeCatalog = compiler.Manager.InternalType;
      match (type)
      {
        | Tuple(itemTypes)   => TupleTypeSerializationBuilder(compiler, type, itemTypes)
        | Array(itemType, 1)                                                       => CreateCollectionBuilder(compiler, CollectionType.Array,          type, itemType)
        | Class(ti, [itemType]) when ti.Equals(typeCatalog.Nemerle_list_tc)        => CreateCollectionBuilder(compiler, CollectionType.NemerleList,    type, itemType)
        | Class(ti, [itemType]) when ti.Equals(typeCatalog.Generic_IList_tc)       => CreateCollectionBuilder(compiler, CollectionType.IList,          type, itemType)
        | Class(ti, [itemType]) when ti.Equals(typeCatalog.Generic_ICollection_tc) => CreateCollectionBuilder(compiler, CollectionType.ICollection,    type, itemType)
        | Class(ti, [itemType]) when ti.Equals(typeCatalog.Generic_IEnumerable_tc) => CreateCollectionBuilder(compiler, CollectionType.IEnumerable,    type, itemType)
        | Class(ti, [itemType]) when EqualsOption(ti, compiler.ImmutableArrayOpt)  => CreateCollectionBuilder(compiler, CollectionType.ImmutableArray, type, itemType)
        | Class(ti, _) =>
          def parseDeclaration(currentTypeInfo : TypeInfo) : TypeSerializationBuilder
          {
            match (currentTypeInfo?.GetTydecl())
            {
              | Alias(type)      => parseDeclaration(type.TypeInfo)
              | Enum             => EnumTypeSerializationBuilder(compiler, type, currentTypeInfo.UnderlyingType)
              | Variant(options) => VariantTypeSerializationBuilder(compiler, type, options)
              | VariantOption    => VariantOptionTypeSerializationBuilder(compiler, type)
              | _                => RecordTypeSerializationBuilder(compiler, type)
            }
          }
          if (ti.IsPrimitive || ti.Equals(typeCatalog.String_tc))
            PrimitiveTypeSerializationBuilder(compiler, type, ti)
          else
            parseDeclaration(ti)

        | _ => Message.FatalError(type.Location, $"Type '$type' is not suppported in serialization compiler.")
      }
    }

    protected virtual CreateCollectionBuilder(compiler : SerializationCompiler, collectionType : CollectionType, type : FixedType, itemType : TypeVar) : TypeSerializationBuilder
    {
      CollectionTypeSerializationBuilder(compiler, type, collectionType, itemType)
    }

    private static EqualsOption(ti : TypeInfo, opt : option[TypeInfo]) : bool
    {
      if (opt is Some(x))
        ti.Equals(x)
      else
        false
    }
  }
}
