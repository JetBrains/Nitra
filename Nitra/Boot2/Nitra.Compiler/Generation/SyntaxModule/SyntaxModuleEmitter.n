using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  internal partial class SyntaxModuleEmitter
  {
    public Environment   : EmitterEnvironment { get; }
    public Env           : GlobalEnv          { get; }
    public SyntaxModule  : SyntaxModuleSymbol { get; }

    public GrammarTB               : TypeBuilder { get; }
    public GrammarImplTB           : TypeBuilder { get; }
    private GrammarDescriptorImplTB : TypeBuilder;

    private SyntaxModuleParseTreeTb        : TypeBuilder; //TODO : переместить в более подходящее место

    private _ruleParserEmitters                  : List[RuleParserEmitter] = List();
    private _ruleDescriptorEmitters              : List[IRuleDescriptorEmitter] = List();
    private _allParseTreeEmitters                : List[IParseTreeEmitter] = List();
    private _simpleParseTreeEmitters             : List[IParseTreeEmitter] = List();
    private _extensionParseTreeEmitters          : List[IParseTreeEmitter] = List();
    private _parseTreeEmitterDefineTypeFunctions : Hashtable[IRuleDeclarationSite, ClassMember.TypeDeclaration -> TypeBuilder] = Hashtable();

    public this(environment : EmitterEnvironment, syntaxModule : SyntaxModuleSymbol)
    {
      Environment       = environment;
      SyntaxModule      = syntaxModule;
      Env               = Environment.NamespaceOfSymbol(syntaxModule);
      _externalGrammars = HashSet(syntaxModule.ExternalImports);

      GrammarTB               = Env.Define(<[ decl: public partial module $(syntaxModule.Name : usesite) { } ]>);
      GrammarImplTB           = GrammarTB.DefineNestedType(<[decl: public module $(AssemblyConstants.GrammarImplName : usesite) { } ]>);
      GrammarDescriptorImplTB = GrammarTB.DefineNestedType(<[decl:
        private sealed class $("_N_GrammarDescriptor_#" : usesite) : Nitra.GrammarDescriptor
        {
          public this() {}
          public override Name        : string { get { $(syntaxModule.Name                 : string) } }
          public override FullName    : string { get { $(syntaxModule.Node.FullName        : string) } }
          public override Namespace   : string { get { $(syntaxModule.Node.Parent.FullName : string) } }
        }
      ]>);
      GrammarTB.DefineConditional(<[ decl: private static _staticDescriptor : Nitra.GrammarDescriptor = $(GrammarDescriptorImplTB.GetMemType() : typed)() ]>);
      GrammarTB.DefineConditional(<[ decl: public static StaticDescriptor : Nitra.GrammarDescriptor { get { _staticDescriptor } } ]>);

      SyntaxModuleParseTreeTb = Env.Define(<[ decl: public partial module $(syntaxModule.Name + "ParseTree" : usesite) {} ]>);

      def allRuleSymbols = SyntaxModule.GetAllRuleSymbols();

      foreach (ruleSymbol in allRuleSymbols)
      {
        | ExtensibleRuleSymbol       => ()
        | ExtensionRuleSymbol  as rs => _ruleParserEmitters.Add(ExtensionRuleParserEmitter(this, rs))
        | SimpleRuleSymbol     as rs => _ruleParserEmitters.Add(SimpleRuleParserEmitter(this, rs))
        | RegularRuleSymbol    as rs => _ruleParserEmitters.Add(RegularRuleParserEmitter(this, rs))
        | _                          => assert(false);
      }

      // TODO: Нужно собрать литералы.
      def code = List();
      foreach (e is RegularRuleParserEmitter in _ruleParserEmitters)
        code.Add(<[  _ = results.Add($(GrammarImplTB.GetMemType() : typed).$(e.MethodName : global)(pos, text)); ]>);

      GrammarDescriptorImplTB.Define(<[ decl:
        public override ParseAllGrammarTokens(pos : int, text : string, results : HashSet[int]) : void
        {
          IgnoreParams();
          ..$code;
        }
      ]>);

      mutable extensibleRuleSymbols = [];
      mutable simpleRuleSymbols     = [];
      mutable regularRuleSymbols    = [];
      mutable postfixRuleSymbols    = [];
      mutable prefixRuleSymbols     = [];
      foreach (ruleSymbol in allRuleSymbols)
      {
        | ExtensibleRuleSymbol                         as ruleSymbol => extensibleRuleSymbols ::= ruleSymbol
        | SimpleRuleSymbol                             as ruleSymbol => simpleRuleSymbols     ::= ruleSymbol
        | RegularRuleSymbol                            as ruleSymbol => regularRuleSymbols    ::= ruleSymbol
        | ExtensionRuleSymbol(ExtensionType = Postfix) as ruleSymbol => postfixRuleSymbols    ::= ruleSymbol
        | ExtensionRuleSymbol(ExtensionType = Prefix)  as ruleSymbol => prefixRuleSymbols     ::= ruleSymbol
        | _ => ()
      }
      unless (extensibleRuleSymbols.IsEmpty) _ruleDescriptorEmitters.Add(ExtensibleRuleDescriptorEmitter(this, extensibleRuleSymbols));
      unless (simpleRuleSymbols.IsEmpty)     _ruleDescriptorEmitters.Add(SimpleRuleDescriptorEmitter(this, simpleRuleSymbols));
      unless (regularRuleSymbols.IsEmpty)    _ruleDescriptorEmitters.Add(RegularRuleDescriptorEmitter(this, regularRuleSymbols));
      unless (prefixRuleSymbols.IsEmpty)     _ruleDescriptorEmitters.Add(PrefixRuleDescriptorEmitter(this, prefixRuleSymbols));
      unless (postfixRuleSymbols.IsEmpty)    _ruleDescriptorEmitters.Add(PostfixRuleDescriptorEmitter(this, postfixRuleSymbols));

      DefineBindingPowerRelations(syntaxModule);
      DefineAllPrecedences(syntaxModule);

      when (SyntaxModuleParseTreeTb != null)
      {
        foreach (ruleSymbol when ruleSymbol.Type is RuleType.Ref in SyntaxModule.Node.GetDescendants.[SyntaxRuleSymbol]())
        {
          match (ruleSymbol)
          {
            | SimpleRuleSymbol as s =>
              def emitter = SimpleRuleParseTreeEmitter(Environment, s);
              _simpleParseTreeEmitters.Add(emitter);
              _allParseTreeEmitters.Add(emitter);

            | ExtensibleRuleSymbol as s =>
              def emitter = ExtensibleRuleParseTreeEmitter(Environment, s);
              _simpleParseTreeEmitters.Add(emitter);
              _allParseTreeEmitters.Add(emitter);

            | ExtensionRuleSymbol as s =>
              def emitter = ExtensionRuleParseTreeEmitter(Environment, s);
              _extensionParseTreeEmitters.Add(emitter);
              _allParseTreeEmitters.Add(emitter);

            | _ => ()
          }
        }

        foreach (extendSyntaxSymbol in SyntaxModule.Node.GetDescendants.[ExtendSyntaxSymbol]())
        {
          def emitter = ExtendSyntaxParseTreeEmitter(extendSyntaxSymbol);
          _simpleParseTreeEmitters.Add(emitter);
          _allParseTreeEmitters.Add(emitter);
        }

        _parseTreeEmitterDefineTypeFunctions[SyntaxModule] = SyntaxModuleParseTreeTb.DefineNestedType;
        foreach (emitter when emitter.Symbol is IRuleDeclarationSite in _allParseTreeEmitters)
        {
          _parseTreeEmitterDefineTypeFunctions[emitter.Symbol :> IRuleDeclarationSite] =
            fun (decl)
            {
              DefineParseTreeTypes(emitter);
              emitter.DefineNestedType(decl)
            };
        }
      }
    }

    private GetPrecedenceSymbolOwnerReference(symbol : IPrecedenceSymbolOwner) : PExpr
    {
      match (symbol)
      {
        | extendSymbol is ExtendSyntaxSymbol =>
          if (extendSymbol.Extend is Some(symbol))
            symbol.GetRuleDescriptorRef();
          else
            PExpr.Error("ICE: Unresolved symbol in DefineBindingPowerRelations.");

        | extensibleRuleSymbol is ExtensibleRuleSymbol =>
          extensibleRuleSymbol.GetRuleDescriptorRef();

        | _ =>
            PExpr.Error("ICE: Unknown symbol in DefineBindingPowerRelations.");
      }
    }

    private DefineBindingPowerRelations(syntaxModule : SyntaxModuleSymbol) : void
    {
      def convert(symbol : IPrecedenceSymbolOwner, relations : List[string * string]) : PExpr
      {
        def symbol = GetPrecedenceSymbolOwnerReference(symbol);
        def relations = relations.Map((lo, hi) => <[($lo, $hi)]>);
        (<[ ($symbol, array[..$relations]) ]>);
      }
      def relations = syntaxModule.PrecedenceRelations.Grouping(r => (r.Symbol, (r.Low, r.Hi))).KeyValuePairs.Map(convert);
      GrammarDescriptorImplTB.DefineConditional(<[ decl: public override BindingPowerRelations : array[ExtensibleRuleDescriptor * array[string * string]] { get { array[..$relations] } } ]> );
    }

    private DefineAllPrecedences(syntaxModule : SyntaxModuleSymbol) : void
    {
      def convert(symbol : IPrecedenceSymbolOwner, allPrecedences : List[string]) : PExpr
      {
        def symbol = GetPrecedenceSymbolOwnerReference(symbol);
        def allPrecedences = allPrecedences.Map((name) => <[$name]>);
        (<[ ($symbol, array[..$allPrecedences]) ]>);
      }
      def relations = syntaxModule.AllPrecedences.Grouping(p => (p.Symbol, p.Name)).KeyValuePairs.Map(convert);
      GrammarDescriptorImplTB.DefineConditional(<[ decl: public override AllPrecedences : array[ExtensibleRuleDescriptor * array[string]] { get { array[..$relations] } } ]> );
    }

    public DefineSimpleParseTreeTypes() : void
    {
      foreach (emitter in _simpleParseTreeEmitters)
        DefineParseTreeTypes(emitter);
    }

    public DefineExtensionParseTreeTypes() : void
    {
      foreach (emitter in _extensionParseTreeEmitters)
        DefineParseTreeTypes(emitter);
    }

    public DefineParseTreeMembers() : void
    {
      foreach (emitter in _allParseTreeEmitters)
        emitter.DefineMembers();
    }

    private _externalGrammars : HashSet[SyntaxModuleSymbol];

    public CallRule(callSymbol : RuleSymbol) : void
    {
      when (callSymbol.DeclaringModule : object != SyntaxModule)
        _ = _externalGrammars.Add(callSymbol.DeclaringModule);
    }

    public DefineParserTypes() : void
    {
      foreach (emitter in _ruleParserEmitters)
        emitter.DefineParser();

      foreach (emitter in _ruleDescriptorEmitters)
        emitter.DefineDescriptor();
    }

    public DefineParserMethods() : void
    {
      foreach (emitter in _ruleParserEmitters)
        emitter.DefineParserMethods();

      when (SyntaxModule.Language is SymbolRef.Some(language))
        GrammarDescriptorImplTB.Define(<[ decl:
          public override Language : Nitra.Language
          {
            get { $(Environment.GetLanguageType(language) : typed).Instance }
          }
        ]>);

      GrammarDescriptorImplTB.DefineConditional(<[decl:
        public override Rules : array[Nitra.RuleDescriptor]
        {
          get { array[..$(_ruleDescriptorEmitters.SelectMany(_.RuleSymbols).Select(_.GetRuleDescriptorRef())) ] }
        }
      ]>);

      GrammarDescriptorImplTB.DefineConditional(<[decl:
        public override Dependencies : array[Nitra.GrammarDescriptor]
        {
          get { array[..$(_externalGrammars.Map(_.GetGrammarDescriptorRef()))] }
        }
      ]>);
    }

    public Emit() : void
    {
      foreach (emitter in _ruleParserEmitters)
        emitter.Emit();
      foreach (emitter in _ruleDescriptorEmitters)
        emitter.Emit();
      foreach (emitter in _allParseTreeEmitters)
        emitter.Emit();
      when (SyntaxModuleParseTreeTb != null)
        SyntaxModuleParseTreeTb.Compile();
      GrammarDescriptorImplTB.Compile();
      GrammarImplTB.Compile();
      GrammarTB.Compile();
    }

    private DefineParseTreeTypes(emitter : IParseTreeEmitter) : void
    {
      unless (emitter.IsTypesDefined)
        emitter.DefineTypes(_parseTreeEmitterDefineTypeFunctions[emitter.SymbolDeclarationSite]);
    }
  }
}
