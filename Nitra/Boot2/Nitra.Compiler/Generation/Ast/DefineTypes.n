using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal partial class RuleAstEmitter
  {
    public DefineTypes(defineType : ClassMember.TypeDeclaration -> TypeBuilder) : void
    {
      match (this.RuleSymbol)
      {
        | ExtensibleRuleSymbol =>
          CreateBaseAstTypeBuilder(defineType);
          CreateMissingAstTypeBuilder();
          CreateAmbiguousAstTypeBuilder();
          CreateSpliceAstTypeBuilder();

        | SimpleRuleSymbol =>
          CreateBaseAstTypeBuilder(defineType);
          CreateMissingAstTypeBuilder();
          CreateSpliceAstTypeBuilder();
          CreateSimpleAstTypeBuilders();

        | ExtensionRuleSymbol as ruleSymbol =>
          CreateExtensionAstTypeBuilders(defineType, ruleSymbol.BaseRule.Value);

        | _ => assert(false)
      }
    }

    private CreateBaseAstTypeBuilder(defineType : ClassMember.TypeDeclaration -> TypeBuilder) : void
    {
      assert(_baseTb == null);
      match (this.RuleSymbol)
      {
        | ExtensibleRuleSymbol
        | SimpleRuleSymbol     =>
          _baseTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
            defineType(<[decl:
            public new abstract partial class $(this.RuleSymbol.Name : usesite) : $(Environment.AstTypeVar : typed)
            {
            }
          ]>))

        | _ => assert2(false); assert(false)
      }
    }

    private CreateSimpleAstTypeBuilders() : void
    {
      assert(_astTb == null);
      _astTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        public new abstract partial class Ast : $(_baseTb.GetMemType() : typed)
        {
        }
      ]>));

      assert(_parsedAstTb == null);
      _parsedAstTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        private new sealed partial class __Parsed__Ast : $(_astTb.GetMemType() : typed)
        {
        }
      ]>));

      assert(_chemicalAstTb == null);
      _chemicalAstTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        private new sealed partial class __Chemical__Ast : $(_astTb.GetMemType() : typed)
        {
        }
      ]>));
    }

    private CreateExtensionAstTypeBuilders(defineType : ClassMember.TypeDeclaration -> TypeBuilder, baseRule : ExtensibleRuleSymbol) : void
    {
      assert(_astTb == null);
      _astTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        defineType(<[decl:
        public new abstract partial class $(this.RuleSymbol.Name : usesite) : $(Environment.GetAstType(baseRule) : typed)
        {
        }
      ]>));

      assert(_parsedAstTb == null);
      _parsedAstTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        defineType(<[decl:
        private new sealed partial class $("__Parsed__" + this.RuleSymbol.Name : usesite) : $(_astTb.GetMemType() : typed)
        {
        }
      ]>));

      assert(_chemicalAstTb == null);
      _chemicalAstTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        defineType(<[decl:
        private new sealed partial class $("__Chemical__" + this.RuleSymbol.Name : usesite) : $(_astTb.GetMemType() : typed)
        {
        }
      ]>));
    }

    private CreateMissingAstTypeBuilder() : void
    {
      assert(_missingTb == null);
      _missingTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        public new sealed partial class MissingAst : $(_baseTb.GetMemType() : typed)
        {
          public this(sourceProvider : Nitra.Runtime.ISourceSnapshotProvider, pos : int)
          {
            this.sourceProvider = sourceProvider;
            this.pos            = pos;
          }

          private sourceProvider : Nitra.Runtime.ISourceSnapshotProvider;
          private pos            : int;

          public override Location : Nitra.Location { get { Nitra.Location(this.sourceProvider.GetSource(), this.pos, this.pos) } }
        }
      ]>));
    }

    private CreateAmbiguousAstTypeBuilder() : void
    {
      assert(_ambiguousTb == null);
      _ambiguousTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        public new sealed partial class AmbiguousAst : $(_baseTb.GetMemType() : typed), Nitra.IAmbiguousAst
        {
          public this(sourceProvider : Nitra.Runtime.ISourceSnapshotProvider, startPos : int, endPos : int, ambiguities : array[$(_baseTb.GetMemType() : typed)])
          {
            this.sourceProvider = sourceProvider;
            this.startPos       = startPos;
            this.endPos         = endPos;
            this.Ambiguities    = ambiguities;
          }

          private sourceProvider : Nitra.Runtime.ISourceSnapshotProvider;
          private startPos       : int;
          private endPos         : int;

          public override Location       : Nitra.Location                         { get { Nitra.Location(this.sourceProvider.GetSource(), this.startPos, this.endPos) } }
          public          Ambiguities    : array[$(_baseTb.GetMemType() : typed)] { get; }
          public          RuleDescriptor : Nitra.RuleDescriptor                   { get { $(RuleSymbol.GetRuleDescriptorRef()) } }
        }
      ]>));
    }

    private CreateSpliceAstTypeBuilder() : void
    {
      assert(_spliceTb == null);
      _spliceTb = Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl:
        public new sealed partial class SpliceAst : $(_baseTb.GetMemType() : typed)
        {
          public this(sourceProvider : Nitra.Runtime.ISourceSnapshotProvider, startPos : int, endPos : int)
          {
            this.sourceProvider = sourceProvider;
            this.startPos       = startPos;
            this.endPos         = endPos;
          }

          private sourceProvider : Nitra.Runtime.ISourceSnapshotProvider;
          private startPos       : int;
          private endPos         : int;

          public override Location : Nitra.Location { get { Nitra.Location(this.sourceProvider.GetSource(), this.startPos, this.endPos) } }
        }
      ]>));
    }
  }
}
