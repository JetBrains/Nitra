using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  public class SplicableAstWalkerEmitter : IConstantEmitter
  {
    public Environment  : EmitterEnvironment { get; }
    _syntaxModuleSymbol : SyntaxModuleSymbol;
    _tb                 : TypeBuilder;

    public this(environment : EmitterEnvironment, syntaxModuleSymbol : SyntaxModuleSymbol)
    {
      assert(syntaxModuleSymbol.IsSplicable);

      Environment         = environment;
      _syntaxModuleSymbol = syntaxModuleSymbol;
      _tb                 = Environment.NamespaceOfSymbol(syntaxModuleSymbol).Define(<[ decl:
        [System.ComponentModel.BrowsableAttribute(false),
         System.ComponentModel.EditorBrowsableAttribute(System.ComponentModel.EditorBrowsableState.Never)]
        public partial module $(_syntaxModuleSymbol.Name + "AstWalker" : usesite)
        {
        }
      ]>);
    }

    [Memoize]
    public CreateConstant(name : string, value : int) : string
    {
      Util.CreateConstant(_tb, name, value)
    }

    private static IsAcceptedForAst(subrule : SubruleCompileInfo) : bool
    {
      IsAcceptedForAst(subrule.SrcRule)
    }

    private static IsAcceptedForAst(rule : Rule) : bool
    {
      match (rule.Type.ChemicalType)
      {
        | Void | StaticString => false
        | _                   => true
      }
    }

    public DefineMembers() : void
    {
      foreach (ruleSymbol in _syntaxModuleSymbol.Node.GetDescendants.[SyntaxRuleSymbol]())
      {
        | SimpleRuleSymbol(Type = Ref)                 as ruleSymbol => CompileSimpleRule(ruleSymbol)
        | ExtensionRuleSymbol(ExtensionType = Prefix)  as ruleSymbol => CompilePrefixRule(ruleSymbol)
        | ExtensionRuleSymbol(ExtensionType = Postfix) as ruleSymbol => CompilePostfixRule(ruleSymbol)
        | _ => ()
      }

      foreach (ruleSymbol in _syntaxModuleSymbol.SpliceSource.Value.Node.GetDescendants.[SyntaxRuleSymbol]())
      {
        | SimpleRuleSymbol(Type = Ref, IsToken = true)                 as ruleSymbol => CompileSimpleRule(ruleSymbol)
        | ExtensionRuleSymbol(ExtensionType = Prefix, IsToken = true)  as ruleSymbol => CompilePrefixRule(ruleSymbol)
        | ExtensionRuleSymbol(ExtensionType = Postfix, IsToken = true) as ruleSymbol => CompilePostfixRule(ruleSymbol)
        | _ => ()
      }
    }

    private CompileSimpleRule(ruleSymbol : SimpleRuleSymbol) : void
    {
      def statements = CompileRule(ruleSymbol);
      _tb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("SplicableAstWalker")]
        public $(MakeRuleWalkMethodName(ruleSymbol) : usesite)[TAst](pos : ref int, parseResult : Nitra.ParseResult, builder : Nitra.Internal.ISplicableAstBuilder[TAst]) : TAst
        {
          unchecked
          {
            def startPos   = pos;
            _ = startPos;
            def ruleParser = parseResult.RuleParser.Grammar.GetSimpleRuleParser($(ruleSymbol.GetRuleDescriptorRef())) :> $(ruleSymbol.GetParserClassRef());
            _ = ruleParser;
            def astPtr     = parseResult.TryGetAst(pos, ruleParser.RuleId);
            if (astPtr > 0)
            {
              ..$statements
            }
            else
              builder.CreateMissing(parseResult, $(ruleSymbol.GetRuleDescriptorRef()), startPos)
          }
        }
      ]>)
    }

    private CompilePrefixRule(ruleSymbol : ExtensionRuleSymbol) : void
    {
      def statements = CompileRule(ruleSymbol);
      _tb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("SplicableAstWalker")]
        public $(MakeRuleWalkMethodName(ruleSymbol) : usesite)[TAst](pos : ref int, astPtr : int, ruleParser : Nitra.Internal.ExtensionRuleParser, parseResult : ParseResult, builder : Nitra.Internal.ISplicableAstBuilder[TAst]) : TAst
        {
          unchecked
          {
            def startPos   = pos;
            _ = startPos;
            def ruleParser = ruleParser :> $(ruleSymbol.GetParserClassRef());
            _ = ruleParser;
            ..$statements;
          }
        }
      ]>)
    }

    private CompilePostfixRule(ruleSymbol : ExtensionRuleSymbol) : void
    {
      def statements = CompileRule(ruleSymbol);
      _tb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("SplicableAstWalker")]
        public $(MakeRuleWalkMethodName(ruleSymbol) : usesite)[TAst](prefix : TAst, prefixStartPos : int, pos : ref int, astPtr : int, ruleParser : Nitra.Internal.ExtensionRuleParser, parseResult : ParseResult, builder : Nitra.Internal.ISplicableAstBuilder[TAst]) : TAst
        {
          unchecked
          {
            def startPos   = prefixStartPos;
            _ = startPos;
            def ruleParser = ruleParser :> $(ruleSymbol.GetParserClassRef());
            _ = ruleParser;
            ..$statements;
          }
        }
      ]>)
    }

    public Emit() : void
    {
      _tb.Compile();
    }

    private CompileRule(ruleSymbol : SyntaxRuleSymbol) : SCG.List[PExpr]
    {
      def variableNames = SCG.Dictionary.[Rule, string](Rule.ReferenceEqualityComparer);

      when (ruleSymbol is ExtensionRuleSymbol(ExtensionType = Postfix(call, _)))
        variableNames.Add(call, "prefix");

      def astPtr = AstPointer.Existing(ruleSymbol.Sequence, this, "astPtr");

      def statements = SCG.List();
      foreach (subrule in ruleSymbol.Sequence.NonMarkerSubrules)
      {
        def parentAstPointer = AstPointerScope(astPtr);
        if (IsAcceptedForAst(subrule))
        {
          def varName = subrule.MakeVariableName("ast");
          statements.Add(<[ def $(varName : usesite) = $(GetSubruleValue(subrule, parentAstPointer)) ]>);
          variableNames.Add(subrule.SrcRule, varName);
        }
        else
          statements.Add(WalkerGenerator.Skip(subrule, parentAstPointer));
      }

      def foldVariables(rule : Rule) : PExpr
      {
        if (rule is Rule.Sequence(Rules = rules))
        {
          def members = SCG.List();
          foreach (rule when IsAcceptedForAst(rule) in rules)
            members.Add(foldVariables(rule));

          match (members.Count)
          {
            | 0 => assert3(false)
            | 1 => members[0]
            | _ => <[ builder.Tuple(parseResult, 0, 0, ..$members) ]> //TODO: set correct location
          }
        }
        else
        {
          mutable variableName;
          unless (variableNames.TryGetValue(rule, out variableName))
            assert3(false);
          <[ $(variableName : usesite) ]>
        }
      }


      def resultStatement = match (ruleSymbol)
      {
        | ExtensionRuleSymbol(SplicableType = Splice(syntaxModule)) =>
          statements.Insert(0, <[ def spliceGrammarDescriptor = $(syntaxModule.GetGrammarDescriptorRef()) ]>);

          def values = SCG.List();
          foreach (rule when IsAcceptedForAst(rule) in ruleSymbol.SubRules)
            values.Add(foldVariables(rule));

          match (values.Count)
          {
            | 0 => assert(false, "Splice rule must produce one value")
            | 1 => values[0]
            | _ => assert(false, "Splice rule produces more that one value")
          }

        | ExtensionRuleSymbol(SplicableType = OptionQuote) =>
          def values = SCG.List();
          foreach (rule when IsAcceptedForAst(rule) in ruleSymbol.SubRules)
            values.Add(foldVariables(rule));

          match (values.Count)
          {
            | 0 => <[ builder.OptimizedOption(parseResult, startPos, pos, true) ]>
            | 1 => <[ builder.OptionSome(parseResult, startPos, pos, $(values[0])) ]>
            | _ => <[ builder.OptionSome(parseResult, startPos, pos, builder.Tuple(parseResult, startPos, pos, ..$values)) ]>
          }

        | ExtensionRuleSymbol(SplicableType = Quote) =>
          def values = SCG.List();
          foreach (rule when IsAcceptedForAst(rule) in ruleSymbol.SubRules)
            values.Add(foldVariables(rule));

          match (values.Count)
          {
            | 0 => assert(false, "Quote rule must produce at least one value")
            | 1 => values[0]
            | _ => <[ builder.Tuple(parseResult, startPos, pos, ..$values) ]>
          }

        | ExtensionRuleSymbol(SplicableType = Normal, SpliceSource = Some(spliceSourceSymbol)) with astRuleSymbol = spliceSourceSymbol
        | ExtensionRuleSymbol(SplicableType = Normal, SpliceSource = None()) as astRuleSymbol
        | SimpleRuleSymbol(SpliceSource = Some(spliceSourceSymbol))                            with astRuleSymbol = spliceSourceSymbol
        | SimpleRuleSymbol(SpliceSource = None())                            as astRuleSymbol =>
          def isSyntaxAst(_ : RuleType) : bool
          {
            | Ref(ExtensibleRuleSymbol(SplicableType = Splice(item))) => isSyntaxAst(item)
            | Ref(ExtensibleRuleSymbol(SplicableType = OptionSplice))
            | Ref(ExtensibleRuleSymbol(SplicableType = LoopSplice)) => false
            | Ref => true
            | _   => false
          }

          def members = SCG.List();
          foreach (rule when IsAcceptedForAst(rule) in ruleSymbol.SubRules)
          {
            def name  = if (isSyntaxAst(rule.Type)) rule.FieldName else rule.FieldName + "Data"; // TODO: придумать более надежный способ получения имени свойства AST
            def value = foldVariables(rule);
            members.Add(<[ Nitra.Internal.SplicableAstMember($(name : string), $value) ]>);
          }

          <[ builder.SyntaxRule(parseResult, $(astRuleSymbol.GetRuleDescriptorRef()), startPos, pos, ..$members) ]>

        | _ => assert(false)
      };

      statements.Add(resultStatement);
      statements
    }

    private static IsSpliceTextRule(ruleSymbol : SyntaxRuleSymbol) : bool
    {
      ruleSymbol.Name == "Splice" && ruleSymbol.DeclaringModule.Name == "SpliceExpressions"
    }

    private GetSubruleValue(subrule : SubruleCompileInfo, parentAstPointer : AstPointerScope) : PExpr
    {
      match (subrule)
      {
        | Empty      as subrule => WalkerGenerator.Skip(subrule, parentAstPointer)
        | SimpleCall as subrule =>
          if (IsSpliceTextRule(subrule.Symbol))
            <[
              pos += $(parentAstPointer.Access().GetSkip(subrule));
              def spliceStartPos = pos;
              pos += $(parentAstPointer.Access().GetSize(subrule));
              builder.Splice(parseResult, spliceGrammarDescriptor, spliceStartPos, pos)
            ]>
          else if (subrule.Symbol.Type is RuleType.Void)
            WalkerGenerator.Skip(subrule, parentAstPointer)
          else
            <[
              pos += $(parentAstPointer.Access().GetSkip(subrule));
              $(GetRuleWalkMethodRef(subrule.Symbol))(ref pos, parseResult, builder)
            ]>

        | ExtensibleCall as subrule =>
          if (subrule.Symbol.Type is RuleType.Void)
            WalkerGenerator.Skip(subrule, parentAstPointer)
          else
            <[
              pos += $(parentAstPointer.Access().GetSkip(subrule));
              Nitra.Internal.ExtensibleRuleAstConstructor.Create(ref pos, parseResult, $(subrule.BindingPower : int), $(subrule.Symbol.GetRuleDescriptorRef()), builder);
            ]>

        | RegularCall as subrule                => WalkerGenerator.GetTokenValue(subrule, parentAstPointer, <[ builder.RegularRule(parseResult, $(subrule.Symbol.GetRuleDescriptorRef()), tokenPos, pos) ]>)
        | List as lst when lst.IsSkipAnyPattern => WalkerGenerator.GetTokenValue(subrule, parentAstPointer, <[ builder.SkipAny(parseResult, tokenPos, pos) ]>)
        | TokenString as subrule                => WalkerGenerator.Skip(subrule, parentAstPointer)
        | Option as subrule =>
          match (subrule.SrcRule.Type)
          {
            | Option(Ref(ExtensibleRuleSymbol(SplicableType = LoopSplice(loopType)))) =>
              def noValueExpr(_ : ChemicalRuleType) : PExpr
              {
                | List                       => <[ builder.List(parseResult, pos, pos, null) ]>
                | Int                        => <[ builder.OptimizedList(parseResult, pos, pos, 0) ]>
                | Tuple([items, separators]) => <[ builder.Tuple(parseResult, pos, pos, $(noValueExpr(items)), $(noValueExpr(separators))) ]>
                | x                          => assert3(false, $"'$x' is not valid loop type")
              }
              WalkerGenerator.GetOptionValue(
                subrule          = subrule,
                parentAstPointer = parentAstPointer,
                hasValue         = GetSequenceValue(subrule.Rule, "valuePointer"),
                noValue          = noValueExpr(loopType.ChemicalType))

            | Option(Ref(ExtensibleRuleSymbol(SplicableType = OptionSplice(optionType)))) =>
              def noValueExpr = match (optionType.ChemicalType)
              {
                | Option => <[ builder.OptionNone(parseResult, pos, pos) ]>
                | Bool   => <[ builder.OptimizedOption(parseResult, pos, pos, false) ]>
                | x      => assert3(false, $"'$x' is not valid option type")
              };
              WalkerGenerator.GetOptionValue(
                subrule          = subrule,
                parentAstPointer = parentAstPointer,
                hasValue         = GetSequenceValue(subrule.Rule, "valuePointer"),
                noValue          = noValueExpr)

            | _ =>
              def expr = match (subrule.SrcRule.Type.ChemicalType)
              {
                | Option =>
                  WalkerGenerator.GetOptionValue(
                    subrule          = subrule,
                    parentAstPointer = parentAstPointer,
                    hasValue         = <[ builder.OptionSome(parseResult, optionPos, pos, $(GetSequenceValue(subrule.Rule, "valuePointer"))) ]>,
                    noValue          = <[ builder.OptionNone(parseResult, optionPos, pos) ]>)

                | Bool =>
                  WalkerGenerator.GetOptionValue(
                    subrule          = subrule,
                    parentAstPointer = parentAstPointer,
                    hasValue         = <[ $(SkipSequence(subrule.Rule, "valuePointer")); builder.OptimizedOption(parseResult, optionPos, pos, true) ]>,
                    noValue          = <[ builder.OptimizedOption(parseResult, optionPos, pos, false) ]>)

                | _ => WalkerGenerator.Skip(subrule, parentAstPointer)
              };
              <[ def optionPos = pos; $expr ]>
          }

        | List as lst =>
          def expr = match (subrule.SrcRule.Type.ChemicalType)
          {
            | List =>
              WalkerGenerator.GetListValue(
                subrule          = lst,
                parentAstPointer = parentAstPointer,
                init             = <[ def itemBuffer = System.Collections.Generic.List(16) ]>,
                item             = <[ itemBuffer.Add($(GetSequenceValue(lst.Rule, "itemPointer"))) ]>,
                returnCode       = <[ builder.List(parseResult, listPos, pos, itemBuffer) ]>,
                emptyCode        = <[ builder.List(parseResult, pos, pos, null) ]>)

            | Int =>
              WalkerGenerator.GetListValue(
                subrule          = lst,
                parentAstPointer = parentAstPointer,
                init             = <[ mutable itemCount; ]>,
                item             = <[ $(SkipSequence(lst.Rule, "itemPointer")); itemCount += 1 ]>,
                returnCode       = <[ builder.OptimizedList(parseResult, listPos, pos, itemCount) ]>,
                emptyCode        = <[ builder.OptimizedList(parseResult, pos, pos, 0) ]>)

            | _ => WalkerGenerator.Skip(subrule, parentAstPointer)
          };
          <[ def listPos = pos; $expr ]>

        | ListWithSeparator as lst =>
          def (itemIsVoid, itemInit, itemBody, itemReturn, itemEmpty) =
            match (lst.Rule.SrcRule.Type.ChemicalType)
            {
              | Void         => (true, <[ () ]>, <[ () ]>, <[ () ]>, <[ () ]>)
              | StaticString =>
                (
                  false,
                  <[ def itemListPos = pos; mutable itemCount; ]>,
                  <[ $(SkipSequence(lst.Separator, "itemPointer")); itemCount += 1 ]>,
                  <[ builder.OptimizedList(parseResult, itemListPos, pos, itemCount) ]>,
                  <[ builder.OptimizedList(parseResult, pos, pos, 0) ]>
                )

              | _ =>
                (
                  false,
                  <[ def itemListPos = pos; def itemBuffer = System.Collections.Generic.List(16); ]>,
                  <[ itemBuffer.Add($(GetSequenceValue(lst.Rule, "itemPointer"))) ]>,
                  <[ builder.List(parseResult, itemListPos, pos, itemBuffer) ]>,
                  <[ builder.List(parseResult, pos, pos, null) ]>
                )
            };

          def (separatorIsVoid, separatorInit, separatorBody, separatorReturn, separatorEmpty) =
            match (lst.Separator.SrcRule.Type.ChemicalType)
            {
              | Void         => (true, <[ () ]>, <[ () ]>, <[ () ]>, <[ () ]>)
              | StaticString =>
                (
                  false,
                  <[ def separatorListPos = pos; mutable separatorCount ]>,
                  <[ $(SkipSequence(lst.Separator, "separatorPointer")); separatorCount += 1 ]>,
                  <[ builder.OptimizedList(parseResult, separatorListPos, pos, separatorCount) ]>,
                  <[ builder.OptimizedList(parseResult, pos, pos, 0) ]>
                )

              | _ =>
                (
                  false,
                  <[ def separatorListPos = pos; def separatorBuffer = System.Collections.Generic.List(16) ]>,
                  <[ separatorBuffer.Add($(GetSequenceValue(lst.Separator, "separatorPointer"))) ]>,
                  <[ builder.List(parseResult, separatorListPos, pos, separatorBuffer) ]>,
                  <[ builder.List(parseResult, pos, pos, null) ]>
                )
            };

          def expr = match (itemIsVoid, separatorIsVoid)
          {
            | (true,  true) => WalkerGenerator.Skip(subrule, parentAstPointer)
            | (false, true) =>
              WalkerGenerator.GetListWithSeparatorValue(
                subrule          = lst,
                parentAstPointer = parentAstPointer,
                init             = itemInit,
                item             = itemBody,
                separator        = SkipSequence(lst.Separator, "separatorPointer"),
                returnCode       = itemReturn,
                emptyCode        = itemEmpty);

            | (true, false) =>
              WalkerGenerator.GetListWithSeparatorValue(
                subrule          = lst,
                parentAstPointer = parentAstPointer,
                init             = separatorInit,
                item             = separatorBody,
                separator        = SkipSequence(lst.Rule, "itemPointer"),
                returnCode       = separatorReturn,
                emptyCode        = separatorEmpty);

            | (false, false) =>
              WalkerGenerator.GetListWithSeparatorValue(
                subrule          = lst,
                parentAstPointer = parentAstPointer,
                init             = <[ $itemInit; $separatorInit; ]>,
                item             = itemBody,
                separator        = separatorBody,
                returnCode       = <[ builder.Tuple(parseResult, listPos, pos, $itemReturn, $separatorReturn) ]>,
                emptyCode        = <[ builder.Tuple(parseResult, listPos, pos, $itemEmpty, $separatorEmpty) ]>);
          };
          <[ def listPos = pos; $expr ]>

        | Marker => <[ () ]>
      }
    }

    private SkipSequence(sequence : SequenceCompileInfo, name : string) : PExpr
    {
      def astPointer    = AstPointerScope(sequence, name, this);
      def statements    = SCG.List();

      foreach (subrule in sequence.NonMarkerSubrules)
        statements.Add(WalkerGenerator.Skip(subrule, astPointer));

      astPointer.Compile(<[ { ..$statements } ]>)
    }

    private GetSequenceValue(sequence : SequenceCompileInfo, name : string) : PExpr
    {
      def astPointer    = AstPointerScope(sequence, name, this);
      def statements    = SCG.List();
      def subruleValues = SCG.List();

      foreach (subrule in sequence.NonMarkerSubrules)
      {
        if (IsAcceptedForAst(subrule))
        {
          def varName = subrule.MakeVariableName("ast");
          statements.Add(<[ def $(varName : usesite) = $(GetSubruleValue(subrule, astPointer)) ]>);
          subruleValues.Add(<[ $(varName : usesite) ]>);
        }
        else
          statements.Add(WalkerGenerator.Skip(subrule, astPointer));
      }

      match (subruleValues.Count)
      {
        | 0 => ()
        | 1 => statements.Add(subruleValues[0]);
        | _ =>
          statements.Insert(0, <[ def seqPos = pos ]>);
          statements.Add(<[ builder.Tuple(parseResult, seqPos, pos, ..$subruleValues)]>);
      }

      astPointer.Compile(<[ { ..$statements } ]>)
    }

    private static MakeRuleWalkMethodName(ruleSymbol : SyntaxRuleSymbol) : string
    {
      $<#..$(ruleSymbol.ModuleRelativeName; "_")#>
    }

    private static GetRuleWalkMethodRef(ruleSymbol : SyntaxRuleSymbol) : PExpr
    {
      def syntaxModule  = ruleSymbol.DeclaringModule;
      def astWalkerName = if (syntaxModule.IsSplicable) syntaxModule.Name + "AstWalker" else "Splicable" + syntaxModule.Name + "AstWalker";
      def fullName      = syntaxModule.Node.Parent.FullNameParts + [astWalkerName, MakeRuleWalkMethodName(ruleSymbol)];
      PExprUtils.FromQualifiedIdentifier(fullName);
    }
  }
}
