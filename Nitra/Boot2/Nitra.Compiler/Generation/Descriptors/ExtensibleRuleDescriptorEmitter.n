using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  internal sealed class ExtensibleRuleDescriptorEmitter : RuleDescriptorEmitter[ExtensibleRuleSymbol]
  {
    protected override BaseType  : PExpr  { get <[ Nitra.ExtensibleRuleDescriptor ]> }
    protected override ClassName : string { get "ExtensibleRuleDescriptor" }

    public override DefineDescriptor() : void
    {
      base.DefineDescriptor();
      DefineIsStartRuleProperty();
      DefineEntranceMethod();

      if (Owner.SyntaxModule.IsSplicable)
      {
        def isSplicableExpr = MakeRuleSelector(ruleSymbol => ruleSymbol.SplicableType, t => <[ $(!(t is ExtensibleRuleSymbol.SplicableRuleType.None) : bool) ]>);
        DescriptorTb.DefineConditional(<[ decl: public override IsSplicable : bool { get { $isSplicableExpr } } ]>);
      }
      else
        DescriptorTb.DefineConditional(<[ decl: public override IsSplicable : bool { get { false } } ]>);
    }
  }
}
