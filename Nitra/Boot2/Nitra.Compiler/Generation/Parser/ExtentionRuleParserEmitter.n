using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Model;
using Nitra.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  internal sealed class ExtensionRuleParserEmitter : RuleParserEmitter
  {
    public Rule : Rule.Sequence { get; }
    private _sequence : SequenceCompileInfo.Ast;

    public this(owner : SyntaxModuleEmitter, rs : ExtensionRuleSymbol)
    {
      base(owner, rs);
      Rule = rs.Rule;
      _sequence = rs.Sequence;
    }

    [RecordIgnore] private mutable _parserTb : TypeBuilder;

    public override DefineParser() : void
    {
      assert(_parserTb == null);
      _parserTb = Owner.GrammarImplTB.DefineNestedType(<[decl:
        public class $(RuleSymbol.DecorateRuleClassName() : global) : Nitra.Internal.ExtensionRuleParser, Nitra.Internal.Recovery.IRecoveryRuleParser
        {
          public this(grammar : Nitra.CompositeGrammar)
          {
            base(grammar, $(RuleSymbol.GetRuleDescriptorRef()), $(RuleSymbol.FirstCharLowerBound), $(RuleSymbol.FirstCharUpperBound));//TODO: Fix me.
          }
        }
      ]>);
    }

    public override DefineParserMethods() : void
    {
      assert(_parserTb != null);
      DefineParse(_parserTb, _sequence);
      DefineInit(_parserTb, _sequence);
      DefineReflection(_parserTb, _sequence);
      DefineGetNextState(_parserTb, _sequence);
      DefineStartState(_parserTb, _sequence);
      DefineUpdateMandatoryTokens(_parserTb, _sequence);
      DefineIsVoid(_parserTb, _sequence);
      
      def count = _sequence.NonMarkerSubrules.Last().Offset - _sequence.NonMarkerSubrules[0].Offset + 1;
      _parserTb.Define(<[ decl: public override SubrulesCount : int { get { $count } } ]>);
    }

    [Memoize]
    public override CreateConstant(name : string, value : int) : string
    {
      Util.CreateConstant(_parserTb, name, value)
    }

    public override Emit() : void
    {
      assert(_parserTb != null);
      _parserTb.Compile();
    }
  }
}
