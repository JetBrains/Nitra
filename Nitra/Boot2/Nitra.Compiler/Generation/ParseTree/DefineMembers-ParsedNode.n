using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  partial class RuleParseTreeEmitter
  {
    protected DefineParsedNodeCtorAndFields(fields : SCG.List[FieldInfo], _parsedTb : TypeBuilder) : void
    {
      _parsedTb.DefineConditional(<[ decl: private parseResult : Nitra.ParseResult; ]>);
      _parsedTb.DefineConditional(<[ decl: private rawTreePtr  : int; ]>);
      _parsedTb.DefineConditional(<[ decl: private startPos    : int; ]>);
      _parsedTb.DefineConditional(<[ decl: private endPos      : int; ]>);

      _parsedTb.DefineConditional(<[ decl: public    override Location          : Nitra.Location                      { get { Nitra.Location(this.parseResult, this.startPos, this.endPos) } } ]>);
      _parsedTb.DefineConditional(<[ decl: public    override Span              : $(Environment.NSpanTypeVar : typed) { get { $(Environment.NSpanTypeVar : typed)(this.startPos, this.endPos) } } ]>);
      _parsedTb.DefineConditional(<[ decl: public    override File              : $(Environment.FileTypeVar : typed)  { get { this.parseResult.SourceSnapshot.File } } ]>);
      _parsedTb.DefineConditional(<[ decl: protected override PrettyPrintSource : Nitra.IPrettyPrintSource            { get { this.parseResult } } ]>);

      def rawTreePtr = RawTreePointer.Existing(this.RuleSymbol.Sequence, this, "rawTreePtr");

      def endPosExpr = this.RuleSymbol.Sequence.NonMarkerSubrules.Fold(<[ pos ]>, (subrule, acc) => <[ $acc + $(rawTreePtr.GetSize(subrule)) ]>);

      match (this.RuleSymbol)
      {
        | SimpleRuleSymbol
        | ExtensionRuleSymbol(ExtensionType = Prefix) =>
          _parsedTb.DefineConditional(<[decl:
            [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
            public this(pos : ref int, rawTreePtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.rawTreePtr  = rawTreePtr;
                this.startPos    = pos;
                if (rawTreePtr > 0)
                {
                  this.endPos = $endPosExpr;
                  pos = this.endPos;
                }
                else
                {
                  this.endPos = pos;
                }
              }
            }
          ]>);

          _parsedTb.DefineConditional(<[decl:
            [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
            public this(startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.rawTreePtr  = ~recoveredSubrulesIndex;
                this.startPos    = startPos;
                this.endPos      = endPos;
              }
            }
          ]>);

        | ExtensionRuleSymbol(ExtensionType = Postfix) as symbol =>
          _parsedTb.DefineConditional(<[ decl: private parseTreeStartPos : int; ]>);
          _parsedTb.DefineConditional(<[ decl: private prefix      : $(Environment.GetParseTreeType(symbol.BaseRule.GetValue(Environment.Project)) : typed); ]>);
          _parsedTb.DefineConditional(<[ decl:
            [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
            public this(prefix : $(Environment.GetParseTreeType(symbol.BaseRule.GetValue(Environment.Project)) : typed), prefixStartPos : int, pos : ref int, rawTreePtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult       = parseResult;
                this.rawTreePtr        = rawTreePtr;
                this.startPos          = prefixStartPos;
                this.parseTreeStartPos = pos;
                this.prefix            = prefix;
                if (rawTreePtr > 0)
                {
                  this.endPos = $endPosExpr;
                  pos = this.endPos;
                }
                else
                {
                  this.endPos = pos;
                }
              }
            }
          ]>);

          _parsedTb.DefineConditional(<[ decl:
            [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
            public this(prefix : $(Environment.GetParseTreeType(symbol.BaseRule.GetValue(Environment.Project)) : typed), prefixStartPos : int, startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult       = parseResult;
                this.rawTreePtr        = ~recoveredSubrulesIndex;
                this.startPos          = prefixStartPos;
                this.endPos            = endPos;
                this.parseTreeStartPos = startPos;
                this.prefix            = prefix;
              }
            }
          ]>);

        | _ => assert(false)
      }

      def getStartPos() : PExpr
      {
        match (this.RuleSymbol)
        {
          | SimpleRuleSymbol
          | ExtensionRuleSymbol(ExtensionType = Prefix)  => <[ this.startPos ]>
          | ExtensionRuleSymbol(ExtensionType = Postfix) => <[ this.parseTreeStartPos ]>
          | _ => assert(false)
        }
      }

      foreach (field in fields)
      {
        mutable needCaching     : bool;
        mutable needPos         : bool;

        def analyze(_ : FieldExpr) : void
        {
          | Prefix                       => ()
          | Tuple(items)                 => needPos = true; foreach (expr in items) analyze(expr);
          | Chars                        => needPos = true;
          | Other                        => needPos = true; needCaching = true;
        }

        analyze(field.Expr);

        def statementsStandardMode = SCG.List();
        when (needPos)
          statementsStandardMode.Add(<[ mutable pos = $(getStartPos()); ]>);

        def compileStandardMode(_ : FieldExpr) : PExpr
        {
          | Prefix(expr)
          | Chars(ExprStandardMode = expr)
          | Other(ExprStandardMode = expr) => expr
          | Tuple(items)   =>
            def tupleCode = SCG.List();
            def tupleArgs = SCG.List();
            foreach (item in items)
            {
              def name = Util.tmpname("parseTree");
              tupleCode.Add(<[ def $(name : usesite) = $(compileStandardMode(item)) ]>);
              tupleArgs.Add(<[ $(name : usesite) ]>);
            }
            <[
              def sequenceStartPos = pos;
              ..$tupleCode;
              Nitra.ConcreteSequenceParseTree(Nitra.Location(parseResult, sequenceStartPos, pos), ..$tupleArgs)
            ]>
        }
        statementsStandardMode.Add(compileStandardMode(field.Expr));

        def compileRecoveryMode(_ : FieldExpr) : PExpr
        {
          | Prefix(expr)
          | Chars(ExprRecoveryMode = expr)
          | Other(ExprRecoveryMode = expr) => expr
          | Tuple(items)   =>
            def tupleCode = SCG.List();
            def tupleArgs = SCG.List();
            foreach (item in items)
            {
              def name   = Util.tmpname("parseTree");
              def isSpan = item is FieldExpr.Chars;
              tupleCode.Add(<[ def $(name : usesite) = $(compileRecoveryMode(item)) ]>);
              tupleArgs.Add(name, isSpan);
            }
            def startPos = match(tupleArgs[0])
            {
              | (name, true)  => <[ $(name : usesite).StartPos ]>;
              | (name, false) => <[ $(name : usesite).Span.StartPos ]>;
            };
            def endPos = match (tupleArgs[tupleArgs.Count - 1])
            {
              | (name, true) => <[ $(name : usesite).EndPos ]>;
              | (name, false) => <[ $(name : usesite).Span.EndPos ]>;
            };
            <[
              ..$tupleCode;
              Nitra.ConcreteSequenceParseTree(Nitra.Location(parseResult, $startPos, $endPos), ..$(tupleArgs.MapToList((name, _) => <[ $(name : usesite) ]>)))
            ]>
        }
        def statementsRecoveryMode = compileRecoveryMode(field.Expr);

        def code = <[ if (this.rawTreePtr > 0) { ..$statementsStandardMode } else $statementsRecoveryMode ]>;

        def codeWithCaching =
          if (needCaching)
          {
            def backingFieldName = Util.tmpname(field.Name);
            _parsedTb.DefineConditional(<[ decl: private mutable $(backingFieldName : usesite) : $(field.Type : typed); ]>);
            assert(!field.Type.IsValueType);
            <[
              when ($(backingFieldName : usesite) : object == null)
              {
                $(backingFieldName : usesite) = $code;
              }
              $(backingFieldName : usesite)
            ]>
          }
          else code;

        _parsedTb.DefineConditional(<[ decl:
          public override $(field.Name : usesite) : $(field.Type : typed)
          {
            get { unchecked ($codeWithCaching) }
          }
        ]>);
      }

      _parsedTb.DefineConditional(<[ decl:
        protected override GetSourceSnapshotProvider() : Nitra.Runtime.ISourceSnapshotProvider
        {
          this.parseResult
        }
      ]>);
    }
  }
}
