using Nemerle;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra
{
  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Assembly)]
  public macro NitraSource(path : string)
  {
    def manager = Nemerle.Macros.Manager();
    manager.MacroColors.InGlobalColor(NitraSourceImpl.Instance(path, manager).BeforeInheritance);
  }

  [MacroUsage(MacroPhase.BeforeTypedMembers, MacroTargets.Assembly)]
  public macro NitraSource(path : string)
  {
    def manager = Nemerle.Macros.Manager();
    manager.MacroColors.InGlobalColor(NitraSourceImpl.Instance(path, manager).BeforeTypedMembers);
  }

  [MacroUsage(MacroPhase.WithTypedMembers, MacroTargets.Assembly)]
  public macro NitraSource(path : string)
  {
    def manager = Nemerle.Macros.Manager();
    manager.MacroColors.InGlobalColor(NitraSourceImpl.Instance(path, manager).WithTypedMembers);
  }

  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Class)]
  public macro ExportableSymbol(tb : TypeBuilder, externalSymbolTypeName : PExpr = null)
  {
    tb.AddImplementedInterface(<[ Nitra.Serialization.IExportableSymbol ]>);
    def externalSymbolType =
      if (externalSymbolTypeName == null)
        None()
      else match (TypeVarEnv(tb.Manager).BindFixedType(tb.GlobalEnv, tb, externalSymbolTypeName, false))
      {
        | FixedType.Class(TypeBuilder as externalSymbolTb, []) =>
          externalSymbolTb.AddImplementedInterface(<[ Nitra.Serialization.IImportableSymbol ]>);
          externalSymbolTb.DisableImplicitConstructor();
          Some(externalSymbolTb)

        | _ =>
          Message.Error("Invalid external symbol type specified");
          None()
      };
    NitraSourceImpl.AddExportableSymbol(tb, externalSymbolType);
  }

  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Class)]
  public macro ChemicalSymbol(tb : TypeBuilder, chemicalSymbolTypeName : PExpr = null)
  {
    def chemicalSymbolType =
      if (chemicalSymbolTypeName == null)
        None()
      else match (TypeVarEnv(tb.Manager).BindFixedType(tb.GlobalEnv, tb, chemicalSymbolTypeName, false))
      {
        | FixedType.Class(TypeBuilder as chemicalSymbolTb, []) =>
          chemicalSymbolTb.DisableImplicitConstructor();
          Some(chemicalSymbolTb)

        | _ =>
          Message.Error("Invalid chemical symbol type specified");
          None()
      };
    NitraSourceImpl.AddChemicalSymbol(tb, chemicalSymbolType);
  }

  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Assembly)]
  public macro MakeSplicable(params syntaxModules : array[PExpr])
  {
    def makeName(expr : PExpr, tail : list[string]) : list[string]
    {
      match (expr)
      {
        | <[ $(n : name) ]>    => n.Id :: tail
        | <[ $p.$(n : name) ]> => makeName(p, n.Id :: tail)
        | _ => Message.FatalError(expr.Location, "qualified syntax module name expected")
      }
    }

    def manager = Nemerle.Macros.Manager();
    foreach (x in syntaxModules)
    {
      def name = makeName(x, []);
      NitraSourceImpl.AddSplicableSyntaxModule(manager, name)
    }
  }
}
