using Nemerle.Collections;
using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  public sealed partial class ExtensibleRuleParser
  {
    public ParserData         : ExtensibleRuleParserData;
    public BindingPower       : int;
    public PrefixId           : int;
    public PostfixId          : int;

    public FirstPostfixRule   : int;
    public FirstPostfixRuleId : int;
    public PrefixRules        : array[PrefixRuleParser];
    public PostfixRules       : array[PostfixRuleParser];
    public PrefixOffset       : int;
    public PostfixOffset      : int;

    public          MandatoryTokenCount              : int                               { get { ParserData.MandatoryTokenCount } }
    public override SequenceInfo                     : SequenceInfo                      { get { null } }
    public override ParsingSequence                  : ParsingSequence                   { get; internal set; }
    public override CanParseEmptyString              : bool                              { get { MandatoryTokenCount == 0 } }
    internal        DynamicExtensibleParsingSequence : ParsingSequence.DynamicExtensible { get; set; }
    internal        IsDynamicExtensible              : bool                              { get; }

    public this(parserData : ExtensibleRuleParserData, bindingPower : int)
    {
      base(parserData.Grammar, parserData.Descriptor);
      ParserData          = parserData;
      BindingPower        = bindingPower;
      PrefixId            = parserData.PrefixId;
      PostfixId           = parserData.PostfixId;
      PrefixRules         = parserData.PrefixParsers;
      PostfixRules        = parserData.PostfixParsers;
      IsDynamicExtensible = parserData.IsDynamicExtensible;
      FirstPostfixRule = 0;
      while (FirstPostfixRule < PostfixRules.Length && bindingPower >= PostfixRules[FirstPostfixRule].BindingPower)
        ++FirstPostfixRule;
      if (PostfixRules.Length > 0)
      {
        if (FirstPostfixRule == PostfixRules.Length)
          FirstPostfixRuleId = int.MaxValue;
        else
          FirstPostfixRuleId = PostfixRules[FirstPostfixRule].RuleId;
      }
      else
        FirstPostfixRuleId = int.MaxValue;
      if (PrefixRules.Length > 0)  PrefixOffset   = PrefixRules[0].RuleId;  else PrefixOffset   = 0;
      if (PostfixRules.Length > 0) PostfixOffset  = PostfixRules[0].RuleId; else PostfixOffset  = 0;
    }

    public IsExtension(parser : object) : bool
    {
      match (parser)
      {
        | PrefixRuleParser  as parser => parser.ParserData.Descriptor : object == Descriptor
        | PostfixRuleParser as parser => parser.ParserData.Descriptor : object == Descriptor && parser.BindingPower >= BindingPower
        | _ => assert3(false)
      }
    }

    public IsExtension(parsingSequencer : ParsingSequence) : bool
    {
      PrefixRules.Exists(prefix => prefix.ParsingSequence : object == parsingSequencer) || PostfixRules.Exists(postfix => postfix.ParsingSequence : object == parsingSequencer)
    }
  }
}
