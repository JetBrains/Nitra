using Nemerle.Collections;
using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  public sealed partial class ExtensibleRuleParser
  {
    public ParserData         : ExtensibleRuleParserData;
    public BindingPower       : int;
    public PrefixId           : int;
    public PostfixId          : int;

    public FirstPostfixRule   : int;
    public FirstPostfixRuleId : int;
    public PrefixRules        : array[ExtensionRuleParser];
    public PostfixRules       : array[ExtensionRuleParser];
    public PrefixOffset       : int;
    public PostfixOffset      : int;

    public override StartRuleId : int { get { PrefixId } }

    public MandatoryTokenCount : int { get; internal set; }
    public ParsingSequence     : ParsingSequence { get; internal set; }

    public this(parserData : ExtensibleRuleParserData, bindingPower : int)
    {
      base(parserData.Grammar, parserData.Descriptor);
      ParserData       = parserData;
      BindingPower     = bindingPower;
      PrefixId         = parserData.PrefixId;
      PostfixId        = parserData.PostfixId;
      PrefixRules      = parserData.PrefixParsers;
      PostfixRules     = parserData.PostfixParsers;
      FirstPostfixRule = 0;
      def postfixRules = parserData.PostfixDescriptors;
      while (FirstPostfixRule < postfixRules.Length && bindingPower >= postfixRules[FirstPostfixRule].BindingPower)
        ++FirstPostfixRule;
      if (PostfixRules.Length > 0)
      {
        if (FirstPostfixRule == PostfixRules.Length)
          FirstPostfixRuleId = int.MaxValue;
        else
          FirstPostfixRuleId = PostfixRules[FirstPostfixRule].RuleId;
      }
      else
        FirstPostfixRuleId = int.MaxValue;
      if (PrefixRules.Length > 0)  PrefixOffset   = PrefixRules[0].RuleId;  else PrefixOffset   = 0;
      if (PostfixRules.Length > 0) PostfixOffset  = PostfixRules[0].RuleId; else PostfixOffset  = 0;
    }

    public IsExtension(parser : object) : bool
    {
      match ((parser :> ExtensionRuleParser).Descriptor)
      {
        | PrefixRuleDescriptor  as descriptor => descriptor.BaseRule : object == Descriptor
        | PostfixRuleDescriptor as descriptor => descriptor.BaseRule : object == Descriptor && descriptor.BindingPower >= BindingPower
        | _ => assert3(false)
      }
    }

    public IsExtension(parsingSequencer : ParsingSequence) : bool
    {
      PrefixRules.Exists(prefix => prefix.ParsingSequence : object == parsingSequencer) || PostfixRules.Exists(postfix => postfix.ParsingSequence : object == parsingSequencer)
    }
  }
}
