using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Threading;

namespace Nitra.Typing
{
  public abstract class Symbol : IEquatable[Symbol]
  {
    public Id         : int  { get; }
    [RecordIgnore]
    public AssemblyId : byte { get; protected set; }
    private static mutable _next : int = 0;

    public this(name : string)
    {
      Name = name;
      Id = Interlocked.Increment(ref _next);
    }

    public abstract Locations     : list[Location] { get; }
    public abstract FirstLocation : Location       { get; }
    public abstract Description   : string         { get; }

    public FullName : string { get { if (Node != null) Node.FullName else Name } }

    public Name : string        { get; }
    public Node : HierarchyNode { get; internal set; }

    public override ToString() : string
    {
      def parent = if (Node == null) "not attached to tree" else if (Node.Parent == null) "<Root>" else Node.Parent.FullName;
      $<#$Name ($Description) Parent: '$parent'#>
    }

    public static @==(x : Symbol, y : Symbol) : bool { x : object == y }
    public static @!=(x : Symbol, y : Symbol) : bool { x : object != y }

    [OverrideObjectEquals]
    public Equals(other : Symbol) : bool
      implements IEquatable[Symbol].Equals
    {
      other : object == this
    }

    public override GetHashCode() : int { Id }
  }
}
