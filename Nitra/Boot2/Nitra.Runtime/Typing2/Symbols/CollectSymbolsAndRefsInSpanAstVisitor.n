using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public class CollectSymbolsAndRefsInSpanAstVisitor : IAstVisitor
  {
    private         _span : NSpan;

    public Refs  : List[IRef] { get; private set; default List(); }
    public Names : List[Name] { get; private set; default List(); }

    public this(span : NSpan) { _span = span; }

    public Visit(parseTree : IAst) : void
    {
      when (parseTree.Span.IntersectsWith(_span))
        parseTree.Accept(this);
    }

    public Visit(name : Name) : void implements IAstVisitor.Visit
    {
      def span = name.Span;

      when (!span.IntersectsWith(_span) || !name.IsSymbolEvaluated)
        return;

      Names.Add(name);
    }

    public Visit(reference : Reference) : void implements IAstVisitor.Visit
    {
      def span = reference.Span;

      when (!span.IntersectsWith(_span) || !reference.IsRefEvaluated)
        return;

      Refs.Add(reference.Ref);
    }

    public Visit(r : IRef) : void
    {
      _ = r;
    }
  }
}
