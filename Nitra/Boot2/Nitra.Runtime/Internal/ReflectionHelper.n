using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Internal
{
  internal module ReflectionHelper
  {
    public GetBaseAstType(ruleDescriptor : RuleDescriptor) : Type
    {
      | SimpleRuleDescriptor
      | ExtensibleRuleDescriptor =>
        def astTypeName = ruleDescriptor.Grammar.FullName + "Ast+" + ruleDescriptor.Name.Replace('.', '+');
        def astType     = ruleDescriptor.Grammar.GetType().Assembly.GetType(astTypeName);
        assert2(astType != null);
        astType

      | _ => assert3(false)
    }

    public GetParsedAstType(ruleDescriptor : RuleDescriptor) : Type
    {
      | SimpleRuleDescriptor =>
        def parsedAstTypeName = ruleDescriptor.Grammar.FullName + "Ast+" + ruleDescriptor.Name.Replace('.', '+') + "+__Parsed__Ast";
        def parsedAstType     = ruleDescriptor.Grammar.GetType().Assembly.GetType(parsedAstTypeName);
        assert2(parsedAstType != null);
        parsedAstType

      | ExtensionRuleDescriptor =>
        def nameParts = ruleDescriptor.Name.Split(array['.']);
        nameParts[nameParts.Length - 1] = "__Parsed__" + nameParts[nameParts.Length - 1];
        def parsedAstTypeName = ruleDescriptor.Grammar.FullName + "Ast+" + string.Join("+", nameParts);
        def parsedAstType     = ruleDescriptor.Grammar.GetType().Assembly.GetType(parsedAstTypeName);
        assert2(parsedAstType != null);
        parsedAstType

      | _ => assert3(false)
    }
  }
}
