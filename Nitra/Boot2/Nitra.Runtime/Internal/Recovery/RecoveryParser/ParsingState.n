using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Extensions;

using Nitra.Runtime.Reflection;

using System;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nitra.Internal.Recovery
{
  [Record]
  public variant ParsingState
  {
    [RecordIgnore] public Id                    : int                          { get; internal set; }
    [RecordIgnore] public Next                  : array[int]                   { get; internal set; }
    [RecordIgnore] public Prev                  : array[int]                   { get; internal set; }
    [RecordIgnore] public IsStart               : bool                         { get; internal set; }
    [RecordIgnore] public Owner                 : ParsingSequence              { get; internal set; }
                   public SubruleInfo           : SubruleInfo                  { get; }
                   public CanConsumeErrorTokens : bool                         { get; }

    [RecordIgnore]
    public FirstTokens : SCG.HashSet[TokenParser] = SCG.HashSet();
    
    | Simple                { RuleParser    : SimpleRuleParser; }
    | DynamicExtensible     { Sequence      : ParsingSequence; }
    | DynamicExtensibleItem { RuleParser    : ExtensibleRuleParser; }
    | Extensible            { RuleParser    : ExtensibleRuleParser; }
    | ExtensionPrefix       { RuleParser    : ExtensibleRuleParser; }
    | ExtensionPostfix      { RuleParser    : ExtensibleRuleParser; }
    | Subsequence           { Sequence      : ParsingSequence; SequenceInfo : SequenceInfo; }
    | List                  { Sequence      : ParsingSequence; }
    | ListWithSeparator     { Sequence      : ParsingSequence; }
    | Scan                  { }
    | Predicate             { HeadPredicate : SubruleInfo.PredicateDelegate; }

    public IsNullable : bool
    {
      get
      {
        match (this)
        {
          | Simple                as state => state.RuleParser.ParsingSequence.IsNullable
          | Extensible                     => false
          | ExtensionPrefix                => false
          | ExtensionPostfix               => true
          | Subsequence           as state => state.Sequence.IsNullable
          | List                  as state => state.SubruleInfo.MandatoryTokenCount == 0
          | ListWithSeparator     as state => state.SubruleInfo.MandatoryTokenCount == 0
          | Scan                  as state => if (state.SubruleInfo == null) false else state.SubruleInfo.CanParseEmptyString
          | Predicate                      => false
          | DynamicExtensible              => false
          | DynamicExtensibleItem          => false
        }
      }
    }

    public MandatoryTokenCount : int
    {
      get
      {
        match (this)
        {
          | Simple                as state => state.RuleParser.SequenceInfo.MandatoryTokenCount
          | Extensible            as state => state.RuleParser.MandatoryTokenCount
          | ExtensionPrefix       as state => state.RuleParser.MandatoryTokenCount
          | ExtensionPostfix               => 0
          | Subsequence           as state => state.SequenceInfo.MandatoryTokenCount
          | List                  as state => state.SubruleInfo.MandatoryTokenCount
          | ListWithSeparator     as state => state.SubruleInfo.MandatoryTokenCount
          | Scan                  as state => state.SubruleInfo.MandatoryTokenCount
          | Predicate                      => 0
          | DynamicExtensible     as state => state.Sequence.States[0].MandatoryTokenCount;
          | DynamicExtensibleItem as state => state.RuleParser.MandatoryTokenCount
        }
      }
    }

    public CalleeSequences : Seq[ParsingSequence]
    {
      get
      {
        match (this)
        {
          | Scan                           => ()
          | Predicate                      => ()
          | Simple                as state => yield state.RuleParser.ParsingSequence
          | List                  as state => yield state.Sequence
          | ListWithSeparator     as state => yield state.Sequence
          | Subsequence           as state => yield state.Sequence
          | Extensible            as state => yield state.RuleParser.ParsingSequence
          | ExtensionPrefix       as state =>
            foreach (parser in state.RuleParser.PrefixRules)
              yield parser.ParsingSequence;

          | ExtensionPostfix      as state =>
            foreach (parser in state.RuleParser.PostfixRules)
              when (state.RuleParser.FirstPostfixRuleId <= parser.RuleId)
                yield parser.ParsingSequence;

          | DynamicExtensible     as state => yield state.Sequence
          | DynamicExtensibleItem as state => yield state.RuleParser.ParsingSequence
        }
      }
    }

    public IsToken : bool
    {
      get
      {
        match (this)
        {
          | Simple                as state => state.RuleParser.IsTokenRule
          | Extensible            as state => state.RuleParser.IsTokenRule
          | ExtensionPrefix       as state => state.RuleParser.IsTokenRule
          | ExtensionPostfix      as state => state.RuleParser.IsTokenRule
          | DynamicExtensibleItem as state => state.RuleParser.IsTokenRule
          | DynamicExtensible              => false
          | Subsequence                    => false
          | List                           => false
          | ListWithSeparator              => false
          | Predicate                      => false
          | Scan                           => true
        }
      }
    }

    public IsVoid : bool
    {
      get
      {
        match (this)
        {
          | Simple                as state => state.RuleParser.IsVoid
          | Extensible            as state => state.RuleParser.IsVoid
          | ExtensionPrefix       as state => state.RuleParser.IsVoid
          | ExtensionPostfix      as state => state.RuleParser.IsVoid
          | DynamicExtensibleItem as state => state.RuleParser.IsTokenRule
          | DynamicExtensible              => false
          | Subsequence           as state => state.SequenceInfo.IsVoid
          | List                  as state => state.SubruleInfo.IsVoid
          | ListWithSeparator     as state => state.SubruleInfo.IsVoid
          | Scan                  as state => state.SubruleInfo.IsVoid
          | Predicate             as state => state.SubruleInfo.IsVoid
        }
      }
    }

    public Description : string
    {
      get
      {
        match (this)
        {
          | Simple            as state => state.RuleParser.Descriptor.Name
          | DynamicExtensible          => this.SubruleInfo.ToString()
          | DynamicExtensibleItem      => "DynamicExtensibleItem"
          | Extensible        as state => state.RuleParser.Descriptor.Name + if (state.RuleParser.BindingPower > 0) " ^ " + state.RuleParser.BindingPower else null
          | ExtensionPrefix   as state => state.RuleParser.Descriptor.Name + " Prefix"
          | ExtensionPostfix  as state => state.RuleParser.Descriptor.Name + " Postfix"
          | List                       => this.SubruleInfo.ToString()
          | ListWithSeparator          => this.SubruleInfo.ToString()
          | Subsequence       as state => state.SequenceInfo.Description
          | Scan              as state => state.SubruleInfo?.Description ?? "Root"
          | Predicate                  => "Predicate"
        }
      }
    }

    public override ToString() : string
    {
      $"$Id $(Next.NToList()) $(Prev.NToList()) $Description"
    }
  }
}
