using Nitra.Internal;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Text;
using System.Linq;

namespace Nitra
{
  public class StringPrettyPrintWriter : PrettyPrintWriter
  {
    _buffer : StringBuilder;
    _spans  : List[SpanInfo];

    mutable _currentIndent : int;
    mutable _lastStartLine : int;
    mutable _lastIndentEnd : int;
    mutable _lastMissing   : int;

    public this(options : PrettyPrintOptions = PrettyPrintOptions.None)
    {
      base(options);
      _buffer = StringBuilder();
      _spans  = List();
    }

    public GetSpans() : List[SpanInfo]
    {
      _spans
    }

    protected override Garbage(source : IPrettyPrintSource, skip : NSpan) : void
    {
      _ = _buffer.Append("◄");
      unless (skip.IsEmpty)
        _ = _buffer.Append(source.Text, skip.StartPos, skip.Length);
      _ = _buffer.Append("►");
    }

    protected override FormatToken(source : IPrettyPrintSource, token : NSpan, canBeEmpty : bool, ruleName : string, spanClass : SpanClass) : void
    {
      TryPrintGarbage(source, token);

      if (token.IsEmpty)
        when (!canBeEmpty && Options %&& PrettyPrintOptions.MissingNodes)
          _ = _buffer.Append('«').Append(ruleName).Append('»');
      else
      {
        def startPos = _buffer.Length;
        _ = _buffer.Append(source.Text, token.StartPos, token.Length);
        when (spanClass != null)
        {
          def endPos = _buffer.Length;
          _spans.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
        }
      }
    }

    protected override FormatString(source : IPrettyPrintSource, token : NSpan, text : string, spanClass : SpanClass) : void
    {
      TryPrintGarbage(source, token);

      if (token.IsEmpty)
        when (Options %&& PrettyPrintOptions.MissingNodes)
          _ = _buffer.Append('«').Append(text).Append('»');
      else
      {
        def startPos = _buffer.Length;
        _ = _buffer.Append(text);
        when (spanClass != null)
        {
          def endPos = _buffer.Length;
          _spans.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
        }
      }
    }

    public override MissingNode(ruleDescriptor : RuleDescriptor) : void
    {
      when (Options %&& PrettyPrintOptions.MissingNodes)
        _ = _buffer.Append('«').Append(ruleDescriptor.Name).Append('»');
      _lastMissing = _buffer.Length;
    }

    public override AmbiguousNode(ambiguousTree : IAmbiguousParseTree, spanClass : SpanClass) : void
    {
      _ = _buffer.Append("<# ambiguous ").Append(ambiguousTree.RuleDescriptor.Name).Append(", ").Append(ambiguousTree.Ambiguities.Count).Append(" options");
      NewLineAndIndent();
      def previousTokenPos = _previousTokenPos;
      foreach (a in ambiguousTree.Ambiguities)
      {
        _previousTokenPos = previousTokenPos;
        a.PrettyPrint(this, 0, spanClass);
        NewLine();
      }
      Unindent();
      _ = _buffer.Append("#>");
      NewLine();
    }

    public override AmbiguousNode[T](ambiguousTree : IAmbiguousParseTree, ruleType : string, source : IPrettyPrintSource, spanClass : SpanClass, printer : Action[PrettyPrintWriter, IPrettyPrintSource, T, SpanClass]) : void
    {
      _ = _buffer.Append("<# ambiguous ").Append(ruleType).Append(", ").Append(ambiguousTree.Ambiguities.Count).Append(" options");
      NewLineAndIndent();
      def previousTokenPos = _previousTokenPos;
      foreach (a in ambiguousTree.Ambiguities)
      {
        _previousTokenPos = previousTokenPos;
        printer(this, source, a :> T, spanClass);
        NewLine();
      }
      Unindent();
      _ = _buffer.Append("#>");
      NewLine();
    }

    public override NewLine() : void
    {
      IndentNewLine();
    }

    public override NewLineAndIndent() : void
    {
      _currentIndent++;
      IndentNewLine();
    }

    public override Whitespace() : void
    {
      _ = _buffer.Append(' ');
    }

    public override Indent() : void
    {
      _currentIndent++;
      when (Options %&& PrettyPrintOptions.DebugIndent && _lastStartLine != _buffer.Length)
        _ = _buffer.Append("!!! The indentation increasing not from the beginning of line. !!!");
      IndentCurrentLine();
    }

    public override Unindent() : void
    {
      _currentIndent--;
      if (_lastIndentEnd == _buffer.Length)
      {
        _buffer.Length = _lastStartLine;
        IndentCurrentLine();
      }
      else if (Options %&& PrettyPrintOptions.DebugIndent)
      {
        if (_lastMissing == _buffer.Length)
        {
          IndentNewLine();
          _currentIndent++;
          Unindent();
        }
        else
        _ = _buffer.Append(" !!! no new line before indentation decreasing !!!");
      }
      else
        IndentNewLine();
    }

    private IndentNewLine() : void
    {
      _ = _buffer.AppendLine();
      _lastStartLine = _buffer.Length;
      IndentCurrentLine();
    }

    private IndentCurrentLine() : void
    {
      // TODO: Make indent customizable.
      _ = _buffer.Append(' ', _currentIndent * 2);
      _lastIndentEnd = _buffer.Length;
    }

    public override ToString() : string
    {
      _buffer.ToString()
    }
  }
}
