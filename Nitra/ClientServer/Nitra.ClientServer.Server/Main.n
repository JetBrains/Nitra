using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;
using Nitra.ClientServer.Server;

using System;
using System.Console;
using System.Collections.Generic;
using System.Globalization;
using System.IO;
using System.IO.Pipes;
using System.Linq;
using System.Threading;

module Program
{
  Main(args : array[string]) : void
  {
    //assert2(false, "Start debugger");
    
    when (args.Length != 1)
    {
      assert2(false, "Invalid number of parameters!");
      ServerUtils.Log("Invalid number of parameters, expected: <request pipe name> <response pipe name>");
      Environment.Exit(42);
    }
    
    def mainPipeName = args[0];
    def requestPipeName       = mainPipeName + Constants.RequestPipeSuffix;
    def asyncResponsePipeName = mainPipeName + Constants.AsyncResponsePipeSuffix;
    def responsePipeName      = mainPipeName + Constants.ResponsePipeSuffix;
      
    using (asyncResponsePipe = NamedPipeServerStream(asyncResponsePipeName, PipeDirection.Out))
    using (responsePipe      = NamedPipeServerStream(responsePipeName,      PipeDirection.Out))
    using (requestPipe       = NamedPipeServerStream(requestPipeName,       PipeDirection.In))
    {
      // Connect to the pipe or wait until the pipe is available.
      Write("Attempting to connect to pipes...");
      requestPipe.      WaitForConnection();
      responsePipe.     WaitForConnection();
      asyncResponsePipe.WaitForConnection();

      def asyncResponseWriter = BinaryWriter(asyncResponsePipe);
      def responseWriter      = BinaryWriter(responsePipe);
      def router              = Router(responseWriter, asyncResponseWriter);
    
      ServerUtils.Log("Connected to pipe.");
      ServerUtils.Log("There are currently $(requestPipe.NumberOfServerInstances) pipe server instances open.");
      //def writer = BinaryWriter(requestPipe);
      def MainPipeReaderThreadMain()
      {
        def thread = Thread.CurrentThread;
        thread.Name = "Server Pipe Reader";
        thread.CurrentCulture = CultureInfo.InvariantCulture;
          
        using (reader = BinaryReader(requestPipe))
        {
          while (requestPipe.IsConnected)
          {
            try
            {
              def msg = Deserializer.Deserialize.[ClientMessage](reader);

              when (msg is ClientMessage.SolutionStartLoading)
                router.ProjectLoadCancel();

              router.AddMessage(msg);
            }
            catch
            {
              | _ is EndOfStreamException when !requestPipe.IsConnected => ServerUtils.Log("Session has been terminated."); Environment.Exit(1);
              | e                                                       => ServerUtils.LogException(e);
            }
          }
        }
      }
      def readerThread = Thread(MainPipeReaderThreadMain);
      readerThread.IsBackground = true;
      readerThread.Start();
      ServerUtils.Log("Server started.");
      router.Wait();
      ServerUtils.Log("Server stopped.")
    }
  }
}