using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;
using Nitra.ClientServer.Server.ProjectSystem;
using Nitra.Declarations;

using System;
using System.Threading;
using System.Globalization;
using System.Collections.Generic;
using System.Collections.Concurrent;
using System.Collections.Immutable;
using System.Linq;

using IO = System.IO;

namespace Nitra.ClientServer.Server
{
  internal partial class Router
  {
            _mainQueue                  : BlockingCollection[RouterMessage] = BlockingCollection();
            _parseQueue                 : BlockingCollection[ParserMessage] = BlockingCollection();
            _mainThread                 : Thread;
            _parseWorkers               : list[ParseWorker];
    mutable _cts                        : CancellationTokenSource = CancellationTokenSource();
            _asyncResponseWriter        : IO.BinaryWriter;
            _responseWriter             : IO.BinaryWriter;
    public  SpanClassesManager          : SpanClassesManager;
    mutable _stopped                    : bool;
    mutable _solution                   : Solution;
    mutable _projectLoadingCts          : CancellationTokenSource;
    public  PrettyPrintState            : PrettyPrintState { get; private set; }
    public  IsParseTreeReflectionEnable : bool { get; private set; }

    public this(responseWriter : IO.BinaryWriter, asyncResponseWriter : IO.BinaryWriter)
    {
      _responseWriter      = responseWriter;
      _asyncResponseWriter = asyncResponseWriter;
      _mainThread          = Thread(RouterThreadMain);
      _mainThread.Start();
      def cores       = ServerUtils.GetCoreCount(); // Environment.ProcessorCount
      ServerUtils.Log("Cores: " + cores);
      this.SpanClassesManager = SpanClassesManager(this);
      _parseWorkers = $[1..cores].Map(_ => ParseWorker(this, _parseQueue, _mainQueue, asyncResponseWriter, SpanClassesManager));
    }
    
    public SolutionId : int { get { if (_solution == null) -1 else _solution.Id } }
    
    public ProjectLoadCancellationToken : CancellationToken
    {
      get
      {
        when (_projectLoadingCts == null)
          _projectLoadingCts = CancellationTokenSource();
        _projectLoadingCts.Token
      }
    }

    public ProjectLoadFinish() : void
    {
      _projectLoadingCts = null;
    }
    
    public ProjectLoadCancel() : void
    {
      def cts = _projectLoadingCts;
      when (cts != null)
      {
        _projectLoadingCts = null;
        //def cts = Interlocked.Exchange(ref _cts, CancellationTokenSource());
        cts.Cancel();
        cts.Dispose();
      }
    }
    
    public SendAsyncResponse(msg : AsyncServerMessage) : void
    {
      lock (_asyncResponseWriter)
        msg.Serialize(_asyncResponseWriter);
    }

    public FlushAsyncResponse() : void
    {
      _asyncResponseWriter.Flush();
    }

    public SendResponse(msg : ServerMessage) : void
    {
      lock (_responseWriter)
        msg.Serialize(_responseWriter);
    }

    public SendResponseTest() : void
    {
      _responseWriter.Write(42);
    }

    public FlushResponse() : void
    {
      _responseWriter.Flush();
    }

    RouterThreadMain() : void
    {
      def thread = Thread.CurrentThread;
      thread.Name = "Router";
      thread.CurrentCulture = CultureInfo.InvariantCulture;

      RouteMessages();
    }
    
    RouteMessages() : void
    {
      while (!_mainQueue.IsCompleted)
      {
        try
        {
          foreach(msg in _mainQueue.GetConsumingEnumerable(_cts.Token))
          {
            | serverMsg is ClientMessage.Shutdown => OnClientMessage(serverMsg); return;
            | serverMsg is ClientMessage          => OnClientMessage(serverMsg);
            | serverMsg is RouterAnswerMessage    => OnRouterAnswerMessage(serverMsg);
            | _ => assert2(false)
          }
        }
        catch
        {
          | e is OperationCanceledException => ServerUtils.LogCanceled(e);
          | e                               => ServerUtils.LogException(_asyncResponseWriter, e)
        }
      }
    }
    
    public Wait() : void
    {
      unless (_stopped)
      {
        _mainThread.Join();
        _cts.Dispose();
        foreach(worker in _parseWorkers)
          worker.Dispose();
        _parseQueue.Dispose();
        _stopped = true;
      }
    }
    
    public AddMessage(msg : RouterMessage): void
    {
      unless (_stopped)
        _mainQueue.Add(msg)
    }
    
    GetLibsMetadata(libs : ImmutableArray[string]) : void 
    {
      def metadataBuilder = ImmutableArray.CreateBuilder(libs.Length);
      
      foreach (path in libs)
      {
        def assembly = ServerUtils.LoadAssembly(path);
        def langs = Language.GetLanguages(assembly);
        def langsBuilder = ImmutableArray.CreateBuilder(langs.Length);
        foreach (lang in langs)
        {
          def langInfo = LanguageInfo(lang.Name, path, array(0));
          langsBuilder.Add(langInfo);
        }
        def metadata = LibMetadata(ImmutableArray.Create("<not supported yet>"), langsBuilder.MoveToImmutable());
        metadataBuilder.Add(metadata);
      }

      SendResponse(ServerMessage.LibsMetadata(SolutionId, metadataBuilder.MoveToImmutable()));
    }

    GetLibsSyntaxModules(libs : ImmutableArray[string]) : void 
    {
      def libsBuilder = ImmutableArray.CreateBuilder(libs.Length);
      
      foreach (path in libs)
      {
        def assembly = ServerUtils.LoadAssembly(path);
        def descriptors = GrammarDescriptor.GetDescriptors(assembly);
        def syntaxModulesBuilder = ImmutableArray.CreateBuilder(descriptors.Length);
        foreach (descriptor in descriptors)
          syntaxModulesBuilder.Add(descriptor.FullName);
        libsBuilder.Add(SyntaxModules(syntaxModulesBuilder.MoveToImmutable()));
      }
      
      SendResponse(ServerMessage.LibsSyntaxModules(SolutionId, libsBuilder.MoveToImmutable()));
    }

    GetLibsProjectSupports(libs : ImmutableArray[string]) : void 
    {
      def libsBuilder = ImmutableArray.CreateBuilder(libs.Length);
      
      foreach (path in libs)
      {
        def typeBuilder = ImmutableArray.CreateBuilder(libs.Length);
        def assembly = ServerUtils.LoadAssembly(path);
        def attributes = assembly.GetCustomAttributes(typeof(ProjectSystem.ProjectSupportAttribute), false);

        foreach (attr is ProjectSystem.ProjectSupportAttribute in attributes)
        {
          def caption = 
            if (attr == null)
            {
              def fullName = attr.Type.FullName;
              def index = fullName.LastIndexOf(".");
              if (index < 1)
                fullName
              else
                fullName.Substring(index)
            }
            else
              attr.Caption;
          typeBuilder.Add(ProjectSupport(caption, attr.Type.FullName, path));
        }
          
        libsBuilder.Add(ProjectSupports(typeBuilder.ToImmutable()));
      }
      
      SendResponse(ServerMessage.LibsProjectSupports(SolutionId, libsBuilder.MoveToImmutable()));
    }
    
    GetFileExtensions(projectId : int, languageNames : ImmutableArray[string]) : void 
    {
      def project = _solution[projectId];
      def fileExtensions = project.GetFileExtensions(languageNames);
      SendResponse(ServerMessage.FileExtensions(SolutionId, fileExtensions));
    }
  }
}
