using Nitra;
using Nitra.Declarations;

using Nemerle.Collections;

using SCG = System.Collections.Generic;

namespace DotNet
{
  //declaration alias TypeParameterAlias = TypeParameter;

  declaration TypeParameter : Type, CustomAttributeHost
  {
    symbol
    {
      Kind      = "type parameter";
      SpanClass = DotNetLang.TypeSpanClass;
      FullName  = Name;

      in TypeConstraints : TypeConstraintSet;
      in Variance        : Variance;
    }
  }

  declaration TypeParameterAlias : Type, CustomAttributeHost
  {
    symbol
    {
      Kind      = "type parameter alias";
      SpanClass = DotNetLang.AliasSpanClass;

      in TypeParameter : TypeParameterSymbol;
    }

    in    TypeParameterSymbols : SCG.IList[TypeParameterSymbol];
    inout Index                : int;
    in    DeclaringMember      : DeclarationSymbol;

  stage 2:
    out    IsValidAlias         : bool = Symbol.TypeParameter.IsNameValid && this.Variance.ValueOrDefault == Symbol.TypeParameter.Variance;

    IndexOut = IndexIn + 1;
    Symbol.TypeParameter          = AstUtils.GetTypeParameter(this, TypeParameterSymbols, IndexIn); // workaround ICE: TypeParameterSymbols[IndexIn]
    Symbol.TypeParameter.Variance = this.Variance.ValueOrDefault;
    Attributes.NameScope          = Scope;

    Variance   : Variance;

    unless (IsValidAlias)
      Error(context, $"Partial declarations of '$(DeclaringMember.FullName)' must have the same type parameter names in the same order.");
  }
}
