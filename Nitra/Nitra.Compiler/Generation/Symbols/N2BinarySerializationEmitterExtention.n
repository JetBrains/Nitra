using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  internal sealed class N2SymbolRefBinarySerializationEmitterExtension : IBinarySerializationEmitterExtension
  {
    _environment : EmitterEnvironment;

    [RecordIgnore] mutable _metadataWriterParam : string;
    [RecordIgnore] mutable _metadataReaderParam : string;

    public CreateParameters(emitter : BinarySerializationEmitter) : void
    {
      _metadataWriterParam = emitter.CreateSerializationParameter("metadataWriter", <[ Nitra.Serialization.IMetadataWriter ]>);
      _metadataReaderParam = emitter.CreateDeserializationParameter("metadataReader", <[ Nitra.Serialization.IMetadataReader ]>);
    }

    public IsSupportedType(type : FixedType) : bool
    {
      type.TryRequire(_environment.IExportableSymbolTypeVar)
       || type.TryRequire(_environment.SymbolTypeVar)
       || type.Equals(_environment.LocationTypeVar)
    }

    public MakeWriteCode(type : FixedType, _emitter : BinarySerializationEmitter) : PExpr
    {
      if (type.TryRequire(_environment.IExportableSymbolTypeVar))
        <[ $(_metadataWriterParam : usesite).WriteSymbolRef(obj, writer); ]>
      else if (type.TryRequire(_environment.SymbolTypeVar))
        <[ $(_metadataWriterParam : usesite).WriteSymbolRef(obj :> Nitra.Serialization.IExportableSymbol, writer); ]>
      else if (type.Equals(_environment.LocationTypeVar))
        <[ $(_metadataWriterParam : usesite).WriteLocation(obj, writer); ]>
      else
        assert(false)
    }

    public MakeReadCode(type : FixedType, _emitter : BinarySerializationEmitter) : PExpr
    {
      if (type.TryRequire(_environment.IExportableSymbolTypeVar) || type.TryRequire(_environment.SymbolTypeVar))
        <[
          ignore(typeCode);
          $(_metadataReaderParam : usesite).ReadSymbolRef(reader) :> $(type : typed);
        ]>
      else if (type.Equals(_environment.LocationTypeVar))
        <[
          ignore(typeCode);
          $(_metadataReaderParam : usesite).ReadLocation(reader);
        ]>
      else
        assert(false)
    }
  }
}
