using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal partial class RuleAstEmitter
  {
    private DefineAstPrettyPrint() : void
    {
      def isVoid(rule : Rule) { rule.Type is RuleType.Void }

      def isRecursiveCallFromPostfixRule(calleeRef)
      {
        match (this.RuleSymbol, calleeRef)
        {
          | (ExtensionRuleSymbol(BaseRule = SymbolRef.Some(baseRule), ExtensionType = Postfix), SymbolRef.Some(callee)) => baseRule.Equals(callee)
          | _ => false
        }
      }
      def fieldToString(rule : Rule) : PExpr
      {
        def loop(expr : PExpr, rule : Rule) : PExpr
        {
          def result =
            match (rule)
            {
              | String(str)                                     => <[ writer.String(source, $expr, $str); ]>
              | And | Not                                       => <[ () ]>
              | Sequence(rules)                                 =>
                def members2 = rules.MapI((i, r) => if (isVoid(r)) (<[ _ ]>, r), else (<[ $($"_item$i" : usesite) ]>, r));
                def noNeedVars = members2.ForAll((_, r) => isVoid(r));
                def varNames = $[ n | (n, r) in members2, !(r.Type is RuleType.Void) ]; // make list of names for not void rules
                def lines = members2.Map(loop);
                match (varNames)
                {
                  | _ when noNeedVars
                  | []        => <[ { ..$lines } ]>
                  | [varName] => <[ { ..$(<[ def $varName      = $expr; ]> :: lines) } ]>
                  | _         => <[ { ..$(<[ def (..$varNames) = $expr; ]> :: lines) } ]>
                }

              | Call(SymbolRef.Some(MarkerSymbol as markerSymbol), _bp) =>
                match (markerSymbol.Node.FullNameParts)
                {
                  | ["PrettyPrint",  "nl"] => <[ writer.NewLine() ]>
                  | ["PrettyPrint", "inl"] => <[ writer.NewLineAndIndent() ]>
                  | ["PrettyPrint",  "sm"] => <[ writer.Whitespace() ]>
                  | ["PrettyPrint",   "d"] => <[ writer.Unindent() ]>
                  | ["PrettyPrint",   "i"] => <[ writer.Indent() ]>
                  | _ => <[ () ]>
                }

              | Call(ruleRef, _bp) when rule.Type is RuleType.Void =>
                match (ruleRef.Value.Name)
                {
                  | "S" | "s" => <[ () ]> // TODO: add analysis of a space usefulness
                  | _ => <[ writer.Whitespace(); ]>
                }

              | Repeat as loop when loop.IsSkipAnyPattern
              | Call when rule.Type is RuleType.Chars                          => <[ writer.Token(source, $expr); ]>
              | Call(ruleRef, bp) when isRecursiveCallFromPostfixRule(ruleRef) => <[ $expr.PrettyPrint(writer, $bp); ]>
              | Call                                                           => <[ $expr.PrettyPrint(writer, 0); ]>
              | Repeat(_, _, rule) when isVoid(rule)                           => <[ writer.Whitespace(); ]>
              | Repeat(0, Some(1), rule)                                       => <[ when ($expr is Some(_x)) $(loop(<[ _x ]>, rule)) ]>
              | Repeat(_, _, rule)                                             => <[ foreach (_elem in $expr) $(loop(<[ _elem ]>, rule)); ]>
              | RepeatWithSeparator(_, _, rule, separator, _) =>
                match (rule.Type, separator.Type)
                {
                  | (Void, Void) => <[ writer.Whitespace(); ]>
                  | (_, Void) =>
                    <[
                      foreach (_elem in $expr with i)
                      {
                        when (i > 0)
                          $(loop(<[ _ ]>, separator));
                        $(loop(<[ _elem ]>, rule));
                      }
                    ]>

                  | (Void, _) =>
                    <[
                      def separators = $expr;
                      foreach (_separator in separators with i)
                      {
                        $(loop(<[ _ ]>, rule));
                        $(loop(<[ _separator ]>, separator));
                      }
                      unless (separators.IsEmpty)
                        $(loop(<[ _ ]>, rule));
                    ]>

                  | _ =>
                    <[
                      def (elems, separators) = $expr;
                      foreach (elem in elems with i)
                      {
                        $(loop(<[ elem ]>, rule));
                        when (i < separators.Length)
                        {
                          def separator = separators[i];
                          $(loop(<[ separator ]>, separator));
                        }
                      }
                    ]>
                }
            };
          result
        }
        loop(<[ this.$(rule.FieldName : usesite) ]>, rule);
      }

      def code = this.RuleSymbol.SubRules.Map(fieldToString);

      def printGarbage =
        if (this.RuleSymbol.IsToken) <[ writer.TryPrintGarbage(source, this.Location.Span); ]>
        else <[ () ]>;
      def decl =
      if (this.RuleSymbol is ExtensionRuleSymbol(ExtensionType = Postfix(BindingPower = bindingPower)))
        <[ decl:
          public override PrettyPrint(writer : Nitra.PrettyPrintWriter, callerBindingPower : int) : void
          {
            def source = this.PrettyPrintSource;
            $printGarbage;
            //TODO: determine correct braces from grammar
            when (callerBindingPower > $bindingPower)
              writer.String(source, Nitra.NSpan(0, 1), "(");
            ..$code;
            when (callerBindingPower > $bindingPower)
              writer.String(source, Nitra.NSpan(0, 1), ")");
          }
        ]>
      else
        <[ decl:
          public override PrettyPrint(writer : Nitra.PrettyPrintWriter, callerBindingPower : int) : void
          {
            def source = this.PrettyPrintSource;
            ignore(writer, callerBindingPower, source);
            ..$code;
          }
        ]>;
      _astTb.DefineConditional(decl);
    }

    private DefineMissingPrettyPrint() : void
    {
      _missingTb.DefineConditional(<[ decl:
        public override PrettyPrint(writer : Nitra.PrettyPrintWriter, _callerBindingPower : int) : void
        {
          writer.MissingNode($(this.RuleSymbol.GetRuleDescriptorRef()));
        }
      ]>);
    }

    private DefineAmbiguousPrettyPrint() : void
    {
      _ambiguousTb.DefineConditional(<[ decl:
        public override PrettyPrint(writer : Nitra.PrettyPrintWriter, _callerBindingPower : int) : void
        {
          writer.AmbiguousNode(this);
        }
      ]>);
    }
  }
}
