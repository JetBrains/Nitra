using Nitra.Typing;
using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ListAstEmitter : CollectionAstEmitter
  {
    public override TypeCompiler : NTypeCompiler { get { Environment.AstListTypeCompiler } }

    public this(environment : EmitterEnvironment, symbol : AstSymbol)
    {
      base(environment, symbol)
    }

    protected override CreateBaseTb(ownerTb : TypeBuilder, itemType : FixedType) : TypeBuilder
    {
      ownerTb.DefineNestedType(<[ decl:
        new public interface IAstList : Nitra.Declarations.IAstList[$(itemType : typed)]
        {
        }
      ]>);
    }

    protected override CreateConcreteTb(ownerTb : TypeBuilder, itemType : FixedType, baseType : FixedType) : TypeBuilder
    {
      ownerTb.DefineNestedType(<[ decl:
        new public sealed class AstList : Nitra.Declarations.AstList[$(itemType : typed)], $(baseType : typed)
        {
          public this(loc : Nitra.Located, params items : array[$(itemType : typed)])
          {
            base(loc, items);
          }

          public this(file : $(FileType : typed), span : $(SpanType : typed), params items : array[$(itemType : typed)])
          {
            base(file, span, items);
          }
        }
      ]>);
    }

    protected override CreateAmbiguousTb(ownerTb : TypeBuilder, itemType : FixedType, baseType : FixedType) : TypeBuilder
    {
      ownerTb.DefineNestedType(<[ decl:
        new public sealed class AmbiguousAstList : Nitra.Declarations.AmbiguousAstList[$(itemType : typed)], $(baseType : typed)
        {
          public this(ambiguities : array[$(baseType : typed)])
          {
            base((ambiguities : object) :> array[Nitra.Declarations.IAstList[$(itemType : typed)]]);
          }
          public new Ambiguities : array[$(baseType : typed)] { get { base.Ambiguities :> array[$(baseType : typed)] } }
        }
      ]>);
    }

    protected override MakeEvalPropertiesBody(
      astProperties        : SCG.Dictionary[DependentAstPropertySymbol, DependentAstPropertyRef],
      collectionProperties : SCG.Dictionary[DependentAstPropertySymbol, DependentAstPropertyRef.Compiling],
      dependentProperties  : array[DependentAstPropertySymbol]) : PExpr
    {
      def setFirstItemInputs     = SCG.List();
      def linkEachItemProperties = SCG.List();
      def setEmptyListOutputs    = SCG.List();
      def outputProperties       = SCG.List();
      def outputFlagInit         = SCG.List();
      def outputFlagSetFirst     = SCG.List();
      def outputFlagSetEach      = SCG.List();

      foreach (propertySymbol in dependentProperties)
      {
        match (propertySymbol.Direction)
        {
          | In =>
            def listInputRef = collectionProperties[propertySymbol];
            def itemInputRef = astProperties[propertySymbol];
            LinkProperties(setFirstItemInputs, <[ this ]>, listInputRef, <[ item ]>, itemInputRef);

            if (propertySymbol.LinkedProperty is SymbolRef.Some(linkedProperty))
            {
              def itemOutputRef = astProperties[linkedProperty];
              LinkProperties(linkEachItemProperties, <[ prevItem ]>, itemOutputRef, <[ item ]>, itemInputRef);
            }
            else
              LinkProperties(linkEachItemProperties, <[ this ]>, listInputRef, <[ item ]>, itemInputRef);

          | Out =>
            def listOutputRef = collectionProperties[propertySymbol];
            if (propertySymbol.LinkedProperty is SymbolRef.Some(linkedProperty))
            {
              def listInputRef = collectionProperties[linkedProperty];
              LinkProperties(setEmptyListOutputs, <[ this ]>, listInputRef, <[ this ]>, listOutputRef);
            }
            else
            {
              SetPropertyValue(setEmptyListOutputs, listOutputRef, <[ array(0) ]>);
              def itemOutputRef = astProperties[propertySymbol];
              def flag          = Util.tmpname(propertySymbol.Name);
              outputProperties.Add(propertySymbol, flag);
              outputFlagInit.Add(<[ mutable $(flag : usesite) = ! $(GenerateHasValueCheck(<[ this ]>, listOutputRef)) ]>);
              outputFlagSetFirst.Add(<[ $(flag : usesite) &= $(GenerateHasValueCheck(<[ item ]>, itemOutputRef)) ]>);
              outputFlagSetEach.Add(<[ $(flag : usesite) &= $(GenerateHasValueCheck(<[ item ]>, itemOutputRef)) ]>);
            }
        }
      }

      def readOutputs =
        match (outputProperties.Count)
        {
          | 0 => <[ () ]>
          | 1 =>
            def (propertySymbol, flag) = outputProperties[0];
            <[
              when ($(flag : usesite))
              {
                def buffer = array(length);
                for (mutable i = 0; i < length; ++i)
                  buffer[i] = _items[i].$(propertySymbol.Name : usesite);
                this.$(propertySymbol.Name : usesite) = buffer;
              }
            ]>

          | _ =>
            mutable pred = <[ $(outputProperties[0][1] : usesite) ]>;
            for (mutable i = 1; i < outputProperties.Count; ++i)
              pred = <[ $pred || $(outputProperties[i][1] : usesite) ]>;

            def createBuffers = SCG.List();
            def fillBuffers   = SCG.List();
            def setBuffers    = SCG.List();
            foreach ((propertySymbol, flag) in outputProperties)
            {
              def bufferName = Util.tmpname("buffer");
              createBuffers.Add(<[ def $(bufferName : usesite) = if ($(flag : usesite)) array(length) else null ]>);
              fillBuffers.Add(<[ when ($(flag : usesite)) $(bufferName : usesite)[i] = item.$(propertySymbol.Name : usesite) ]>);
              setBuffers.Add(<[ when ($(flag : usesite)) this.$(propertySymbol.Name : usesite) = $(bufferName : usesite) ]>);
            }
            <[
              when ($pred)
              {
                ..$createBuffers;
                for (mutable i = 0; i < length; ++i)
                {
                  item = _items[i];
                  ..$fillBuffers;
                }
                ..$setBuffers;
              }
            ]>
        };

      <[
        def length = _items.Length;
        if (length > 0)
        {
          ..$outputFlagInit;
          mutable item = _items[0];
          ..$setFirstItemInputs;
          item.EvalProperties(context);
          ..$outputFlagSetFirst;

          for (mutable i = 1; i < length; ++i)
          {
            def prevItem = item;
            _ = prevItem;
            item = _items[i];
            ..$linkEachItemProperties;
            item.EvalProperties(context);
            ..$outputFlagSetEach;
          }
          $readOutputs;
        }
        else
        {
          ..$setEmptyListOutputs
        }
      ]>
    }
  }
}
