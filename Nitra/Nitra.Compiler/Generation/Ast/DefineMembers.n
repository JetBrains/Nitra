using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal partial class RuleAstEmitter
  {
    public DefineMembers() : void
    {
      when (_baseTb != null)
      {
        DefineBaseAstCreateMethod();
        DefineBaseAstRuleMethods();
      }
      when (_astTb != null)
      {
        def fields = GetFields();
        DefineAstCtorAndFields(fields);
        DefineParsedAstCtorAndFields(fields);
        DefineChemicalAstCtorAndFields(fields);
        DefineAstApplyMethod(fields);
        DefineAstPrettyPrint();
        DefineAstRuleMethods();
      }
      when (_missingTb != null)
      {
        DefineMissingPrettyPrint();
        DefineMissingRuleMethods();
      }
      when (_ambiguousTb != null)
      {
        DefineAmbiguousPrettyPrint();
        DefineAmbiguousRuleMethods();
      }
    }

    private DefineBaseAstCreateMethod() : void
    {
      when (this.RuleSymbol is SimpleRuleSymbol)
      {
        _baseTb.DefineConditional(<[ decl:
          public static Create(pos : ref int, parseResult : Nitra.ParseResult) : $(_baseTb.GetMemType() : typed)
          {
            def ruleParser = parseResult.RuleParser.Grammar.GetSimpleRuleParser($(this.RuleSymbol.GetRuleDescriptorRef()));
            def astPtr = parseResult.TryGetAst(pos, ruleParser.RuleId);
            if (astPtr > 0)
              $(_baseTb.GetMemType() : typed).__Parsed__Ast(ref pos, astPtr, parseResult)
            else
              $(_baseTb.GetMemType() : typed).MissingAst(parseResult, pos)
          }
        ]>);
      }
      when (this.RuleSymbol.Options.IsStart)
      {
        match (this.RuleSymbol)
        {
          | SimpleRuleSymbol =>
            _baseTb.DefineConditional(<[ decl:
              public static Create(parseResult : Nitra.ParseResult) : $(_baseTb.GetMemType() : typed)
              {
                mutable pos = parseResult.StartPos;
                Create(ref pos, parseResult);
              }
            ]>);

          | ExtensibleRuleSymbol =>
            _baseTb.DefineConditional(<[ decl:
              public static Create(parseResult : Nitra.ParseResult) : $(_baseTb.GetMemType() : typed)
              {
                mutable pos = parseResult.StartPos;
                Nitra.Internal.ExtensibleRuleAstConstructor.Create(ref pos, parseResult, 0, $(this.RuleSymbol.GetRuleDescriptorRef()), Nitra.Internal.DefaultExtensibleRuleAstFactory.Instance);
              }
            ]>);

          | _ => assert2(false);
        }
      }
    }

    private DefineAstCtorAndFields(fields : SCG.List[FieldInfo]) : void
    {
      def traverse(ruleType : RuleType, obj : PExpr) : ChemicalRuleType * PExpr
      {
        match (ruleType)
        {
          | Void         => assert3(false)
          | Ref(symbol)  => (ChemicalRuleType.Ast(symbol),    obj)
          | Chars(Some)  => (ChemicalRuleType.StaticString(), <[ () ]>)
          | Chars(None)  => (ChemicalRuleType.String(),       <[ this.GetText($obj) ]>)
          | Option(type) =>
            def itemVariable = Util.tmpname("item");
            def (itemType, itemExpr) = traverse(type, <[ $(itemVariable : usesite) ]>);
            if (itemType is ChemicalRuleType.StaticString)
              (ChemicalRuleType.Bool(), <[ $obj.IsSome ]>)
            else
            {
              def expr =
              <[
                match ($obj)
                {
                  | Some($(itemVariable : usesite)) => Some($itemExpr)
                  | None                            => None()
                }
              ]>;
              (ChemicalRuleType.Option(itemType), expr)
            }

          | List(type) =>
            def itemVariable = Util.tmpname("item");
            def (itemType, itemExpr) = traverse(type, <[ $(itemVariable : usesite) ]>);
            if (itemType is ChemicalRuleType.StaticString)
              (ChemicalRuleType.Int(), <[ $obj.Length ]>)
            else
            {
              def expr =
              <[
                def items = System.Collections.Generic.List();
                foreach ($(itemVariable : usesite) in $obj)
                  items.Add($itemExpr);
                Nemerle.Collections.NList.ToList(items);
              ]>;
              (ChemicalRuleType.List(itemType), expr)
            }

          | Tuple(types) =>
            def memberTypes  = SCG.List();
            def patterns     = SCG.List();
            def exprs        = SCG.List();

            foreach(t in types)
            {
              def memberVariable = Util.tmpname("member");
              def (memberType, memberExpr) = traverse(t, <[ $(memberVariable : usesite) ]>);
              if (memberType is ChemicalRuleType.StaticString)
                patterns.Add(<[ _ ]>)
              else
              {
                memberTypes.Add(memberType);
                patterns.Add(<[ $(memberVariable : usesite) ]>);
                exprs.Add(memberExpr);
              }
            }

            match (memberTypes.Count)
            {
              | 0 => (ChemicalRuleType.StaticString(),               <[ () ]>)
              | 1 => (memberTypes[0],                                <[ def (..$patterns) = $obj; $(exprs[0]) ]>)
              | _ => (ChemicalRuleType.Tuple(memberTypes.NToList()), <[ def (..$patterns) = $obj; (..$exprs) ]>)
            }
        }
      }

      foreach (field in fields)
      {
        _astTb.DefineConditional(<[ decl:
          public new abstract $(field.Name : usesite) : $(field.Type : typed) { get; }
        ]>);

        match (field.Rule.Type)
        {
          | Ref | Chars(Some) => ()
          | ruleType =>
            def (propertyType, expr) = traverse(ruleType, <[ this.$(field.Name : usesite) ]>);
            unless (propertyType is ChemicalRuleType.StaticString)
              _astTb.DefineConditional(<[ decl:
                public $(field.Name + "Data" : usesite) : $(Environment.GetAstType(propertyType) : typed)
                {
                  get { unchecked ($expr) }
                }
              ]>);
        }
      }
    }

    private DefineParsedAstCtorAndFields(fields : SCG.List[FieldInfo]) : void
    {
      _parsedAstTb.DefineConditional(<[ decl: private parseResult : Nitra.ParseResult; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private startPos    : int; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private endPos      : int; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private astPtr      : int; ]>);

      _parsedAstTb.DefineConditional(<[ decl:
        public override Location : Nitra.Location
        {
          get { Nitra.Location(this.parseResult.OriginalSource, this.startPos, this.endPos) }
        }
      ]>);

      _parsedAstTb.DefineConditional(<[ decl:
        protected override PrettyPrintSource : Nitra.IPrettyPrintSource
        {
          get { this.parseResult }
        }
      ]>);

      def astPtr = AstPointer.Existing(this.RuleSymbol.Sequence, this, "astPtr");

      def endPosExpr = this.RuleSymbol.Sequence.NonMarkerSubrules.Fold(<[ pos ]>, (subrule, acc) => <[ $acc + $(astPtr.GetSize(subrule)) ]>);

      match (this.RuleSymbol)
      {
        | SimpleRuleSymbol
        | ExtensionRuleSymbol(ExtensionType = Prefix) =>
          _parsedAstTb.DefineConditional(<[decl:
            public this(pos : ref int, astPtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.startPos    = pos;
                this.endPos      = $endPosExpr;
                this.astPtr      = astPtr;
                pos              = this.endPos;
              }
            }
          ]>);

        | ExtensionRuleSymbol(ExtensionType = Postfix) as symbol =>
          _parsedAstTb.DefineConditional(<[ decl: private astStartPos : int; ]>);
          _parsedAstTb.DefineConditional(<[ decl: private prefix      : $(Environment.GetAstType(symbol.BaseRule.Value) : typed); ]>);
          _parsedAstTb.DefineConditional(<[ decl:
            public this(prefix : $(Environment.GetAstType(symbol.BaseRule.Value) : typed), prefixStartPos : int, pos : ref int, astPtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.startPos    = prefixStartPos;
                this.endPos      = $endPosExpr;
                this.astPtr      = astPtr;
                this.astStartPos = pos;
                this.prefix      = prefix;
                pos              = this.endPos;
              }
            }
          ]>);

        | _ => assert(false)
      }

      foreach (field in fields)
      {
        def statements = SCG.List();

        mutable needRuleParser  : bool;
        mutable needCaching     : bool;

        def analyze(_ : FieldExpr) : void
        {
          | Prefix         => ()
          | Tuple(items)   => foreach (expr in items) analyze(expr);
          | Chars(_, true) => needRuleParser = true;
          | Chars          => ()
          | Other(_, true) => needRuleParser = true; needCaching = true;
          | Other          =>                        needCaching = true;
        }

        analyze(field.Expr);

        when (needRuleParser)
          statements.Add(<[ def ruleParser = parseResult.GetRuleParser(astPtr) :> $(this.RuleSymbol.GetParserClassRef()) ]>);

        def compile(_ : FieldExpr) : PExpr
        {
          | Prefix(expr)
          | Chars(expr, _)
          | Other(expr, _) => expr
          | Tuple(items)   => <[ Nitra.SequenceAst(Nitra.Location.Default, ..$(items.Map(compile))) ]>
        }

        statements.Add(compile(field.Expr));

        def code =
          if (needCaching)
          {
            def backingFieldName = Util.tmpname(field.Name);
            _parsedAstTb.DefineConditional(<[ decl: private mutable $(backingFieldName : usesite) : $(field.Type : typed); ]>);
            if (field.Type.IsValueType)
            {
              def loadedBit = _parsedAstTb.GetBitFieldManager().Allocate();
              <[
                unless ($(loadedBit.Read()))
                {
                  $(backingFieldName : usesite) = { ..$statements };
                  $(loadedBit.Set());
                }
                $(backingFieldName : usesite)
              ]>
            }
            else
            {
              <[
                when ($(backingFieldName : usesite) : object == null)
                {
                  $(backingFieldName : usesite) = { ..$statements };
                }
                $(backingFieldName : usesite)
              ]>
            }
          }
          else
            <[ { ..$statements } ]>;

        _parsedAstTb.DefineConditional(<[ decl:
          public override $(field.Name : usesite) : $(field.Type : typed)
          {
            get { unchecked ($code) }
          }
        ]>);
      }
    }

    private DefineChemicalAstCtorAndFields(fields : SCG.List[FieldInfo]) : void
    {
      def traverse(ruleType : RuleType, obj : PExpr) : ChemicalRuleType * PExpr
      {
        match (ruleType)
        {
          | Void                 => assert3(false)
          | Ref(symbol)          => (ChemicalRuleType.Ast(symbol),    obj)
          | Chars(Some(keyword)) => (ChemicalRuleType.StaticString(), <[ context.GetKeywordSpan($(keyword : string)) ]>)
          | Chars(None)          => (ChemicalRuleType.String(),       <[ context.GetSpan($obj) ]>)
          | Option(type) =>
            def itemVariable = Util.tmpname("item");
            def (itemType, itemExpr) = traverse(type, <[ $(itemVariable : usesite) ]>);
            if (itemType is ChemicalRuleType.StaticString)
              (ChemicalRuleType.Bool(), <[ if ($obj) Nitra.OptionAst.Some(Nitra.Location.Default, $itemExpr) else Nitra.OptionAst.None(Nitra.Location.Default) ]>)
            else
            {
              def expr =
              <[
                match ($obj)
                {
                  | Some($(itemVariable : usesite)) => Nitra.OptionAst.Some(Nitra.Location.Default, $itemExpr)
                  | None                            => Nitra.OptionAst.None(Nitra.Location.Default)
                }
              ]>;
              (ChemicalRuleType.Option(itemType), expr)
            }

          | List(type) =>
            def itemVariable = Util.tmpname("item");
            def (itemType, itemExpr) = traverse(type, <[ $(itemVariable : usesite) ]>);
            if (itemType is ChemicalRuleType.StaticString)
            {
              def expr =
              <[
                def items = array($obj);
                for (mutable i = 0; i < items.Length; ++i)
                  items[i] = $itemExpr;
                Nitra.ListAst(Nitra.Location.Default, items);
              ]>;
              (ChemicalRuleType.Int(), expr)
            }
            else
            {
              def expr =
              <[
                def items = System.Collections.Generic.List();
                foreach ($(itemVariable : usesite) in $obj)
                  items.Add($itemExpr);
                Nitra.ListAst(Nitra.Location.Default, items);
              ]>;
              (ChemicalRuleType.List(itemType), expr)
            }

          | Tuple(types) =>
            def memberTypes        = SCG.List();
            def patternVariables   = SCG.List();
            def exprs              = SCG.List();

            foreach(t in types)
            {
              def memberVariable = Util.tmpname("member");
              def (memberType,  memberExpr) = traverse(t, <[ $(memberVariable : usesite) ]>);
              unless (memberType is ChemicalRuleType.StaticString)
              {
                memberTypes.Add(memberType);
                patternVariables.Add(memberVariable);
              }
              exprs.Add(memberExpr);
            }

            match (memberTypes.Count)
            {
              | 0 => (ChemicalRuleType.StaticString(),               <[ Nitra.SequenceAst(Nitra.Location.Default, ..$exprs) ]>)
              | 1 => (memberTypes[0],                                <[ def $(patternVariables[0] : usesite)                                   = $obj; Nitra.SequenceAst(Nitra.Location.Default, ..$exprs) ]>)
              | _ => (ChemicalRuleType.Tuple(memberTypes.NToList()), <[ def (..$(patternVariables.MapToList(name => <[ $(name : usesite) ]>))) = $obj; Nitra.SequenceAst(Nitra.Location.Default, ..$exprs) ]>)
            }
        }
      }

      _chemicalAstTb.DefineConditional(<[ decl: private sourceProvider   : Nitra.Runtime.ISourceSnapshotProvider; ]>);
      _chemicalAstTb.DefineConditional(<[ decl: private span             : Nitra.NSpan; ]>);
      _chemicalAstTb.DefineConditional(<[ decl: public override Location : Nitra.Location { get { Nitra.Location(this.sourceProvider.GetSource(), this.span) } } ]>);

      def ctorParameters = SCG.List();
      ctorParameters.Add(<[ parameter: sourceProvider : Nitra.Runtime.ISourceSnapshotProvider ]>);
      ctorParameters.Add(<[ parameter: span           : Nitra.NSpan ]>);

      def ctorStatements = SCG.List();
      ctorStatements.Add(<[ this.sourceProvider = sourceProvider ]>);
      ctorStatements.Add(<[ this.span           = span ]>);

      def createParameters = SCG.List();
      createParameters.Add(<[ parameter: context : Nitra.Runtime.ChemicalAstContext ]>);

      def ctorArguments = SCG.List();
      ctorArguments.Add(<[ context ]>);
      ctorArguments.Add(<[ Nitra.NSpan(0, 0) ]>);

      foreach (field in fields)
      {
        _chemicalAstTb.DefineConditional(<[ decl:
          public override $(field.Name : usesite) : $(field.Type : typed) { get; }
        ]>);

        ctorParameters.Add(<[ parameter: $(field.Name : usesite) : $(field.Type : typed) ]>);
        ctorStatements.Add(<[ this.$(field.Name : usesite) = $(field.Name : usesite) ]>);

        def (parameterType, expr) = traverse(field.Rule.Type, <[ $(field.Name : usesite) ]>);
        unless (parameterType is ChemicalRuleType.StaticString)
          createParameters.Add(<[ parameter: $(field.Name : usesite) : $(Environment.GetAstType(parameterType) : typed) ]>);
        ctorArguments.Add(expr);
      }

      _chemicalAstTb.DefineConditional(<[ decl:
        public this(..$ctorParameters) { ..$ctorStatements }
      ]>);

      match (this.RuleSymbol)
      {
        | SimpleRuleSymbol    with tb = _baseTb
        | ExtensionRuleSymbol with tb = _astTb =>
          tb.DefineConditional(<[ decl:
            public static Create(..$createParameters) : $(_astTb.GetMemType() : typed)
            {
              unchecked($(_chemicalAstTb.GetMemType() : typed)(..$ctorArguments))
            }
          ]>);

        | _ => assert3(false)
      }
    }
  }
}
