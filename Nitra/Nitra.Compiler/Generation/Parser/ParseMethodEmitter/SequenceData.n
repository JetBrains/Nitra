using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Model;
using Nitra.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  internal partial class ParseMethodEmitter
  {
    [Record]
    private sealed class SequenceData : SequenceDataBase[ParseMethodEmitter]
    {
      [RecordIgnore] public OkLabel   : PExprLabel { get; private set; }
      [RecordIgnore] public FailLabel : PExprLabel { get; private set; }
      [RecordIgnore] public AstPtr    : AstPointer { get; private set; }
                     public AstStart  : PExpr { get { <[ $(Emitter.CreateVariable($"astStart$(Structure.Level)") : global) ]> } }

      public override Init() : void
      {
        AstPtr = AstPointer.New(Structure, Emitter.ConstantEmitter, Emitter, "astPtr");
        match (Structure)
        {
          | SequenceCompileInfo.Ast =>
            OkLabel   = Emitter.Environment.MakeLabel("Ast_Ok");
            FailLabel = Emitter.Environment.MakeLabel("Ast_Fail");

          | SequenceCompileInfo.Predicate =>
            def id = ManagerClass.Instance.GetNewId();
            OkLabel   = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(id)_Ok");
            FailLabel = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(id)_Fail");

          | _ =>
            OkLabel   = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(Structure.Owner.State)_Ok");
            FailLabel = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(Structure.Owner.State)_Fail");
        }
      }
    }

    [Record]
    private sealed class SubruleData : SubruleDataBase[ParseMethodEmitter]
    {
      [RecordIgnore] public ParseLabel         : PExprLabel { get; private set; }
      [RecordIgnore] public RecoveryLabel      : PExprLabel { get; private set; }

      public FailParseLabel     : PExprLabel { get { Emitter._data.SequenceData[Field.Owner].FailLabel } }
      public ContinueParseLabel : PExprLabel
      {
        get
        {
          def getSibling(subrule : SubruleCompileInfo) : SubruleCompileInfo
          {
            | Marker => getSibling(subrule.NextSibling)
            | _      => subrule
          }
          def nextSibling = getSibling(Field.NextSibling);
          if (nextSibling == null)
            Emitter._data.SequenceData[Field.Owner].OkLabel
          else
            Emitter._data.SubruleData[nextSibling].ParseLabel
        }
      }

      public override Init() : void
      {
        ParseLabel    = Emitter.Environment.MakeLabel($"Parse_$LabelSuffix");
        RecoveryLabel = Emitter.Environment.MakeLabel($"Recovery_$LabelSuffix");
      }
    }

    _data : SequenceDataContainer[ParseMethodEmitter, SequenceData, SubruleData];

#pragma warning disable 10003
    private OkLabel      (sequence : SequenceCompileInfo) : PExpr      { _data.SequenceData[sequence].OkLabel.Label()   }
    private GotoOkLabel  (sequence : SequenceCompileInfo) : PExpr      { _data.SequenceData[sequence].OkLabel.Goto()    }
    private FailLabel    (sequence : SequenceCompileInfo) : PExpr      { _data.SequenceData[sequence].FailLabel.Label() }
    private GotoFailLabel(sequence : SequenceCompileInfo) : PExpr      { _data.SequenceData[sequence].FailLabel.Goto()  }
    private AstPtr       (sequence : SequenceCompileInfo) : AstPointer { _data.SequenceData[sequence].AstPtr            }
    private AstStart     (sequence : SequenceCompileInfo) : PExpr      { _data.SequenceData[sequence].AstStart          }
    private FunctionName (sequence : SequenceCompileInfo) : string     { sequence.ParseFunctionName                          }

    private ParseLabel            (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].ParseLabel.Label()         }
    private GotoParseLabel        (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].ParseLabel.Goto()          }
    private RecoveryLabel         (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].RecoveryLabel.Label()      }
    private GotoRecoveryLabel     (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].RecoveryLabel.Goto()       }
    private ContinueParseLabel    (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].ContinueParseLabel.Label() }
    private GotoContinueParseLabel(subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].ContinueParseLabel.Goto()  }
    private FailParseLabel        (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].FailParseLabel.Label()     }
    private GotoFailParseLabel    (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].FailParseLabel.Goto()      }
#pragma warning restore
  }
}
