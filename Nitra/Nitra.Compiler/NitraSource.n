using DotNet;

using Nitra.Compiler;
using Nitra.Declarations;
using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Typing;
using Nitra.Runtime;
using Nitra.Util;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;

using Nemerle.Utility;
using System;
using System.IO;
using System.Diagnostics;
using System.Linq;

using System.Reflection;

using SCG = System.Collections.Generic;
using G = NitraSyntaxParseTree;
using PExpr = Nemerle.Compiler.Parsetree.PExpr;

namespace Nitra
{
  internal class NitraSourceImpl
  {
    private Path          : string                 { get; }
    private Manager       : ManagerClass           { get; }
    private IsUnderNitraForVisualStudio : bool;

    private static RestrictedNames : SCG.HashSet[string] = SCG.HashSet(array[
      // Parse tree classes
      "Located",
      "ParseTree",
      "AmbiguousOrMissingNode",
      "Nested",

      // Parse tree Methods
      "Equals",
      "GetHashCode",
      "GetType",
      "ReferenceEquals",
      "MemberwiseClone",
      "Finalize",
      "ToString",
      "PrettyPrint",
      "GetText",
      "FirstChar",
      "IntersectsWith",
      "Intersection",
      "Apply",
      "Create",
      "GetSourceSnapshotProvider",
      "GetAst",
      "GetAstUntyped",

      // Parse tree Properties
      "Location",
      "Span",
      "File",
      "PrettyPrintSource",
      "RuleDescriptor",
      "IsMissing",
      "IsAmbiguous",
      "Ambiguities",
      "AmbiguityType",
    ]);
    public this(path : string, manager : ManagerClass)
    {
      Path    = path;
      Manager = manager;
      IsUnderNitraForVisualStudio =
        match (AppDomain.CurrentDomain.GetData("NitraForVisualStudio"))
        {
          | x is bool => x
          | _         => false
        };
      _ = UpdateCurrentProjectContext();
    }

    IsListUserDataPresent[T]() : bool
    {
      def map = Manager.UserData[typeof(SCG.List[T])];
      map != null
    }

    GetUserData[T](init : T -> void = null) : T
      where T: new(), class
    {
      def key = typeof(T);
      mutable result = Manager.UserData[key] :> T;
      when (result == null)
      {
        result = T();
        when (init != null)
          init(result);
        Manager.UserData[key] = result;
      }
      result
    }

    ResetListUserData[T]() : void
    {
      Manager.UserData[typeof(SCG.List[T])] = null
    }

    UpdateCurrentProjectContext() : CompilingNitraProject
    {
      def project = GetUserData.[CompilingNitraProject](p =>
        p.Init(Manager.ReferencedAssemblies.Select(a => a.Assembly.GetName())));
      project
    }

    public static Instance(path : string, manager : ManagerClass) : NitraSourceImpl
    {
      def projPath = manager.Options.ProjectPath;
      def projDir =
        if (File.Exists(projPath))
          Path.GetFullPath(Path.GetDirectoryName(projPath))
        else if (Directory.Exists(projPath))
          Path.GetFullPath(projPath)
        else
          Environment.CurrentDirectory;
      def path =
        if (IO.Path.IsPathRooted(path))
          path;
        else
          IO.Path.Combine(projDir, path);

      if (manager.UserData.Contains(path))
        manager.UserData[path] :> NitraSourceImpl;
      else
      {
        def impl = NitraSourceImpl(path, manager);
        manager.UserData[path] = impl;
        impl;
      }
    }

    [Record]
    private class ExportableSymbolData
    {
      public TypeBuilder          : TypeBuilder         { get; }
      public ExportableSymbolType : option[TypeBuilder] { get; }
    }

    [Record]
    private class ChemicalSymbolData
    {
      public TypeBuilder          : TypeBuilder         { get; }
      public ChemicalSymbolType   : option[TypeBuilder] { get; }
    }

    public static AddExportableSymbol(tb : TypeBuilder, exportableSymbolType : option[TypeBuilder]) : void
    {
      def data = ExportableSymbolData(tb, exportableSymbolType);
      tb.Manager.GetListUserData.[ExportableSymbolData]().Add(data);
    }

    public static AddChemicalSymbol(tb : TypeBuilder, chemicalSymbolType : option[TypeBuilder]) : void
    {
      def data = ChemicalSymbolData(tb, chemicalSymbolType);
      tb.Manager.GetListUserData.[ChemicalSymbolData]().Add(data);
    }

    public BeforeInheritance() : void
    {
      when (!IO.File.Exists(Path))
      {
        Message.Error($<#The "$Path" not exists.#>);
        return;
      }

      def makeWrapperClass(source : ISource, processingCount : int) : void
      {
        // request rebuilding type tree when code changed
        if (processingCount > 1 && Manager.IsIntelliSenseMode)
          Manager.RequestOnBuildTypesTree();
        else try
        {
          def project = UpdateCurrentProjectContext();
          def file    = project.CreateNemerleFile(source);
          Parse(file)
        }
        catch { | e => Message.Error("Exception: " + e.Message); }
      }
      SubscribeSourceChangedWithCounter(Manager, Location.GetFileIndex(Path), makeWrapperClass);
    }

    private Parse(file : ProjectSystem.NemerleFile) : void
    {
      try
      {
        def parseTree = file.GetParseTree() :> G.Start;
        unless (IsUnderNitraForVisualStudio)
          file.GetCompilerMessages().TranslateTo(file.Project.CompilerMessages);

        // TODO: create parse tree when syntactic errors exist
        when (file.ParseResult.IsSuccess)
        {
          def fileParseTrees = Manager.GetListUserData.[G.Start]();
          fileParseTrees.Add(parseTree);
        }
      }
      catch
      {
        | e => Message.Error(Nemerle.Compiler.Location(file.Index, 1, 1), e.ToString());
      }
    }

    private LoadParsedSymbols(project : CompilingNitraProject, fileParseTrees : SCG.List[G.Start]) : bool
    {
      def errorCount = project.ErrorCount;

      foreach (start in fileParseTrees)
        start.CollectGlobalSymbols(project.RootNamespace);

      project.ErrorCount == errorCount
    }

    private TypeSyntaxModules(project : CompilingNitraProject, fileParseTrees : SCG.List[G.Start]) : bool
    {
      def errorCount   = project.ErrorCount;

      def context = RootContext(project, project.RootNamespace, Manager.CoreEnv);

      foreach (start in fileParseTrees)
        _ = start.MakeContext(context);

      foreach (start in fileParseTrees)
        start.TypeLanguage();

      foreach (start in fileParseTrees)
        start.Typing();

      foreach (start in fileParseTrees)
        start.TypeAst();

      foreach (start in fileParseTrees)
        start.TypeSymbolMembers();

      TypingUtils.SetOutDependentProperties(project);

      TypingUtils.SetNameReferenceSyntaxMapping(project, project.Environment.Manager);

      foreach (start in fileParseTrees)
        start.TypeSyntaxMappings();

      TypingUtils.ValidateSyntaxMapping(project);

      // HACK: force FSM build for each regular rule symbol (shall we make new pass in regular rule typing process?)
      foreach (symbol in project.Environment.Root.GetDescendants.[ParsedRegularRuleSymbol]())
        _ = symbol.Fsm;

      project.ErrorCount == errorCount
    }

    private CheckRestrictedNames(project : CompilingNitraProject) : bool
    {
      def errorCount   = project.ErrorCount;

      def check(loc : Location, name : string) : void
      {
        when (RestrictedNames.Contains(name))
          project.Error(loc, $"Name '$name' is restricted due to internal use");
      }

      foreach (syntaxModule in project.Environment.Root.GetDescendants.[ParsedSyntaxModuleSymbol]())
      {
        foreach (ruleSymbol in syntaxModule.Node.GetDescendants.[RuleSymbol]())
        {
          check(ruleSymbol.FirstLocation, ruleSymbol.Name);

          when (ruleSymbol is SyntaxRuleSymbol as s)
            foreach (ruleMethodSymbol in s.RuleMethods)
              check(ruleMethodSymbol.FirstLocation, ruleMethodSymbol.Name);

          match (ruleSymbol)
          {
            | SimpleRuleSymbol    as s with seq = s.NonVoidSubRules
            | ExtensionRuleSymbol as s with seq = s.NonVoidSubRules =>
              foreach (r in seq)
                check(r.Location, r.FieldName);

            | _ => ()
          }
        }
      }

      project.ErrorCount == errorCount
    }

    private MakeEmitters(project : CompilingNitraProject) : void
    {
      def environment = project.Environment;

      def languageEmitters     = Manager.GetListUserData.[LanguageEmitter]();
      def languageEmitterTable = SCG.Dictionary();

      def makeLanguageEmitter(languageSymbol : LanguageSymbol) : void
      {
        when (languageSymbol.AssemblyId == AssemblyIds.CurrentAssembly)
        unless (languageEmitterTable.ContainsKey(languageSymbol))
        {
          when (languageSymbol.Parent is SymbolRef.Some(parent))
            makeLanguageEmitter(parent);

          def languageEmitter = LanguageEmitter(environment, languageSymbol);
          languageEmitterTable.Add(languageSymbol, languageEmitter);
          languageEmitters.Add(languageEmitter);
        }
      }

      foreach (languageSymbol in environment.Root.GetDescendants.[LanguageSymbol]())
        makeLanguageEmitter(languageSymbol);
      
      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (syntaxModule when syntaxModule.AssemblyId == AssemblyIds.CurrentAssembly in environment.Root.GetDescendants.[SyntaxModuleSymbol]())
        syntaxModuleEmitters.Add(SyntaxModuleEmitter(environment, syntaxModule));

      def astEmitters     = Manager.GetListUserData.[AstEmitter]();
      def astEmitterTable = SCG.Dictionary();

      def makeAstEmitter(astSymbol : AstSymbol) : void
      {
        when (astSymbol.AssemblyId == AssemblyIds.CurrentAssembly)
        unless (astEmitterTable.ContainsKey(astSymbol))
        {
          foreach (baseAst in astSymbol.Parents)
            makeAstEmitter(baseAst);

          def astEmitter =
            match (astSymbol)
            {
              | AbstractAstSymbol                                  as s => AbstractAstEmitter(environment, s)
              | SimpleAstSymbol(DefinedIn = SymbolRef.None)        as s => SimpleAstEmitter(environment, s, null)
              | SimpleAstSymbol(DefinedIn = SymbolRef.Some(owner)) as s => SimpleAstEmitter(environment, s, astEmitterTable[owner])
              | _ => assert(false)
            };

          astEmitterTable.Add(astSymbol, astEmitter);
          astEmitters.Add(astEmitter);
        }
      }

      foreach (astSymbol in environment.Root.GetDescendants.[AstSymbol]())
        makeAstEmitter(astSymbol);

      def symbolEmitters = Manager.GetListUserData.[SymbolEmitter]();

      def visitedSymbolSymbols = SCG.HashSet();
      def makeSymbolSymbol(symbolSymbol : SymbolSymbol) : void
      {
        when (symbolSymbol.AssemblyId == AssemblyIds.CurrentAssembly)
        when (visitedSymbolSymbols.Add(symbolSymbol))
        {
          foreach (baseSymbol in symbolSymbol.Parents)
            makeSymbolSymbol(baseSymbol);

          symbolEmitters.Add(SymbolEmitter(environment, symbolSymbol));
        }
      }
      foreach (symbolSymbol in environment.Root.GetDescendants.[SymbolSymbol]())
        makeSymbolSymbol(symbolSymbol);

      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      def serializationEmitter   = Manager.CreateBinarySerializationEmitter(NitraBinarySerializationMessageEmitter(), NitraSymbolRefBinarySerializationEmitterExtension(environment));
      foreach (data in Manager.GetListUserData.[ExportableSymbolData]())
        externalSymbolEmitters.Add(ExternalSymbolEmitter(environment, data.TypeBuilder, data.ExportableSymbolType, serializationEmitter));
      ResetListUserData.[ExportableSymbolData]();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      foreach (data in Manager.GetListUserData.[ChemicalSymbolData]())
        chemicalSymbolEmitters.Add(ChemicalSymbolEmitter(environment, data.TypeBuilder, data.ChemicalSymbolType));
      ResetListUserData.[ChemicalSymbolData]();
    }

    private DefineParseTree() : void
    {
      def languageEmitters = Manager.GetListUserData.[LanguageEmitter]();
      foreach (emitter in languageEmitters)
        emitter.DefineTypes();
      foreach (emitter in languageEmitters)
        emitter.DefineMembers();

      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineSimpleParseTreeTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineExtensionParseTreeTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParseTreeMembers();
    }

    private DefineParsers() : void
    {
      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParserTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParserMethods();
    }

    private DefineSymbols() : void
    {
      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      foreach (emitter in externalSymbolEmitters)
        emitter.DefineMembers();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      foreach (emitter in chemicalSymbolEmitters)
        emitter.DefineMembers();
    }

    private DefineAsts() : void
    {
      def astEmitters    = Manager.GetListUserData.[AstEmitter]();
      def symbolEmitters = Manager.GetListUserData.[SymbolEmitter]();

      foreach (emitter in astEmitters)
        emitter.DefineTypes();
      foreach (emitter in symbolEmitters)
        emitter.DefineTypes();

      foreach (emitter in astEmitters)
        emitter.DefineMembers();
      foreach (emitter in symbolEmitters)
        emitter.DefineMembers();
    }

    private EmitSymbols() : void
    {
      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      try
      {
        foreach (emitter in externalSymbolEmitters)
          emitter.Emit();
        when (!Manager.IsIntelliSenseMode && externalSymbolEmitters.Count > 0)
        {
          def buffer = SCG.List();
          foreach (emitter in externalSymbolEmitters)
            buffer.Add(<[ typeof($(emitter.TargetSymbolType.GetMemType() : typed)) ]>);
          Manager.Hierarchy.AddAssemblyAttribute(Manager.CoreEnv, <[ Nitra.Serialization.ExportableSymbolsAttribute(array[..$buffer]) ]>);
        }
        Manager.FinalizeBinarySerializationEmitter();
      }
      finally ResetListUserData.[ExternalSymbolEmitter]();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      try
        foreach (emitter in chemicalSymbolEmitters)
          emitter.Emit();
      finally ResetListUserData.[ChemicalSymbolEmitter]();
    }

    private EmitSyntaxModules() : void
    {
      def languageEmitters = Manager.GetListUserData.[LanguageEmitter]();
      try
      {
        foreach (emitter in languageEmitters)
          emitter.Emit();
        when (!Manager.IsIntelliSenseMode && languageEmitters.Count > 0)
        {
          def buffer = SCG.List();
          foreach (emitter in languageEmitters)
            buffer.Add(<[ typeof($(emitter.Environment.GetLanguageType(emitter.Symbol) : typed)) ]>);
          Manager.Hierarchy.AddAssemblyAttribute(Manager.CoreEnv, <[ Nitra.LanguagesAttribute(array[..$(buffer.ToNList())]) ]>);
        }
      }
      finally ResetListUserData.[LanguageEmitter]();

      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      try
      {
        foreach (emitter in syntaxModuleEmitters)
          emitter.Emit();
        when (!Manager.IsIntelliSenseMode && syntaxModuleEmitters.Count > 0)
        {
          def buffer = SCG.List();
          foreach (emitter in syntaxModuleEmitters)
            buffer.Add(<[ typeof($(emitter.GrammarTB.GetMemType() : typed)) ]>);
          Manager.Hierarchy.AddAssemblyAttribute(Manager.CoreEnv, <[ Nitra.GrammarsAttribute(array[..$(buffer.ToNList())]) ]>);
        }
      }
      finally ResetListUserData.[SyntaxModuleEmitter]();
    }

    private EmitAsts() : void
    {
      def symbolEmitters = Manager.GetListUserData.[SymbolEmitter]();
      try
        foreach (emitter in symbolEmitters)
          emitter.Emit();
      finally ResetListUserData.[SymbolEmitter]();

      def astEmitters = Manager.GetListUserData.[AstEmitter]();
      try
        foreach (emitter in astEmitters)
          emitter.Emit();
      finally ResetListUserData.[AstEmitter]();
    }

    private ReadMetadata(project : CompilingNitraProject) : bool
    {
      def errorCount = project.ErrorCount;

      using (loader = ExternalSymbolLoader())
      {
        loader.Registry.Register(typeof(SyntaxModuleSymbol).Assembly);
        def externalSymbolPropertyReader = NitraExternalSymbolPropertyReader();

        foreach (reference in Manager.ReferencedAssemblies)
        {
          def assembly       = reference.Assembly;
          def metadataStream = assembly.GetManifestResourceStream("Nitra.Metadata");
          when (metadataStream != null)
          {
            def assemblyId = project.GetAssemblyId(assembly.GetName());
            def location   = project.MakeFakeNitraLocation(Nemerle.Compiler.Location(assembly.Location, 1, 1, 1, 1));
            def reader     = MetadataReader(metadataStream, loader.Registry, externalSymbolPropertyReader, project, assemblyId, location);
            loader.AddSource(reader, location);
          }
        }

        loader.Load();
      }

      project.ErrorCount == errorCount
    }

    private WriteMetadata(project : Project) : void
    {
      def metadataFileName = Manager.Options.OutputFileName + ".nitrametadata";
      using(symbolWriter = MetadataWriter(FileStream(metadataFileName, FileMode.Create, FileAccess.Write), project))
        symbolWriter.WriteSymbols();

      Manager.Options.EmbeddedResources ::= metadataFileName + ",Nitra.Metadata";
    }

    public BeforeTypedMembers() : void
    {
      when (IsListUserDataPresent.[G.Start]())
        try
        {
          //assert2(false);
          def project  = UpdateCurrentProjectContext();
          def fileParseTrees = Manager.GetListUserData.[G.Start]();

          unless (ReadMetadata(project))
            throw Recovery();
          unless (LoadParsedSymbols(project, fileParseTrees))
            throw Recovery();
          unless (TypeSyntaxModules(project, fileParseTrees))
            throw Recovery();
          unless (CheckRestrictedNames(project))
            throw Recovery();

          MakeEmitters(project);

          DefineAsts();
          DefineParseTree();
          DefineParsers();

          def writeMetadataCallback(_)
          {
            WriteMetadata(project);
            Manager.BeforeSaveAssembly -= writeMetadataCallback;
          }
          unless (Manager.IsIntelliSenseMode)
            Manager.BeforeSaveAssembly += writeMetadataCallback;
        }
        finally ResetListUserData.[G.Start]();
    }

    public WithTypedMembers() : void
    {
      DefineSymbols();

      EmitSymbols();
      EmitSyntaxModules();
      EmitAsts();
    }

    public SubscribeSourceChangedWithCounter(manager : ManagerClass, fileIndex : int, handler : ISource * int -> void) : void
    {
      mutable counter = 0;
      manager.SubscribeSourceChanged(fileIndex, source =>
      {
        counter += 1;
        handler(source, counter);
      });
    }
  }
}
