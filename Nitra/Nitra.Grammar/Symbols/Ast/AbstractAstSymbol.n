using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using G   = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class AbstractAstSymbol : AstSymbol, INamespaceMember
  {
    public override Description : string         { get { if (IsDeclaration) "abstract declaration" else "abstract ast" } }
    public override Cardinality : AstCardinality { get { AstCardinality.Single } }
    public override IsAbstract  : bool           { get { true } }
  }

  public sealed class ParsedAbstractAstSymbol : AbstractAstSymbol, IDependentPropertyAssignmentContainer
  {
    private         _node                     : G.NamespaceMember.Ast;
    private         _assignments              : SCG.List[DependentPropertyAssignment];
    private mutable _initializedOutProperties : list[DependentPropertySymbol];

    public this(node : G.NamespaceMember.Ast, isDeclaration : bool)
    {
      base(node.Name, node.Name.Value());
      _node                     = node;
      _assignments              = SCG.List();
      _initializedOutProperties = [];
      IsDeclaration             = isDeclaration;
    }

    private mutable _symbol             : SymbolRef[SymbolSymbol];
    private mutable _propertyDependency : list[PropertyDependencyNodeData];

    public override Symbol                   : SymbolRef[SymbolSymbol]       { get { assert(_symbol : object != null); _symbol } }
    public override IsDeclaration            : bool                          { get; }
    public override BaseAsts                 : list[AstSymbol]               { get { _node.BaseAsts } }
    public override InitializedOutProperties : list[DependentPropertySymbol] { get { _initializedOutProperties } }
    public override HasEvaluations           : bool                          { get { _assignments.Count > 0 } }
    public override PropertyDependency       : list[PropertyDependencyNodeData] { get { assert(_propertyDependency != null); _propertyDependency } }

    public AddAssignment(assignment : DependentPropertyAssignment) : void { AddAssignmentImpl(assignment, _assignments, ref _initializedOutProperties); }
    public GetAssignments() : Seq[DependentPropertyAssignment] { _assignments }
    public override SetPropertyDependency(graph : list[PropertyDependencyNodeData]) : void { assert(graph != null); _propertyDependency = graph; }

    protected override CreateSymbolSymbol() : SymbolRef[SymbolSymbol]
    {
      assert(_symbol : object == null);

      _symbol =
        CreateSymbolSymbolImpl(fun (baseSymbols)
        {
          def symbolSymbol =
            if (_node.Body.IsSymbolSealed())
              ParsedSimpleSymbolSymbol(this, baseSymbols)
            else
              ParsedAbstractSymbolSymbol(this, baseSymbols);
          _ = Node.Parent.AddAndReport.[SymbolSymbol](symbolSymbol, FirstLocation);
          symbolSymbol
        });
      _node.Body.CollectSymbolMembers(this, _symbol);
      _symbol
    }
  }
}
