using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Ast;
using Nitra.Declarations;
using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra
{
  module AstUtils
  {
    public MakeMembersScope(currentScope : Scope, opening : list[Scope], aliases : TableScope) : Scope
    {
      def makeCurrentScope(scope : Scope) : Scope
      {
        def withAliases =
          if (aliases.Symbols.Count == 0)
            scope
          else
            scope.UnionWith(aliases);

        def withUsings =
          match (opening)
          {
            | []    => withAliases
            | [one] => one.HideWith(withAliases)
            | _     => Scope.Union(opening).HideWith(withAliases)
          };

        withUsings
      }

      if (currentScope is HidingScope as h)
        h.Hidden.HideWith(makeCurrentScope(h.Scope))
      else
        makeCurrentScope(currentScope)
    }

    public MakeEnteredScope(parentScope : Scope, parentNs : NamespaceSymbol, enteredNs : NamespaceSymbol) : Scope
    {
      def buildEnterNsScope(ns : NamespaceSymbol) : Scope
      {
        def parent = ns.Parent;
        def scope  = ns.Scope;

        if (parent : object == parentNs)
          parentScope.HideWith(scope)
        else
          buildEnterNsScope(parent).HideWith(scope)
      }
      
      buildEnterNsScope(enteredNs)
    }
    
    [Memoize]
    public TypeOverloadResolution(argumentsCount : int) : ResolutionAlgorithm[ISymbol2, ISymbol2]
    {
      def resolve(candidate : ISymbol2) : ValueOption[ISymbol2]
      {
        //| symbol is BuiltInTypeAliasSymbol    when symbol.IsTypeEvaluated                       => resolve(symbol.Type)
        //| symbol is GenericTypeSymbol         when argumentsCount == symbol.TypeParametersCount => VSome(symbol)
        | symbol is UsingAliasDirectiveSymbol           when argumentsCount == 0                          => VSome(symbol)
        | symbol is NamespaceSymbol                     when argumentsCount == 0                          => VSome(symbol)
        | symbol is SyntaxModuleSymbol                  when argumentsCount == 0                          => VSome(symbol)
        | symbol is NamespaceMemberDeclarationSymbol    when argumentsCount == 0                          => VSome(symbol)
        | symbol is SyntaxModuleMemberDeclarationSymbol when argumentsCount == 0                          => VSome(symbol)
        | _ => VNone()
      }
      resolve
    }

    public CreateMissingQualifiedReference(file : File, span : NSpan) : QualifiedReference
    {
      QualifiedReference.Simple(
        File = file,
        Span = span,
        Name = Reference.AmbiguousOrMissing(file, span))
    }

    public UpdateLiterals(inMap : Map[string, string], literals : StringLiteral.IAstList, name : string) : Map[string, string]
    {
      when (string.IsNullOrEmpty(name))
        return inMap;

      def xs = literals.Select(x => x.Value.ValueOrDefault).Where(x => !string.IsNullOrEmpty(x)).Select(x => (x, name));
      inMap.ReplaceRange(xs)
    }

    public NMakeLiteralName(value : string, literals : Map[string, string]) : string
    {
      if (literals.Find(value) is Some(name))
        name
      else if (Utils.IsIdentifier(value))
        Utils.ToPascalCase(value, "Kw")
      else
        ""
    }
  } // module
} // namespace
