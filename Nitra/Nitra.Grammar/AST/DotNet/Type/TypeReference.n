using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra;
using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;
using Nitra.Serialization2;

using System;
using System.Collections.Generic;
using System.Linq;

namespace DotNet
{
  [Record]
  public sealed class TypeReference : ITypeReference, IEquatable[TypeReference]
  {
    public File      : File                { get; }
    public Span      : NSpan               { get; }
    public Ref       : IRef                { get; }
    public Arguments : list[TypeReference] { get; }

    public Name     : string { get { Ref.Name } }
    public FullName : string
    {
      get
      {
        if (Arguments.IsEmpty)
          $<#$(Ref.FullName)#>
        else
          $<#$(Ref.FullName)[..$(Arguments; ", "; _.FullName)]#>
      }
    }

    public IsClass : bool
    {
      get
      {
        if (Ref.IsSymbolEvaluated)
        {
          match (Ref.Symbol)
          {
            | TopClassSymbol | NestedClassSymbol => true
            | _ => false
          }
        }
        else false
      }
    }

    public IsInterface : bool
    {
      get
      {
        if (Ref.IsSymbolEvaluated)
        {
          match (Ref.Symbol)
          {
            | TopInterfaceSymbol | NestedInterfaceSymbol => true
            | _ => false
          }
        }
        else false
      }
    }

    public IsTypeParameter : bool
    {
      get
      {
        if (Ref.IsSymbolEvaluated)
        {
          match (Ref.Symbol)
          {
            | TypeParameterSymbol => true
            | _ => false
          }
        }
        else false
      }
    }

    public override ToString() : string { FullName }

    public Equals(other : TypeReference) : bool
      implements IEquatable[TypeReference].Equals
    {
      | null => false
      | _ => this.Ref.Equals(other.Ref) && this.Arguments.Equals(other.Arguments)
    }

    public override Equals(other : object) : bool
    {
      | TypeReference as other => Equals(other)
      | _ => false
    }

    public override GetHashCode() : int
    {
      Ref.GetHashCode() ^ Arguments.GetHashCode()
    }

    public static Create(r : IRef) : TypeReference
    {
      if (r.IsSymbolEvaluated)
      {
        match (r.Symbol)
        {
          | UsingAliasDirectiveSymbol(Type = originalType) => TypeReference(r.File, r.Span, originalType.Ref, originalType.Arguments)
          | _ => TypeReference(r.File, r.Span, r, [])
        }
      }
      else
        TypeReference(r.File, r.Span, r, [])
    }

    public static Create(r : IRef, arguments : IList[TypeReference]) : TypeReference
    {
      TypeReference(r.File, r.Span, r, arguments.NToList())
    }

    public Serialize(writer : IO.BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      metadataWriter.WriteObject(File, writer);
      metadataWriter.WriteObject(Ref, writer);
      metadataWriter.WriteObjects(Arguments, writer);
    }

    public static Deserialize(reader : IO.BinaryReader, metadataReader : MetadataReader) : this
    {
      def file = metadataReader.ReadObject(reader);
      def r    = metadataReader.ReadObject(reader);
      def args = metadataReader.ReadObjects(reader);
      TypeReference(file, NSpan(), r, args.ToNList())
    }
  }
}
