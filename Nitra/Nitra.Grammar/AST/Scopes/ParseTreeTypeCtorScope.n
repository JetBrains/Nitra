using Nitra;
using Nitra.Declarations;
using Nitra.Serialization2;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Linq;

namespace Nitra.Ast
{
  public sealed class ParseTreeTypeCtorScope : Scope
  {
    public static Instance : ParseTreeTypeCtorScope = ParseTreeTypeCtorScope();
    public static SupportedTypes : ImmutableArray[Reference];

    static this()
    {
      def loc = Location();
      SupportedTypes = ImmutableArray.Create(Reference(loc, "Some"), Reference(loc, "None"), Reference(loc, "Cons"), Reference(loc, "Nil"));
    }
    
    private this()
    {
    }

    // TODO: add type subst
    public override AllSymbols : Seq[DeclarationSymbol] { get { [] } } 

    public override Bind[TSymbol](reference : Reference) : BindResult[TSymbol]
    {
      match (TryCreateTypeCtor(reference))
      {
        | s is TSymbol => BindResult.Single(s)
        | _ => BindResult.Nil()
      }
    }
    
    public override Bind[TSymbol](isMatch : Predicate[TSymbol]) : BindResult[TSymbol]
    {
      def res = LightList();

      foreach (type in SupportedTypes)
      {
        def sym = TryCreateTypeCtor(type);
        assert(sym != null);
        match (sym)
        {
          | ctor is TSymbol when isMatch(ctor) => res.Add(ctor);
          | _ => ()
        }
      }

      res.BindResult()
    }
    
    public override ToDot(nodeToIdMap : Dictionary[object, int], nodes : List[string], _edges : List[string]) : void
    {
      def id            = nodeToIdMap.GetObjectId(this);

      nodes.Add($<#Obj_$id [label="ParseTreeTypeCtorScope"]#>);
    }

    private TryCreateTypeCtor(reference : Reference) : DeclarationSymbol
    {
      match (reference.Text)
      {
        | "Some" =>
          def context = DependentPropertyEvalContext();
          def typeVar = AstUtils.CreateRuleTypeVar(context);
          AstUtils.CreateFunctionType(reference, array[typeVar], AstUtils.InitOption(context, typeVar), context)

        | "None" =>
          def context = DependentPropertyEvalContext();
          def typeVar = AstUtils.CreateRuleTypeVar(context);
          AstUtils.CreateFunctionType(reference, array[], AstUtils.InitOption(context, typeVar), context)

        | "Cons" =>
          def context = DependentPropertyEvalContext();
          def typeVar = AstUtils.CreateRuleTypeVar(context);
          def lst     = AstUtils.InitList(context, typeVar);
          AstUtils.CreateFunctionType(reference, array[typeVar, lst], lst, context)

        | "Nil"  =>
          def context = DependentPropertyEvalContext();
          def typeVar = AstUtils.CreateRuleTypeVar(context);
          AstUtils.CreateFunctionType(reference, array[], AstUtils.InitList(context, typeVar), context)

        | _      => null
      }
    }

    public override ToString() : string
    {
      "pattern scope"
    }

    public override Serialize(writer : IO.BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      IgnoreParams();
    }

    public static Deserialize(reader : IO.BinaryReader, metadataReader : MetadataReader) : this
    {
      IgnoreParams();
      Instance
    }
  }
}
