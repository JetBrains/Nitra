using Nitra;
using Nitra.Declarations;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Internal;
using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Ast
{
  public abstract class RuleTypeScope : Scope
  {
    private mutable _members  : BindResult[DeclarationSymbol];

    protected abstract CreateMembers() : BindResult[DeclarationSymbol];

    Init() : void
    {
      when (_members : object == null)
        _members = CreateMembers();
    }
    
    // TODO: add type subst
    public override AllSymbols : Seq[DeclarationSymbol]
    {
      get
      {
        Init();
        _members.Symbols
      }
    } 

    public override Bind[TSymbol](reference : Reference) : BindResult[TSymbol]
    {
      def id = reference.Id;
      
      when (id <= StringIndex.InvalidId)
        return BindResult.Nil();

      Init();
      
      def isMatch(symbol : TSymbol) : bool
      {
        symbol.NameId == id
      }
      _members.Filter.[TSymbol](isMatch);

    }

    public override Bind[TSymbol](isMatch : Predicate[TSymbol]) : BindResult[TSymbol]
    {
      _members.Filter(isMatch)
    }

    public override ToDot(nodeToIdMap : Dictionary[object, int], nodes : List[string], edges : List[string]) : void
    {
      Init();
      
      def id = nodeToIdMap.GetObjectId(this);

      nodes.Add($<#Obj_$id [label="RuleTypeScope"]#>);
      foreach (symbol in AllSymbols)
      {
        def symbolId = nodeToIdMap.GetObjectId(symbol);
        def symbolName = symbol.InitialName;
        nodes.Add($<#Sym_$symbolId [label="$symbolName", shape=box, style=filled, color=lightskyblue]#>);
        edges.Add($<#Obj_$id -> Sym_$symbolId#>);
      }
    }

    public override ToString() : string
    {
      "rule"
    }
  }
}
