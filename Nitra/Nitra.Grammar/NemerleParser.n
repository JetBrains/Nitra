using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Runtime;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxAst;

namespace Nitra
{
  public module NemerleParser
  {
    public Parse(body : G.RuleMethodBody, context : Context) : PExpr
    {
      match (body)
      {
        | G.RuleMethodBody.Block      as b => ParseImpl(b.Block, context)
        | G.RuleMethodBody.Expression as b => ParseImpl(b.Expression, context)
        | G.RuleMethodBody.Empty           => <[ () ]>
        | _                                => <[ () ]>
      }
    }

    public ParseParameter(context : Context, name : Nitra.Ast, type : Nitra.Ast) : PParameter
    {
      match (ParseImpl(name, context))
      {
        | <[ $(n : name) ]> => <[ parameter: $n : $(ParseImpl(type, context)) ]>
        | _ => Project.CurrentProject.FatalError(name, "Expected simple name.", -1)
      }
    }

    public Parse(parms : list[G.RuleMethodsParam], context : Context) : list[PParameter]
    {
      def parsed = List();
      foreach (parm in parms)
      {
        | G.RuleMethodsParam.Ast as p => parsed.Add(ParseParameter(context, p.Name, p.Type))
        | e => _ = Project.CurrentProject.FatalError.[object](e, "Parse error.", -1)
      }
      parsed.ToNList()
    }

    public Parse(type : G.Type, context : Context) : PExpr
    {
      ParseImpl(type, context)
    }

    ParseImpl(ast : Nitra.Ast, context : Context) : PExpr
    {
      def env = context.GetNemerleEnvironment();
      def simpleParse(ast : Nitra.Ast) : PExpr
      {
        def text = ast.Location.GetText();
        MainParser.ParseExpr(env, text, ast.NLocation())
      }
      def parseAndAddIt(ast : Nitra.Ast) : PExpr
      {
        def e = simpleParse(ast);

        def addIt(e)
        {
          | <[ value ]> => e
          | <[ $(n : name) ]> => <[ it.$(n : name)]>
          | <[ $x = $y ]> => <[ $(addIt(x)) = $(addIt(y)) ]>
          | _ => e
        }

        addIt(e)
      }

      match (ast)
      {
        | G.Expression.Ast(ExpressionContents=[G.ExpressionContent.Content1(Content=G.Content.Fold as fold)]) =>
          def collection = simpleParse(fold.Identifier);
          def expr1 = simpleParse(fold.Expression);
          def exprs = fold.Expressions[0].Map(parseAndAddIt);
          //exprs.Fold()
          <[ $collection.Fold($expr1, fun((it, value)) { ..$exprs }) ]>

        | _ => simpleParse(ast)
      }
    }
  }
}
