using Nitra.Model;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal NTypeToAstTypeWithMessage(nitraType : G.NitraType, context : Context) : AstType
    {
      def t      = nitraType.Typing(context)[0];
      def result = NTypeToAstType(t, context);
      when (result.IsVoid)
        context.Error(nitraType, $"Type '$t' is not valid AST type");
      result
    }

    internal NTypeToAstType(t : NType, context : Context) : AstType
    {
      match (t)
      {
        | Qualified as name =>
          if (context.TryBind.[AstSymbol](name) is SymbolRef.Some(astSymbol))
            AstType.Ast(t.Location, astSymbol)
          else
          {
            AstType.Void(t.Location)
          }

        | OptionList(Qualified as name) =>
          if (context.TryBind.[AstSymbol](name) is SymbolRef.Some(astSymbol))
            AstType.OptionList(t.Location, astSymbol)
          else
            AstType.Void(t.Location)

        | List(Qualified as name) =>
          if (context.TryBind.[AstSymbol](name) is SymbolRef.Some(astSymbol))
            AstType.List(t.Location, astSymbol)
          else
            AstType.Void(t.Location)

        | Option(Qualified as name) =>
          if (context.TryBind.[AstSymbol](name) is SymbolRef.Some(astSymbol))
            AstType.Option(t.Location, astSymbol)
          else
          {
            AstType.Void(t.Location)
          }

        | _ => AstType.Void(t.Location)
      }
    }
  }
}
