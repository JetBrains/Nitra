using Nitra;
using Nitra.Utils;
using Nitra.Grammar.TypingUtils;
using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Runtime;
using Nitra.Typing;

using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Compiler.Parsetree;

using System.Collections.Generic;
using System.Text;
using System.Linq;

syntax module NitraSyntax
{
  using Outline;
  using PrettyPrint;
  using TokenNames;
  using StandardSpanClasses;
  using Whitespaces;
  using Identifiers;
  using CStyleComments;

  literal Prefix              = "\\u";
  literal DodDot              = "..";
  literal Op                  = ".";
  literal Pipe                = "|";

  braces "(", ")";
  braces "{", "}";
  braces "[", "]";

  keyword regex IdentifierStartCharacter (IdentifierPartCharacters | '-')* rule S;

  alias Name            = Identifier;
  alias QualifiedName   = QualifiedIdentifier;
  alias NamespaceOrType = QualifiedIdentifier;
  alias Char            = Any;
  alias GrammarName     = QualifiedIdentifier;
  alias BaseName        = QualifiedIdentifier;

  regex PredefinedType = "void" | "int" | "string";

  regex Keyword = "as" | "syntax" | "marker" | "token" | "field" | "span" | "alias" | "regex" | "literal"
                | "in" | "out" | "rule" | "precedence" | "using" | "namespace" | PredefinedType;

  extend token IgnoreToken
  {
    | [SpanClass(Comment)] SingleLineComment;
    | [SpanClass(Comment)] MultiLineComment;
  }

  regex HexDigit                  = ['0'..'9', 'a'..'f', 'A'..'F'];
  regex ReservedStringChar        = "\"" | "\\";
  regex ReservedCharChar          = "\'" | "\\";

  [SpanClass(String)]
  token StringLiteral                    = "\""  StringPart* "\""
  {
    Value() : string = FoldThrough(StringParts, StringBuilder(), _.Value(_)).ToString();
  }

  token StringPart
  {
    Value(sb : StringBuilder) : StringBuilder;

    | UnicodeEscapeSequence = "\\u" HexDigit HexDigit HexDigit HexDigit
      {
        override Value = sb.Append(HexToChar(this, HexDigit1.StartPos, HexDigit4.EndPos));
      }
    | EscapeSequence        = "\\"  Char
      {
        override Value = sb.Append(UnescapeChar(FirstChar(Char)));
      }
    | Simple                = Chars=(!ReservedStringChar !NewLine Any)+
      {
        override Value = sb.Append(this.GetText());
      }
  }

  [SpanClass(Char)]
  token CharLiteral                      = "\'"  CharPart "\'" { Value() : char = CharPart.Value(); }

  token CharPart
  {
    Value() : char;

    | Simple                = !ReservedCharChar Char
      {
        override Value = FirstChar(this.Char);
      }
    | UnicodeEscapeSequence = "\\u" HexDigit HexDigit HexDigit HexDigit
      {
        override Value = HexToChar(this, HexDigit1.StartPos, HexDigit4.EndPos);
      }
    | EscapeSequence        = "\\" Char
      {
        override Value = UnescapeChar(FirstChar(this.Char));
      }
  }

  regex Digits = ['0'..'9']+;

  [SpanClass(Number)]
  token Number = Digits { Value() : int = DecToInt(this, Digits.StartPos, Digits.EndPos); }

  token Identifier                       = !(Keyword !IdentifierPartCharacters) IdentifierBody
  {
    Value() : string = GetText(IdentifierBody);
    Identifier() : Nitra.Model.Identifier = Nitra.Model.Identifier(this, IdentifierBody);
  }

  [ExplicitSpaces]
  syntax QualifiedIdentifier              = Names=(Identifier; s "." s)+
  {
    Identifiers() : list[Nitra.Located] = Names.Item1.MapToList(_.Identifier());
    Parts() : list[string] = Names.Item1.MapToList(_.Value());
    CalcLocation() : Nitra.Location
    {
      this.Location // At present QualifiedIdentifier have no 's' at the end of rule. This allows the use rule location.
    }
    CalcLocations() : list[Nitra.Location] = Names.Item1.MapToList(_.Location);
  }

  ///////////////////////////////////////////////////////////////////////////
  //

  [StartRule, ExplicitSpaces]
  syntax Start                = s NamespaceBody !Any
  {
    CollectGlobalSymbols(rootNs : NamespaceSymbol) : void = NamespaceBody.CollectGlobalSymbols(rootNs);
    [Cached] MakeContext(parent : Context) : Context = NamespaceBody.MakeContext(parent);
    Typing() : void = NamespaceBody.Typing();
  }

  syntax Usings = outline_hiden outline_begin Using* outline_end_before
  {
    MakeContext(parent : Context) : Context = FoldThrough(Usings, parent, _.MakeContext(_));
  }

  syntax NamespaceBody = Usings NamespaceMember*
  {
    CollectGlobalSymbols(rootNs : NamespaceSymbol) : void = foreach (topDecl in NamespaceMembers) topDecl.CollectGlobalSymbols(rootNs);
    [Cached] MakeContext(parent : Context) : Context
    {
      def context1 = Usings.MakeContext(parent);
      def context2 = PassThrough(NamespaceMembers, context1, _.MakeContext(_));
      context2
    }
    Typing() : void = foreach (topDecl in NamespaceMembers) topDecl.Typing();
  }

  syntax Using
  {
    [Cached] MakeContext(parent : Context) : Context;

    | UsingOpen  = "using" sm NamespaceOrType ";" nl
      {
        override MakeContext = OpenNamespaceOrTypeContext(parent, NamespaceOrType);
      }
    | UsingAlias = "using" Name sm "=" sm NamespaceOrType ";" nl
      {
        override MakeContext = NamespaceOrTypeAliasContext(parent, Name, NamespaceOrType);
      }
  }

  syntax NitraTypeArguments = "<" (NitraType; "," sm; ?)+ ">"
  {
    Typing() : list[NType]
    {
      def buffer = List();
      foreach (t in this.NitraTypes.Item1)
        buffer.Add(t.Typing()[0]);
      buffer.ToNList()
    }
  }

  syntax NitraTypeName = Identifier NitraTypeArguments?
  {
    Typing() : TypeNamePart
    {
      def name     = this.Identifier.GetText();
      def typeArgs = if (this.NitraTypeArgumentsOpt is Some(ta)) ta.Typing() else [];
      TypeNamePart(name, typeArgs)
    }
  }

  syntax NitraType
  {
    Typing() : NType * bool;

    | PredefinedType
      {
        override Typing
        {
          def value = match (GetText(this.PredefinedType))
          {
            | "void"   => NPredefinedType.Void
            | "int"    => NPredefinedType.Int
            | "string" => NPredefinedType.String
            | _        => assert3(false)
          };
          (NType.Predefined(value), false);
        }
      }
    | QualifiedType  = (NitraTypeName; ".")+
      {
        override Typing
        {
          def parts = List();
          foreach (t in this.NitraTypeNames.Item1)
            parts.Add(t.Typing());
          (NType.Qualified(parts.ToNList()), false);
        }
      }
    | Braces         = "(" NitraType ")"
      {
        override Typing = (this.NitraType.Typing()[0], false);
      }
    | Tuple          = NitraType "*" NitraType     precedence 40  right-associative
      {
        override Typing
        {
          def first = this.NitraType1.Typing()[0];
          match (this.NitraType2.Typing())
          {
            | (NType.Tuple(members), true) => (NType.Tuple(first :: members), true)
            | (second, _)                  => (NType.Tuple([first, second]), true)
          }
        }
      }
    | Func           = NitraType "->" NitraType    precedence 30  right-associative
      {
        override Typing = (NType.Func(this.NitraType1.Typing()[0], this.NitraType2.Typing()[0]), false);
      }
    | OptionList     = NitraType "*" precedence 50
      {
        override Typing = (NType.OptionList(this.NitraType.Typing()[0]), false);
      }
    | List           = NitraType "+" precedence 50
      {
        override Typing = (NType.List(this.NitraType.Typing()[0]), false);
      }
    | Option         = NitraType "?" precedence 50
      {
        override Typing = (NType.Option(this.NitraType.Typing()[0]), false);
      }
  }

  syntax NitraField = Name ":" NitraType RuleMethodBody
  {
    CollectGlobalSymbols(site : IMemberSite) : void
    {
      def symbol = ParsedNitraFieldSymbol(this);
      _ = site.Node.AddAndReport(symbol, Name.Location);
    }
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeType() : NType = this.NitraType.Typing()[0];
    [Cached] private MakeBody() : RuleMethod.RuleMethodBody = RuleMethod.RuleMethodBody(this.RuleMethodBody.BodyLocation(), NemerleParser.Parse(this.RuleMethodBody, Context));
    Typing() : void
    {
      _ = MakeType();
      _ = MakeBody();
    }
  }

  syntax DeclarationMember
  {
    CollectGlobalSymbols(site : IMemberSite) : void;
    [Cached] MakeContext(parent : Context) : Context;
    Typing() : void;

    | NitraField
      {
        override CollectGlobalSymbols = NitraField.CollectGlobalSymbols(site);
        override MakeContext          = NitraField.MakeContext(parent);
        override Typing               = NitraField.Typing();
      }
  }

  syntax SymbolMember
  {
    CollectGlobalSymbols(site : IMemberSite) : void;
    [Cached] MakeContext(parent : Context) : Context;
    Typing() : void;

    | NitraField
      {
        override CollectGlobalSymbols = NitraField.CollectGlobalSymbols(site);
        override MakeContext          = NitraField.MakeContext(parent);
        override Typing               = NitraField.Typing();
      }
  }

  syntax NamespaceMember
  {
    CollectGlobalSymbols(ns : NamespaceSymbol) : void;
    [Cached] MakeContext(parent : Context) : Context;
    Typing() : void;

    | Symbol      = "symbol"      sm Name outline_begin_before nl "{" inl Members=SymbolMember*      d "}" outline_end_before nl
      {
        override CollectGlobalSymbols
        {
          def symbol = ParsedSymbolSymbol(this);
          _ = ns.Node.AddAndReport(symbol, Name.Location);
          foreach (decl in Members)
            decl.CollectGlobalSymbols(symbol);
        }
        [Cached] private MakeInitialContext(contextToSave : SymbolContext) : SymbolContext = contextToSave;
        override MakeContext
        {
          def  context1 = SymbolContext(parent, Name);
          def _context2 = PassThrough(Members, context1, _.MakeContext(_));
          parent
        }
        override Typing = foreach (member in Members) member.Typing();
      }

    | Declaration = "declaration" sm Name outline_begin_before nl "{" inl Members=DeclarationMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols
        {
          def symbol = ParsedDeclarationSymbol(this);
          _ = ns.Node.AddAndReport(symbol, Name.Location);
          foreach (decl in Members)
            decl.CollectGlobalSymbols(symbol);
        }
        [Cached] private MakeInitialContext(contextToSave : DeclarationContext) : DeclarationContext = contextToSave;
        override MakeContext
        {
          def  context1 = DeclarationContext(parent, Name);
          def _context2 = PassThrough(Members, context1, _.MakeContext(_));
          parent
        }
        override Typing = foreach (member in Members) member.Typing();
      }

    | Namespace   = "namespace"   sm QualifiedName outline_begin_before nl "{" inl NamespaceBody      d "}" outline_end_before nl
      {
        [Cached] private MakeCurrentSymbol(ns : NamespaceSymbol) : NamespaceSymbol = MakeNamespaceSymbol(ns, QualifiedName.CalcLocations());
        override CollectGlobalSymbols = NamespaceBody.CollectGlobalSymbols(MakeCurrentSymbol(ns));
        override MakeContext
        {
          def nsContext = EnteredNamespaceContext(parent, CurrentSymbol);
          _ = NamespaceBody.MakeContext(nsContext);
          parent
        }
        override Typing = NamespaceBody.Typing();
      }

    | SyntaxModule = SyntaxModuleAttributes "syntax" sm "module"sm Name outline_begin_before nl "{" inl Usings SyntaxModuleMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols
        {
          def symbol = ParsedSyntaxModuleSymbol(this);
          _ = ns.Node.AddAndReport(symbol, Name.Location);
          foreach (decl in SyntaxModuleMembers)
            decl.CollectGlobalSymbols(symbol);
        }
        [Cached] private Options() : SyntaxModuleOptions = SyntaxModuleAttributes.Options(SyntaxModuleOptions.Empty);
        [Cached] private MakeInitialContext(contextToSave : SyntaxModuleContext) : SyntaxModuleContext = contextToSave;
        [Cached] private MakeFinalContext(contextToSave : Context) : Context = contextToSave;
        override MakeContext
        {
          def context1 = MakeInitialContext(SyntaxModuleContext(parent, Name));
          def context2 = Usings.MakeContext(context1);
          def context3 = SyntaxModuleBodyContext(context2);
          def finalContext = MakeFinalContext(FoldThrough(SyntaxModuleMembers, context3, _.MakeContext(_)));
          Utils.RegisterGlobalSpanClassSymbols(context1.SyntaxModuleSymbol, finalContext);
          parent
        }
        [Cached] private MakeTokenLiteralNames() : Map[string, Nitra.Model.Identifier] = FoldThrough(SyntaxModuleMembers, Map(), _.GetTokenLiteralNames(_));
        override Typing
        {
          _ = MakeTokenLiteralNames();
          foreach (decl in SyntaxModuleMembers)
            decl.Typing();
        }
      }
  }

  syntax SyntaxModuleAttribute
  {
    Options(inOptions : SyntaxModuleOptions) : SyntaxModuleOptions = inOptions;
    [Cached] MakeContext(_parent : Context) : Context = _parent;

    | Splicable = AttributeName="Splicable"
      {
        override Options = inOptions.SetNeedSplicable(this, true);
      }
  }

  syntax SyntaxModuleAttributeList = "[" (SyntaxModuleAttribute; ","sm)+ "]" sm
  {
    Options(inOptions : SyntaxModuleOptions) : SyntaxModuleOptions = FoldThrough(SyntaxModuleAttributes.Item1, inOptions, _.Options(_));
    MakeContext(parent : Context) : Context = FoldThrough(SyntaxModuleAttributes.Item1, parent, _.MakeContext(_));
  }

  syntax SyntaxModuleAttributes    = SyntaxModuleAttributeList*
  {
    [Cached] Options(inOptions : SyntaxModuleOptions) : SyntaxModuleOptions = FoldThrough(SyntaxModuleAttributeLists, inOptions, _.Options(_));
    [Cached] MakeContext(parent : Context) : Context = FoldThrough(SyntaxModuleAttributeLists, parent, _.MakeContext(_));
  }

  syntax RuleAttribute
  {
    Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = inOptions;
    [Cached] MakeContext(_parent : Context) : Context = _parent;

    | Start                  = AttributeName="StartRule"
      {
        override Options = inOptions.SetStart(this, true);
      }
    | SpanClass              = AttributeName="SpanClass" "(" Class=Identifier ")"
      {
        override Options = Context.Bind.[SpanClassSymbol](Class).Map(inOptions.SetSpanClass(Class, _)) ?? inOptions;
      }
    | ExplicitSpaces         = AttributeName="ExplicitSpaces"
      {
        override Options = inOptions.SetExplicitSpaces(this, true);
      }
    | ImplicitSpaces         = AttributeName="ImplicitSpaces"
      {
        override Options = inOptions.SetExplicitSpaces(this, false);
      }
    | SuppressTrailingSpaces = AttributeName="SuppressTrailingSpaces"
      {
        override Options = inOptions.SetSuppressTrailingSpaces(this, true);
      }
    | AllowTrailingSpaces    = AttributeName="AllowTrailingSpaces"
      {
        override Options = inOptions.SetSuppressTrailingSpaces(this, false);
      }
    | NoAst                  = AttributeName="NoAst"
      {
        override Options = inOptions.SetNoAst(this, true);
      }
    | KeyToken               = AttributeName="Key"
      {
        override Options = inOptions.SetKeyToken(this, true);
      }
    | Keyword               = AttributeName="Keyword"
      {
        override Options = inOptions.SetKeyword(this, true);
      }
    | Identifier            = AttributeName="Identifier"
      {
        override Options = inOptions.SetIdentifier(this, true);
      }
  }

  syntax RuleAttributeList = "[" (RuleAttribute; ","sm)+ "]" sm
  {
    Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = FoldThrough(RuleAttributes.Item1, inOptions, _.Options(_));
    MakeContext(parent : Context) : Context = FoldThrough(RuleAttributes.Item1, parent, _.MakeContext(_));
  }

  syntax RuleAttributes    = RuleAttributeList*
  {
    [Cached] Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = FoldThrough(RuleAttributeLists, inOptions, _.Options(_));
    [Cached] MakeContext(parent : Context) : Context = FoldThrough(RuleAttributeLists, parent, _.MakeContext(_));
  }

  [StartRule]
  syntax SyntaxModuleMember
  {
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void = _ = owner;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void {}

    GetTokenLiteralNames(result : Map[string, Nitra.Model.Identifier]) : Map[string, Nitra.Model.Identifier] = result;

    | RegexRule
      {
        override CollectGlobalSymbols = RegexRule.CollectGlobalSymbols(owner);
        override MakeContext          = RegexRule.MakeContext(parent);
        override Typing               = RegexRule.Typing();
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(owner);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(owner);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(owner);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | ExtensibleTokenRule
      {
        override CollectGlobalSymbols = ExtensibleTokenRule.CollectGlobalSymbols(owner);
        override MakeContext          = ExtensibleTokenRule.MakeContext(parent);
        override Typing               = ExtensibleTokenRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(owner);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | Marker              = "marker"              sm Name                                                   ";"nl
      {
        override CollectGlobalSymbols = _ = owner.Node.AddAndReport(ParsedMarkerSymbol(Name, owner), Location);
      }
    | ExtendSyntax        = "extend"  sm "syntax" sm (Name sm "=" sm)? BaseName outline_begin_before nl "{" inl ExtensionRule* d "}" outline_end_before nl
      {
        [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtendSyntaxSymbol = ParsedExtendSyntaxSymbol(this, owner);
        override CollectGlobalSymbols
        {
          def symbol = MakeSymbol(owner);
          _ = owner.Node.AddAndReport.[ExtendSyntaxSymbol](symbol, Location);
          foreach (rule in ExtensionRules)
            rule.CollectGlobalSymbols(symbol);
        }
        override MakeContext
        {
          def context = RuleDeclarationSiteContext(parent, Symbol);
          _ = FoldThrough(ExtensionRules, context, _.MakeContext(_));
          parent
        }
        override Typing
        {
          def baseRule = this.Symbol.Extend;
          when (baseRule is Some(symbol) when symbol.Options.IsTokenRule)
            Project.CurrentProject.Error(BaseName.Location, "Can't extend token with syntax rule.", -1);            
          foreach (m in ExtensionRules)
            m.Typing(baseRule);
        }
      }
    | ExtendToken         = "extend"  sm "token"  sm (Name sm "=" sm)? BaseName outline_begin_before nl "{" inl ExtensionRule* d "}" outline_end_before nl
      {
        [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtendTokenSymbol = ParsedExtendTokenSymbol(this, owner);
        override CollectGlobalSymbols
        {
          def symbol = MakeSymbol(owner);
          _ = owner.Node.AddAndReport.[ExtendSyntaxSymbol](symbol, Location);
          foreach (rule in ExtensionRules)
            rule.CollectGlobalSymbols(symbol);
        }
        override MakeContext
        {
          def context = RuleDeclarationSiteContext(parent, Symbol);
          _ = FoldThrough(ExtensionRules, context, _.MakeContext(_));
          parent
        }
        override Typing
        {
          def baseRule = this.Symbol.Extend;
          when (baseRule is Some(symbol) when !symbol.Options.IsTokenRule)
            Project.CurrentProject.Error(BaseName.Location, "Can't extend syntax rule with token.", -1);            
          foreach (m in ExtensionRules)
            m.Typing(baseRule);
        }
      }
    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenLiteral        = "literal"             sm Name  sm "="  sm (StringLiteral; ","sm)+               ";"nl
      {
        override GetTokenLiteralNames
        {
          def name = Name.Identifier();
          FoldThrough(StringLiterals.Item1, result, (strLit, result) => result.Replace(strLit.Value(), name))
        }
        override MakeContext = TokenLiteralContext(parent, Name, StringLiterals.Item1);
      }
    | SpanClass           = "span"    sm "class"  sm Name MatchTokens=("=" RegexExpression)?                ";"nl
      {
        [Cached] private MakeSymbol(context : Context) : SpanClassSymbol
        {
          def symbol = ParsedSpanClassSymbol(this, context);
          NitraProject.CurrentProject.AddSymbolDef(symbol, Name.Location);
          symbol
        }
        [Cached] private RegularRule() : option[RegularRule] = MatchTokens.Map(t =>
        {
          def rr = t.Item2.Typing();
          rr.ValidateRegularRule();
          rr
        });
        override MakeContext
        {
          def context = SpanClassContext(parent, MakeSymbol(parent));
          _ = MatchTokens.Map(t => t.Item2.MakeContext(context));
          context
        }
        override Typing
        {
          _ = RegularRule();
        }
      }
    | BracePair           = "braces"  sm Open=RegexExpression "," sm Close=RegexExpression                  ";"nl
      {
        override MakeContext
        {
          def context = BracePairContext(parent, Open, Close);
          _ = Open.MakeContext(context);
          _ = Close.MakeContext(context);
          context
        }
        override Typing
        {
          Open.Typing().ValidateRegularRule();
          Close.Typing().ValidateRegularRule();
        }
      }
    | KeywordRegex        = "keyword" sm "regex"  sm MatchTokens=RegexExpression sm "rule" sm QualifiedName ";"nl
      {
        override MakeContext
        {
          def context = KeywordRegexContext(parent, MatchTokens, QualifiedName);
          _ = MatchTokens.MakeContext(context);
          context
        }
      }
  }

  syntax RuleAlias = "alias" sm Name sm "=" sm RuleExpression ";"nl
  {
    [Cached] private MakeSymbol() : RuleAliasSymbol
    {
      def symbol = ParsedRuleAliasSymbol(this);
      NitraProject.CurrentProject.AddSymbolDef(symbol, Name.Location);
      symbol
    }
    MakeContext(parent : Context) : Context
    {
      RuleAliasContext(parent, MakeSymbol());
    }
  }

  syntax TokenRule = RuleAttributes "token" sm Name sm "=" sm RuleExpression SimpleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedTokenRuleSymbol = ParsedTokenRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule());
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      SimpleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = SimpleRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeTypedRule()   : Rule.Sequence = TypeRuleExpression(Options(), SimpleRuleBody.Context, RuleExpression.Typing(None(), 0, None(), SimpleRuleBody.Context, Options()));
    Typing() : void
    {
      _ = MakeTypedRule();
      SimpleRuleBody.Typing();
    }
  }

  syntax RegexRule = RuleAttributes "regex" sm Name sm "=" sm RegexExpression RegexRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedRegularRuleSymbol = ParsedRegularRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.Empty);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      RegexRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = RegexExpression.MakeContext(context);
      _ = RegexRuleBody.MakeContext(context);
      parent
    }
    [Cached] private Regex() : RegularRule      = RegexExpression.Typing();
    Typing() : void
    {
      _ = Regex();
      RegexRuleBody.Typing();
    }
  }

  syntax RegexRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : RegularRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : Nitra.Location;
    Typing() : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl RegexRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in RegexRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(RegexRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in RegexRuleBodyMembers) x.Typing();
      }
  }

  syntax RegexRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : RegularRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | RegexRule
      {
        override CollectGlobalSymbols = RegexRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RegexRule.MakeContext(parent);
        override Typing               = RegexRule.Typing();
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
  }

  syntax SimpleRule = RuleAttributes "syntax" sm Name sm "=" sm RuleExpression SimpleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedSimpleRuleSymbol = ParsedSimpleRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.Empty);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      SimpleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = SimpleRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeTypedRule()   : Rule.Sequence = TypeRuleExpression(Options(), SimpleRuleBody.Context, RuleExpression.Typing(None(), 0, None(), SimpleRuleBody.Context, Options()));
    Typing() : void
    {
      _ = MakeTypedRule();
      SimpleRuleBody.Typing();
    }
  }

  syntax VoidRule = RuleAttributes "void" sm Name sm "=" sm RuleExpression SimpleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedVoidRuleSymbol = ParsedVoidRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule());
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      SimpleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = SimpleRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeTypedRule()   : Rule.Sequence = TypeRuleExpression(Options(), SimpleRuleBody.Context, RuleExpression.Typing(None(), 0, None(), SimpleRuleBody.Context, Options()));
    Typing() : void
    {
      _ = MakeTypedRule();
      SimpleRuleBody.Typing();
    }
  }

  syntax SimpleRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : SimpleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : Nitra.Location;
    Typing() : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl SimpleRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in SimpleRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(SimpleRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in SimpleRuleBodyMembers) x.Typing();
      }
  }

  syntax SimpleRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : SimpleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | RegexRule
      {
        override CollectGlobalSymbols = RegexRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RegexRule.MakeContext(parent);
        override Typing               = RegexRule.Typing();
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleTokenRule
      {
        override CollectGlobalSymbols = ExtensibleTokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleTokenRule.MakeContext(parent);
        override Typing               = ExtensibleTokenRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | RuleMethod
      {
        override CollectGlobalSymbols = RuleMethod.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethod.MakeContext(parent);
        override Typing               = RuleMethod.Typing();
      }
    | RuleMethodOverride
      {
        override CollectGlobalSymbols = RuleMethodOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodOverride.MakeContext(parent);
        override Typing               = RuleMethodOverride.Typing();
      }
    | RuleMethodMissing
      {
        override CollectGlobalSymbols = RuleMethodMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodMissing.MakeContext(parent);
        override Typing               = RuleMethodMissing.Typing();
      }
  }

  syntax ExtensibleRule = RuleAttributes "syntax"  sm Name ExtensibleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtensibleRuleSymbol = ParsedExtensibleRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.Empty);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      ExtensibleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = ExtensibleRuleBody.MakeContext(context);
      parent
    }
    Typing() : void
    {
      _ = Options();
      _ = ExtensibleRuleBody.Typing(ExtensibleRuleRef.Some(Name.Location, false, Symbol));
    }
  }

  syntax ExtensibleTokenRule = RuleAttributes "token"  sm Name ExtensibleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtensibleTokenRuleSymbol = ParsedExtensibleTokenRuleSymbol(this, owner);
    [Cached] private Options() : RuleDefinitionOptions = RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule());
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      ExtensibleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = ExtensibleRuleBody.MakeContext(context);
      parent
    }
    Typing() : void
    {
      _ = Options();
      _ = ExtensibleRuleBody.Typing(ExtensibleRuleRef.Some(Name.Location, false, Symbol));
    }
  }

  syntax ExtensibleRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : ExtensibleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : Nitra.Location;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl ExtensibleRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in ExtensibleRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(ExtensibleRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in ExtensibleRuleBodyMembers) x.Typing(_baseRule);
      }
  }

  syntax ExtensibleRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : ExtensibleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | RegexRule
      {
        override CollectGlobalSymbols = RegexRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RegexRule.MakeContext(parent);
        override Typing               = RegexRule.Typing();
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleTokenRule
      {
        override CollectGlobalSymbols = ExtensibleTokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleTokenRule.MakeContext(parent);
        override Typing               = ExtensibleTokenRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | ExtensionRule
      {
        override CollectGlobalSymbols = ExtensionRule.CollectGlobalSymbols(ruleSymbol);
        override Typing               = ExtensionRule.Typing(_baseRule);
        override MakeContext          = ExtensionRule.MakeContext(parent);
      }
    | RuleMethod
      {
        override CollectGlobalSymbols = RuleMethod.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethod.MakeContext(parent);
        override Typing               = RuleMethod.Typing();
      }
    | RuleMethodOverride
      {
        override CollectGlobalSymbols = RuleMethodOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodOverride.MakeContext(parent);
        override Typing               = RuleMethodOverride.Typing();
      }
    | RuleMethodMissing
      {
        override CollectGlobalSymbols = RuleMethodMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodMissing.MakeContext(parent);
        override Typing               = RuleMethodMissing.Typing();
      }
  }

  syntax ExtensionRule = "|" sm RuleAttributes (Name "=")? RuleExpression Precedence? ExtensionRuleBody?
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtensionRuleSymbol = ParsedExtensionRuleSymbol(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport.[RuleDefSymbol](symbol, Location);
      when (ExtensionRuleBodyOpt is Some(ruleBody))
        ruleBody.CollectGlobalSymbols(symbol);
    }
    [Cached] MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = MakeRuleTypingContext(context);
      parent
    }
    [Cached] private MakeRuleTypingContext(parent : Context) : Context
    {
      if (ExtensionRuleBodyOpt is Some(ruleBody))
        ruleBody.MakeContext(parent)
      else
        parent
    }
    [Cached] private MakeBaseRuleRef(baseRule : ExtensibleRuleRef)  : ExtensibleRuleRef     = baseRule;
    [Cached] private MakeOptions()                                  : RuleDefinitionOptions = RuleAttributes.Options(BaseRuleRef.Map(r => r.Options.InheritedCopy()) ?? RuleDefinitionOptions.Empty);
    [Cached] private MakeTypedRule()                                : Rule.Sequence         = TypeRuleExpression(Options, RuleTypingContext, RuleExpression.Typing(BaseRuleRef, PrecedenceOpt.Map(_.Value()) ?? 0, None(), RuleTypingContext, Options));
    Typing(baseRule : ExtensibleRuleRef) : void
    {
      _ = MakeBaseRuleRef(baseRule);
      _ = MakeOptions();
      _ = MakeTypedRule();
      when (ExtensionRuleBodyOpt is Some(ruleBody))
        ruleBody.Typing(BaseRuleRef);
    }
  }

  syntax ExtensionRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : ExtensionRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : Nitra.Location;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_hiden outline_begin_before inl "{" inl ExtensionRuleBodyMember* d "}" outline_end_before nl  d
      {
        override CollectGlobalSymbols = foreach (member in ExtensionRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(ExtensionRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in ExtensionRuleBodyMembers) x.Typing(_baseRule);
      }
  }

  syntax ExtensionRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : ExtensionRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | RegexRule
      {
        override CollectGlobalSymbols = RegexRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RegexRule.MakeContext(parent);
        override Typing               = RegexRule.Typing();
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleTokenRule
      {
        override CollectGlobalSymbols = ExtensibleTokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleTokenRule.MakeContext(parent);
        override Typing               = ExtensibleTokenRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | RuleMethod
      {
        override CollectGlobalSymbols = RuleMethod.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethod.MakeContext(parent);
        override Typing               = RuleMethod.Typing();
      }
    | RuleMethodOverride
      {
        override CollectGlobalSymbols = RuleMethodOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodOverride.MakeContext(parent);
        override Typing               = RuleMethodOverride.Typing();
      }
    | RuleMethodMissing
      {
        override CollectGlobalSymbols = RuleMethodMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = RuleMethodMissing.MakeContext(parent);
        override Typing               = RuleMethodMissing.Typing();
      }
  }

  syntax RuleMethodsParam = Name sm ":" sm Type;
  token RuleMethodPrivate = "private" sm;

  syntax RuleMethodAttribute
  {
    IsCached() : bool;

    | Cached = AttributeName="Cached"
      {
        override IsCached = true;
      }
  }
  syntax RuleMethodAttributeList = "[" (RuleMethodAttribute; ",")+ "]" nl
  {
    IsCached() : bool
    {
      foreach(x when x.IsCached() in RuleMethodAttributes.Item1)
        return true;
      false
    }
  }
  syntax RuleMethodAttributes = RuleMethodAttributeList*
  {
    IsCached() : bool
    {
      foreach(x when x.IsCached() in RuleMethodAttributeLists)
        return true;
      false
    }
  }

  syntax RuleMethod = RuleMethodAttributes RuleMethodPrivate? Name sm "(" (RuleMethodsParam; "," sm)* ")" sm ":" sm Type RuleMethodBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol(ruleSymbol : SyntaxRuleSymbol) : RuleMethod.DefinitionSymbol
    {
      if (RuleMethodPrivateOpt.IsSome)
      {
        when (RuleMethodBody.IsEmpty())
          Project.CurrentProject.Error(this.Location, "A private rule method must have a body.", -1);
        unless (ruleSymbol is SimpleRuleSymbol || ruleSymbol is ExtensionRuleSymbol)
          Project.CurrentProject.Error(this.Location, "A private rule method can be declared only in simple rules and extension rules.", -1);
        RuleMethod.ParsedDefPrivateSymbol(this)
      }
      else if (RuleMethodBody.IsEmpty())
      {
        unless (ruleSymbol is ExtensibleRuleSymbol)
          Project.CurrentProject.Error(this.Location, "An abstract rule method must be defined in an extension point rule only.", -1);
        RuleMethod.ParsedAbstractSymbol(this)
      }
      else
      {
        unless (ruleSymbol is SimpleRuleSymbol || ruleSymbol is ExtensibleRuleSymbol)
          Project.CurrentProject.Error(this.Location, "A public rule method must be defined in an extension point rule or a simple rule only.", -1);
        RuleMethod.ParsedDefSymbol(this)
      }
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(ruleSymbol), this.Location);
    [Cached] private MakeSignature() : RuleMethod.RuleMethodSignature = RuleMethod.RuleMethodSignature(this.Location, NemerleParser.Parse(this.RuleMethodsParams.Item1, Context), NemerleParser.Parse(this.Type, Context));
    [Cached] private MakeBody()      : RuleMethod.RuleMethodBody      = RuleMethod.RuleMethodBody(this.RuleMethodBody.BodyLocation(), NemerleParser.Parse(this.RuleMethodBody, Context));
    [Cached] private MakeIsCached()  : bool                           = RuleMethodAttributes.IsCached();
    Typing() : void
    {
      _ = MakeSignature();
      _ = MakeBody();
      _ = MakeIsCached();
    }
  }

  syntax RuleMethodOverride = RuleMethodAttributes "override"     sm  Name RuleMethodBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol() : RuleMethod.DefinitionSymbol
    {
      when (RuleMethodBody.IsEmpty())
        Project.CurrentProject.Error(this.Location, "An overriding rule method must have a body.", -1);
      RuleMethod.ParsedOverrideSymbol(this);
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(), this.Location);
    [Cached] private MakeBaseRuleMethod() : SymbolRef[RuleMethod.RuleMethodSymbol]
    {
      match (this.Symbol.DeclaringRule)
      {
        | ExtensionRuleSymbol(BaseRule = SymbolRef.Some(baseRuleSymbol)) =>
          match (baseRuleSymbol.Node.BindMany.[RuleMethod.DefinitionSymbol](Name.Location))
          {
            | [RuleMethod.DefSymbol      as baseRuleMethodSymbol]
            | [RuleMethod.AbstractSymbol as baseRuleMethodSymbol] =>
              SymbolRef.Some(Name.Location, false, baseRuleMethodSymbol)

            | [_] =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"Cannot override '$name' rule method.", -1); //TODO: rewrite error message!
              SymbolRef.None(Name.Location, false)

            | _ =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"No '$name' rule method to override.", -1);
              SymbolRef.None(Name.Location, false)
          }

        | _ =>
          Project.CurrentProject.Error(this.Location, "An rule method can be overridden only in extending rules.", -1);
          SymbolRef.None(Name.Location, false)
      }
    }
    [Cached] private MakeBody() : RuleMethod.RuleMethodBody = RuleMethod.RuleMethodBody(this.RuleMethodBody.BodyLocation(), NemerleParser.Parse(this.RuleMethodBody, Context));
    Typing() : void
    {
      _ = MakeBaseRuleMethod();
      _ = MakeBody();
    }
  }

  syntax RuleMethodMissing = RuleMethodAttributes "missing"        sm  Name RuleMethodBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol() : RuleMethod.RuleMethodMissingValueSymbol
    {
      when (RuleMethodBody.IsEmpty())
        Project.CurrentProject.Error(this.Location, "A 'missing' handler must have a body.", -1);
      RuleMethod.ParsedRuleMethodMissingValueSymbol(this);
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(), this.Location);
    [Cached] private MakeHandlingRuleMethod() : SymbolRef[RuleMethod.RuleMethodSymbol]
    {
      match (this.Symbol.DeclaringRule)
      {
        | ExtensibleRuleSymbol as targetRuleSymbol
        | SimpleRuleSymbol     as targetRuleSymbol =>
          match (targetRuleSymbol.Node.BindMany.[RuleMethod.DefinitionSymbol](Name.Location))
          {
            | [ruleMethodSymbol] => SymbolRef.Some(Name.Location, false, ruleMethodSymbol)
            | _ =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"No '$name' rule method to handle 'missing'.", -1);
              SymbolRef.None(Name.Location, false)
          }

        | _ =>
          Project.CurrentProject.Error(this.Location, "A 'missing' handler for a rule method can be defined in simple and extensible rules.", -1);
          SymbolRef.None(Name.Location, false)
      }
    }
    [Cached] private MakeBody() : RuleMethod.RuleMethodBody = RuleMethod.RuleMethodBody(this.RuleMethodBody.BodyLocation(), NemerleParser.Parse(this.RuleMethodBody, Context));
    Typing() : void
    {
      _ = MakeHandlingRuleMethod();
      _ = MakeBody();
    }
  }

  syntax Precedence = "precedence" sm Number RightAssociative=("right-associative")?
  {
    Value() : int = if (RightAssociative.IsSome) -Number.Value() else Number.Value();
  }

  syntax RuleMethodBody
  {
    BodyLocation() : Nitra.Location;
    IsEmpty() : bool;

    | Block
      {
        override BodyLocation = Block.Location;
        override IsEmpty = false;
      }
    | Expression = sm "=" sm Expression ";" nl
      {
        override BodyLocation = Expression.Location;
        override IsEmpty = false;
      }
    | Empty = ";" nl
      {
        override BodyLocation = Location(this.Location.Source, this.Location.StartPos, this.Location.StartPos);
        override IsEmpty = true;
      }
  }

  syntax TypeParams = "[" (Type; ","sm)+ "]";
  syntax SimpleName = Identifier TypeParams?;

  syntax Type
  {
    | PredefinedType
    | SimpleName
    | Qualified = Type "." SimpleName precedence 20;
    | Pointer   = Type "*" Type       precedence 10;
  }

  syntax Block = outline_hiden outline_begin_before nl "{" inl (Expression; ";" nl)* ";"* nl d "}" outline_end_before nl;
  syntax Parentheses = "(" Expression? ")";

  syntax Content
  {
    | Block;
    | IgnoreToken+;
    | CharLiteral;
    | StringLiteral;
    | Parentheses;
    | "fold" sm Identifier "(" Expression ";" (Expression; ";"sm)+ ")";
  }

  syntax ExpressionContent
  {
    | Content1 = Content;
    | Content2 = (!";" !")" !"}" !Content Any)+;
  }

  syntax Expression = ExpressionContent+;

  syntax Range
  {
    RangeSet(inCharSet : RangeSet) : RangeSet;

    | SingleChar = CharLiteral
      {
        override RangeSet = inCharSet.AddSingleCharRange(CharLiteral.Value());
      }
    | CharRange       = CharLiteral ".." CharLiteral
      {
        override RangeSet = inCharSet.AddRange(CharLiteral1.Value(), CharLiteral2.Value());
      }
    | UnicodeRange    = Name
      {
        override RangeSet
        {
          def nameText = Name.Value();
          if (UnicodeCategoryTable.StrToUnicodeCategory(nameText) is Some(cat))
            inCharSet.AddRange(cat)
          else
          {
            Project.CurrentProject.Error(this, $"Invalid unicode category '$nameText'.", -1);
            inCharSet
          }
        }
      }
  }

  alias Separator  = RegexExpression;

  syntax RegexExpression
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] Typing() : RegularRule;

    | Choice                  = LeftRule=RegexExpression ^ 10 sm RightRules=("|" RegexExpression ^ 10)+
      {
        override MakeContext
        {
          def context1 = LeftRule.MakeContext(parent);
          def context2 = FoldThrough(RightRules, context1, (t, c) => t.Item2.MakeContext(c));
          context2
        }
        override Typing = RegularRule.Choice(Location, LeftRule.Typing() :: RightRules.Map(r => r.Item2.Typing()));
      }
    | Sequence                = LeftRule=RegexExpression ^ 20 sm RightRules=(RegexExpression ^ 20)+
      {
        override MakeContext
        {
          def context1 = LeftRule.MakeContext(parent);
          def context2 = FoldThrough(RightRules, context1, _.MakeContext(_));
          context2
        }
        override Typing = RegularRule.Sequence(Location, LeftRule.Typing() :: RightRules.Map(_.Typing()));
      }
    | Optional                = RegexExpression "?" precedence 30
      {
        override MakeContext = RegexExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 0, Some(1), this.RegexExpression.Typing());
      }
    | ZeroOrMany              = RegexExpression "*" precedence 30
      {
        override MakeContext = RegexExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 0, None(), this.RegexExpression.Typing());
      }
    | OneOrMany               = RegexExpression "+" precedence 30
      {
        override MakeContext = RegexExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 1, None(), this.RegexExpression.Typing());
      }
    | Invert                  = "~" RegexExpression precedence 25
      {
        override MakeContext = RegexExpression.MakeContext(parent);
        override Typing = RegularRule.Invert(Location, this.RegexExpression.Typing());
      }
    | Subtract                = Rule1 = RegexExpression "-" Rule2 = RegexExpression precedence 22
      {
        override MakeContext = Rule2.MakeContext(Rule1.MakeContext(parent));
        override Typing = RegularRule.Subtract(Location, this.Rule1.Typing(), this.Rule2.Typing());
      }
    | Char                    = CharLiteral
      {
        override Typing = RegularRule.Chars(Location, [RangeSet().AddSingleCharRange(CharLiteral.Value())]);
      }
    | String                  = StringLiteral
      {
        override Typing = RegularRule.Chars(Location, RangeSet.MakeString(StringLiteral.Value()));
      }
    | Call                    = QualifiedName
      {
        override Typing
        {
          def ruleRef = SymbolRef.Create(QualifiedName, Context.Bind(QualifiedName));
          RegularRule.Call(Location, ruleRef)
        }
      }
    | Rounds                  = "(" RegexExpression ")"
      {
        override MakeContext = RegexExpression.MakeContext(parent);
        override Typing = RegexExpression.Typing();
      }
    | Range                   = "[" (Range; ","sm)+ "]"
      {
        override Typing = RegularRule.Chars(Location, [FoldThrough(Ranges.Item1, RangeSet(), _.RangeSet(_))]);
      }
    | InvertedRange           = "[" "^" (Range; ","sm)+ "]"
      {
        override Typing = RegularRule.Chars(Location, InvertRange(FoldThrough(Ranges.Item1, RangeSet(), _.RangeSet(_))));
      }
    | ZeroOrManyWithSeparator = "(" RegexExpression ";"sm Separator ")" "*"
      {
        override MakeContext  = parent |> RegexExpression.MakeContext |> Separator.MakeContext;
        override Typing = RegularRule.RepeatWithSeparator(Location, 0, None(), RegexExpression.Typing(), Separator.Typing());
      }
    | OneOrManyWithSeparator  = "(" RegexExpression ";"sm Separator ")" "+"
      {
        override MakeContext = parent |> RegexExpression.MakeContext |> Separator.MakeContext;
        override Typing = RegularRule.RepeatWithSeparator(Location, 1, None(), RegexExpression.Typing(), Separator.Typing());
      }
  }

  alias Separator   = RuleExpression;

  syntax RuleExpression
  {
    /// precedence < 0 if it is right-associative
    Typing(_baseRule : option[ExtensibleRuleSymbol], _precedence : int, _fieldName : option[string], context : Context, _options : RuleDefinitionOptions) : Nitra.Model.Rule;
    InferName() : string = "";

    | Sequence                        = LeftRule=RuleExpression ^ 10 RightRules=(sm RuleExpression ^ 10)+
      {
        override Typing = TypeSequence(_baseRule, _precedence, _fieldName, context, _options, LeftRule, RightRules, this);
        override InferName = LeftRule.InferName();
      }
    | Not                             = "!" RuleExpression ^ 20
      {
        override Typing
        {
          def rule = this.RuleExpression.Typing(None(), 0, None(), context, _options);
          Rule.Not(this.Location, RuleType.Void(), "", rule);
        }
      }
    | And                             = "&" RuleExpression ^ 20
      {
        override Typing = Rule.And(this.Location, RuleType.Void(), "", RuleExpression.Typing(None(), 0, None(), context, _options));
      }
    | Optional                        = RuleExpression "?" precedence 30
      {
        override Typing
        {
          def rule  = RuleExpression.Typing(None(), 0, None(), context, _options);
          def type  = EvaluateOptionRuleType(rule);
          Rule.Repeat(this.Location, type, _fieldName ?? (rule.FieldName + "Opt"), 0, Some(1), rule);
        }
        override InferName = RuleExpression.InferName() + "Opt";
      }
    | ZeroOrMany                      = RuleExpression "*" precedence 30
      {
        override Typing
        {
          def rule = RuleExpression.Typing(None(), 0, None(), context, _options);
          def type = EvaluateListRuleType(rule);
          Rule.Repeat(this.Location, type, MakePlural(_fieldName, rule, context), 0, None(), rule);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrMany                       = RuleExpression "+" precedence 30
      {
        override Typing
        {
          def rule = RuleExpression.Typing(None(), 0, None(), context, _options);
          def type = EvaluateListRuleType(rule);
          Rule.Repeat(this.Location, type, MakePlural(_fieldName, rule, context), 1, None(), rule);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | FieldName1                      = RuleExpression ^ 12 sm "as" sm Name
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, Some(Name.Value()), context, _options);
        override InferName = Name.GetText();
      }
    | FieldName2                      = Name sm "=" sm RuleExpression ^ 11
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, Some(Name.Value()), context, _options);
        override InferName = Name.GetText();
      }
    | Char                            = CharLiteral
      {
        override Typing = StringRuleTyping(CharLiteral.Location, CharLiteral.Value().ToString(), _fieldName, context);
        override InferName = CharLiteral.Value().ToString();
      }
    | String                          = StringLiteral
      {
        override Typing = StringRuleTyping(StringLiteral.Location, StringLiteral.Value().ToString(), _fieldName, context);
        override InferName = StringLiteral.Value().ToString();
      }
    | Rounds                          = "(" RuleExpression ")"
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, _fieldName, context, _options);
        override InferName = RuleExpression.InferName();
      }
    | Call                            = QualifiedName BindingPower=(sm "^" sm Number)?
      {
        override Typing = TypeCall(_baseRule, _precedence, _fieldName, context, _options, QualifiedName, BindingPower);
        override InferName = QualifiedName.Parts().LastOrDefault("");
      }
    | ZeroOrManyWithSeparator         = "(" RuleExpression ";"sm Separator ")" "*"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, _options);
          def sepRule = Separator.Typing(None(), 0, None(), context, _options);
          def type    = EvaluateListWithSeparatorRuleType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 0, None(), rule, sepRule, false);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | ZeroOrManyWithHangingSeparator  = "(" RuleExpression ";"sm Separator ";"sm "?" ")" "*"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, _options);
          def sepRule = Separator.Typing(None(), 0, None(), context, _options);
          def type    = EvaluateListWithSeparatorRuleType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 0, None(), rule, sepRule, true);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrManyWithSeparator          = "(" RuleExpression ";"sm Separator ")" "+"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, _options);
          def sepRule = Separator.Typing(_baseRule, 0, None(), context, _options);
          def type    = EvaluateListWithSeparatorRuleType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 1, None(), rule, sepRule, false);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrManyWithHangingSeparator   = "(" RuleExpression ";"sm Separator ";"sm "?" ")" "+"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, _options);
          def sepRule = Separator.Typing(_baseRule, 0, None(), context, _options);
          def type    = EvaluateListWithSeparatorRuleType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 1, None(), rule, sepRule, true);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
  } // extend syntax RuleExpression
} // syntax module
