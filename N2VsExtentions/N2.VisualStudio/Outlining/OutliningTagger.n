using Nitra.Internal;
using Nitra.VisualStudio.Parsing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Microsoft.VisualStudio.Text;
using Microsoft.VisualStudio.Text.Tagging;

using System;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.VisualStudio.Outlining
{
  public class OutliningTagger : ITagger[IOutliningRegionTag]
  {
    public                   Language    : Language        { get _parseAgent.Language }
    private                  _parseAgent : ParseAgent;
    private                  _buffer     : ITextBuffer;
    private mutable volatile _data       : OutliningData;

    [Record]
    private struct OutliningData
    {
      public Outlinings    : List[OutliningInfo];
      public Snapshot      : ITextSnapshot;
    }

    public this(parseAgent : ParseAgent, buffer : ITextBuffer)
    {
      _parseAgent    = parseAgent;
      _buffer        = buffer;

      def parseResult = _parseAgent.LastResult;
      _data = 
        if (parseResult != null)
          OutliningData(GetOutlinings(parseResult), buffer.CurrentSnapshot)
        else
          OutliningData(null, buffer.CurrentSnapshot);

      _parseAgent.ParseComplete += fun (_, args)
      {
        _data = OutliningData(GetOutlinings(args.Result), args.Snapshot);
        TagsChanged?.Invoke(this, SnapshotSpanEventArgs(SnapshotSpan(args.Snapshot, 0, args.Snapshot.Length)));
      };
      _parseAgent.ParseFailed += fun (_, args)
      {
        _data = OutliningData(null, args.Snapshot);
        TagsChanged?.Invoke(this, SnapshotSpanEventArgs(SnapshotSpan(args.Snapshot, 0, args.Snapshot.Length)));
      };
    }

    public event TagsChanged : EventHandler[SnapshotSpanEventArgs];

    public GetTags(_spans : NormalizedSnapshotSpanCollection) : Seq[ITagSpan[IOutliningRegionTag]]
    {
      def data = _data;
      if (data.Outlinings != null)
      {
        def result = List();
        foreach (o in data.Outlinings)
        {
          def span = o.Span;
          def currentSpn = SnapshotSpan(data.Snapshot, span.StartPos, span.Length);
          result.Add(TagSpan(currentSpn, OutliningRegionTag(o.IsDefaultCollapsed, o.IsImplementation, null, null)));
        }
        result
      }
      else Enumerable.Empty()
    }

    private GetOutlinings(parseResult : ParseResult) : List[OutliningInfo]
    {
      def outlinings = List();
      parseResult.GetOutlining(outlinings);
      outlinings
    }
  }
}
