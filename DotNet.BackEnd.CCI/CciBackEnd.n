using DotNet;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra;
using Nitra.Declarations;
using Nitra.ProjectSystem;

using Microsoft.Cci;

using System;
using System.IO;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;

using NRB = Nitra.Declarations;

namespace Nitra.BackEnd.Cci
{
  [Record]
  public class CciBackEnd : IBackEnd
  {
    public CciHost : IMetadataHost { get; protected set; }
    
    public this() { this(PeReader.DefaultHost()) }
    
    public LoadExternalSymbols(project : Project, context : DependentPropertyEvalContext) : NamespaceSymbol
    {
      def keyToSymbolMap    = Hashtable.[uint, TypeSymbol]();
      def noFile = SourceSnapshot.Default.File;
      def noSpan = NSpan(0);
      def noLoc  = Location(noFile, noSpan);
      def rootNamespace = NamespaceSymbol();
      rootNamespace.MemberTable = TableScope("<root namespace>", rootNamespace);
      rootNamespace.DeclaredIn  = null;
      rootNamespace.FullName    = "";
      rootNamespace.EvalProperties(context);
      def libs = project.Libs;
      def createTypeParameters(cciType : ITypeDefinition) : array[TypeParameterSymbol]
      {
        def count = cciType.GenericParameterCount : int;
        if (count == 0)
          AstUtils.NoTypeParameters
        else
        {
          def result = array(count);
          foreach (tp in cciType.GenericParameters with i)
          {
            def name  = NRB.Name(noFile, noSpan, tp.Name.Value); // TODO: type.Name.UniqueKey
            def tps   = TypeParameterDeclaration(name, tp).DefineSymbol();
            tps.EvalProperties(context);
            result[i] = tps;
          }
          result
        }
      }
      def makeTypeParameters(symbol : GenericTypeSymbol, cciType : ITypeDefinition) : void
      {
        unless (symbol.IsTypeParametersCountEvaluated)
          symbol.TypeParametersCount = cciType.GenericParameterCount;
        unless (symbol.IsTypeParametersEvaluated)
          symbol.TypeParameters = createTypeParameters(cciType);
      }
      
      def host = CciHost;
      
      foreach(lib in libs)
      {
        | name is FullNameLibReference with filePath = name.Path
        | file is FileLibReference     with filePath = file.Path =>
          def fullPath = Path.GetFullPath(IO.Path.Combine(project.ProjectDir, filePath));
            
          when (!File.Exists(fullPath))
          {
            project.CompilerMessages.Error(noLoc, "The referenced file '" + fullPath + "' is not found.");
            continue;
          }
            
          def unit = host.LoadUnitFrom(fullPath) :> IAssembly;
          
          when (unit == null || unit.Equals(Dummy.Module) || unit.Equals(Dummy.Assembly))
          {
            project.CompilerMessages.Error(noLoc, "The referenced file '" + fullPath + "' is not .Net assembly.");
            continue;
          }
          
          def loadNsMember(member : INamespaceMember, declaredIn : NamespaceSymbol) : void
          {
            match (member)
            {
              | type is INamespaceTypeDefinition when type.IsPublic =>
                def name = Name(noFile, noSpan, type.Name.Value); // TODO: type.Name.UniqueKey
                def symbol : NamespaceMemberDeclarationSymbol =
                         if (type.IsDelegate)  ExternalTypeDeclaration.[TopDelegateSymbol] (name, type).DefineSymbol(declaredIn.MemberTable) 
                    else if (type.IsInterface) ExternalTypeDeclaration.[TopInterfaceSymbol](name, type).DefineSymbol(declaredIn.MemberTable)
                    else if (type.IsValueType) ExternalTypeDeclaration.[TopStructSymbol]   (name, type).DefineSymbol(declaredIn.MemberTable)
                    else if (type.IsEnum)      ExternalTypeDeclaration.[TopEnumSymbol]     (name, type).DefineSymbol(declaredIn.MemberTable)
                    else                       ExternalTypeDeclaration.[TopClassSymbol]    (name, type).DefineSymbol(declaredIn.MemberTable);
                
                when ((symbol : object) is ModifierHostSymbol as modifierHost)
                {
                  def flags = ModifierSet(context);
                  mutable modifiers;
                  when (type.IsAbstract)
                    modifiers |= Modifiers.Abstract;
                  when (type.IsPublic)
                    modifiers |= Modifiers.Public;
                  when (type.IsSealed)
                    modifiers |= Modifiers.Sealed;
                  when (type.IsStatic)
                    modifiers |= Modifiers.Static;
                  flags.Add(modifiers);
                  modifierHost.Flags = flags;
                }
                when (symbol is TopGenericTypeSymbol as genericSymbol)
                  makeTypeParameters(genericSymbol, type);
                unless (symbol.IsDeclaredInEvaluated)
                  symbol.DeclaredIn = declaredIn;
                def typeSymbol = symbol :> TypeSymbol;
                keyToSymbolMap[type.InternedKey] = typeSymbol; 
                symbol.Scope = EmptyScope.Instance; // TODO: fill with type members
                symbol.EvalProperties(context);
                      
              | cciNs is INamespaceDefinition => //loadNsMembers(ns, nsSymbol.MemberTable)
                def name   = NRB.Name(noFile, noSpan, cciNs.Name.Value); // TODO: ns.Name.UniqueKey
                def symbol = ExternalNamespaceDeclaration(name, cciNs).DefineSymbol(declaredIn.MemberTable);
                unless (symbol.IsDeclaredInEvaluated)
                  symbol.DeclaredIn = declaredIn;
                symbol.EvalProperties(context);
                foreach (subMember in cciNs.Members)
                  loadNsMember(subMember, symbol);
                
              | _ => {}
            }
          }
              
          foreach (subMember in unit.NamespaceRoot.Members)
            loadNsMember(subMember, rootNamespace);
            
        | _ => throw NotImplementedException("Loading types from " + lib + " not supported yet.")
      }

      assert(true);
      def singleDimensionArray = SingleDimensionArraySymbol();
      singleDimensionArray.TypeParametersCount = 0;
      singleDimensionArray.EvalProperties(context);
      def cciObject = host.PlatformType.SystemObject.ResolvedType;
      mutable objectSymbol;
      unless (keyToSymbolMap.TryGetValue(cciObject.InternedKey, out objectSymbol))
        return rootNamespace;
      def timer = Stopwatch.StartNew();
      mutable resolvs = 0;
      mutable resolvedByKey = 0;
      mutable resolvedByResolvedTypeProp = 0;
      
      def makeSymbol(cciTypeReference : ITypeReference) : TypeSymbol
      {
        | t is IGenericTypeParameter =>
          def typeInfo = makeSymbol(t.DefiningType) :> GenericTypeSymbol;
          def tp = typeInfo.TypeParameters[t.Index];
          tp
          
        | t is IArrayTypeReference =>
          def args = array[makeSymbol(t.ElementType)];
          def typeInfo = if (t.IsVector) singleDimensionArray : ArraySymbol
                         else 
                         {
                           def a = MultiDimensionArraySymbol();
                           a.TypeParametersCount = 0;
                           a.Rank        = t.Rank;
                           //a.LowerBounds = t.LowerBounds.ToArray();
                           //a.Sizes       = t.Sizes.ToArray();
                           a.EvalProperties(context);
                           a
                         };
          def constructedType = TopConstructedTypeSymbol();
          constructedType.TypeInfo = typeInfo;
          constructedType.Args = args;
          constructedType.EvalProperties(context);
          constructedType
        
        | t is ITypeDefinition =>
          def keyToSymbolMap = keyToSymbolMap;
          mutable typeSymbol;
          if (keyToSymbolMap.TryGetValue(t.InternedKey, out typeSymbol))
          {
            typeSymbol
          }
          else
          {
            // the type definition not found
            objectSymbol
          }
          
        | t is IGenericTypeInstanceReference =>
          def typeInfo = makeSymbol(t.GenericType) :> GenericTypeSymbol;
          def args = t.GenericArguments.Select(makeSymbol).ToArray();

          def constructedType = TopConstructedTypeSymbol();
          constructedType.TypeInfo = typeInfo;
          constructedType.Args = args;
          constructedType.EvalProperties(context);
          constructedType
               
        | _ =>
          resolvs++;
          def keyToSymbolMap = keyToSymbolMap;
          mutable typeSymbol;
          
          if (keyToSymbolMap.TryGetValue(cciTypeReference.InternedKey, out typeSymbol))
          {
            resolvedByKey++;
            typeSymbol
          }
          else
          {
            objectSymbol
          }
      }
            
      foreach (x in keyToSymbolMap.Values)
      {
        def cciType = (x.FirstDeclarationOrDefault :> IExternalTypeDeclaration).Type;
        when (x is SupportsInheritanceTypeSymbol as type)
        {
          def baseTypeSet = BaseTypeReferenceSet(context);
          type.BaseTypeSet = baseTypeSet;
          foreach (cciBaseClasse in cciType.BaseClasses)
          {
            def typeSymbol = makeSymbol(cciBaseClasse);
            baseTypeSet.AddParent(typeSymbol);
          }
          foreach (cciInterface in cciType.Interfaces)
          {
            def typeSymbol = makeSymbol(cciInterface);
            baseTypeSet.AddParent(typeSymbol);
          }
        }
      }

      Debug.WriteLine($"Took: $(timer.Elapsed) resolvs=$resolvs, resolvedByKey=$resolvedByKey, resolvedByResolvedTypeProp=$resolvedByResolvedTypeProp");
      assert(true);
      
      rootNamespace
    }
  }

  [Record]
  public class ExternalNamespaceDeclaration : ExternalDeclaration[NamespaceSymbol]
  {
    public Namespace : INamespaceDefinition { get; private set; }
    public override CreateSymbol() : DeclarationSymbol { NamespaceSymbol() }
  }
  
  public interface IExternalTypeDeclaration
  {
    Type : INamespaceTypeDefinition { get; }
  }

  [Record]
  public class ExternalTypeDeclaration[TSymbol] : ExternalDeclaration[TSymbol], IExternalTypeDeclaration
    where TSymbol : DeclarationSymbol
  {
    public Type : INamespaceTypeDefinition { get; private set; }
  }

  [Record]
  public class TypeParameterDeclaration : ExternalDeclaration[TypeParameterSymbol]
  {
    public TypeParam : IGenericTypeParameter;
    public override CreateSymbol() : DeclarationSymbol { TypeParameterSymbol() }
  }
}
