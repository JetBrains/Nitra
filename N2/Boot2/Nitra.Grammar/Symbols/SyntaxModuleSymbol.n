using Nitra.Model;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = N2SyntaxAst;

namespace Nitra.Typing
{
  [Record, ExportableSymbol]
  public abstract class SyntaxModuleSymbol : SingleDefSymbol, IRuleDeclarationSite, INamespaceMember
  {
    [RecordIgnore] public Children : list[INamespaceMember] { get; private set; }

    public abstract TokenLiterals : list[TokenLiteralSymbol] { get; }

    public abstract ExternalImports : list[SyntaxModuleSymbol] { get; }

    public DeclarationSite : IRuleDeclarationSite { get null }

    [RecordIgnore] private mutable _ruleIdCounter : int;

    internal GetNewRuleId() : int
    {
      def newId = _ruleIdCounter;
      ++_ruleIdCounter;
      newId
    }
  }

  public sealed class ParsedSyntaxModuleSymbol : SyntaxModuleSymbol
  {
    private _ast : G.NamespaceMember.SyntaxModule { get; }

    public this(ast : G.NamespaceMember.SyntaxModule)
    {
      base(ast.Name, ast.Name.Value());
      _ast = ast;
    }

    public override TokenLiterals : list[TokenLiteralSymbol]
    {
      [Memoize] get
      {
        def loop(context : TokenLiteralContext, acc)
        {
          if (null == context)
            acc
          else
            loop(context.TryGetParent(), context.TokenLiteral :: acc)
        }
        loop(_ast.FinalContext.TryGetContext(), [])
      }
    }

    public override ExternalImports : list[SyntaxModuleSymbol]
    {
      [Memoize] get
      {
        def loop(context : OpenNamespaceOrTypeContext, acc)
        {
          if (null == context)
            acc
          else
            loop(context.TryGetParent(), if (context.OpenSymbol is Some(SyntaxModuleSymbol as symbol)) symbol :: acc else acc)
        }
        loop(_ast.FinalContext.TryGetContext(), [])
      }
    }

    public override Description : string
    {
      get { "syntax module" }
    }
  }
}
