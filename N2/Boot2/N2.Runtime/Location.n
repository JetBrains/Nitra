using System;
using System.Runtime;
using System.Runtime.ConstrainedExecution;

using Nemerle.Assertions;
using Nemerle.Extensions;
using Nemerle.Utility;

using SCG = System.Collections.Generic;

namespace N2
{
  public struct Location : IEquatable[string]
  {
    public Source   : SourceSnapshot { get; }
    public StartPos : int            { get; }
    public EndPos   : int            { get; }
    public NToken   : NToken         { get { NToken(StartPos, EndPos) } }
    public Length : int
    {
      [TargetedPatchingOptOut("Performance critical to inline across NGen image boundaries"), ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
      get { EndPos - StartPos }
    }

    public this(source : SourceSnapshot, __startPos : int, endPos : int)
    {
      assert(__startPos <= endPos);
      Source   = source;
      StartPos = __startPos;
      EndPos   = endPos;
    }

    public this(source : SourceSnapshot, tok : NToken)
    {
      this(source, tok.StartPos, tok.EndPos)
    }

    public this(source : SourceSnapshot, a : NToken, b : NToken)
    {
      this(source, Math.Min(a.StartPos, b.StartPos), Math.Max(a.EndPos, b.EndPos))
    }

    public static Default : Location { get; default Location(SourceSnapshot.Default, -1, -1) }

    public IsEmpty         : bool { get { StartPos == EndPos } }
    public IsTextAvailable : bool
    {
      get
      {
        def len = Source?.Text.Length;
        StartPos >= 0 && EndPos >= 0 && StartPos < len && EndPos < len
      }
    }

    public GetText() : string { Source.Text.Substring(StartPos, EndPos - StartPos) }

    public StartLineColumn : LineColumn { get { Source.PositionToLineColumn(StartPos) } }
    public EndLineColumn   : LineColumn { get { Source.PositionToLineColumn(EndPos) } }

    [TargetedPatchingOptOut("Performance critical to inline across NGen image boundaries")]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public IntersectsWith(start : int, end : int) : bool { start <= EndPos && end >= StartPos }
    [TargetedPatchingOptOut("Performance critical to inline across NGen image boundaries")]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public IntersectsWith(other : Location)       : bool { assert(other.Source.Equals(Source)); IntersectsWith(other.StartPos, other.EndPos) }
    [TargetedPatchingOptOut("Performance critical to inline across NGen image boundaries")]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public IntersectsWith(other : NToken)         : bool { IntersectsWith(other.StartPos, other.EndPos) }
    [TargetedPatchingOptOut("Performance critical to inline across NGen image boundaries")]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public Intersection(start : int, end : int)   : ValueOption[Location]
    {
      def start = Math.Max(StartPos, start);
      def end   = Math.Min(EndPos, end);

      if (start <= end)
        VSome(Location(Source, start, end))
      else
        VNone()
    }

    public override ToString() : string
    {
      $<#Start: $StartPos Len: $(EndPos - StartPos) ($(if (IsTextAvailable) GetText() else "<not available>"))#>
    }

    public Equals(str : string) : bool
      implements IEquatable[string].Equals
    {
      // TODO: rewrite with unsafe code
      if (str : object != null && IsTextAvailable && EndPos - StartPos == str.Length)
      {
        def text = Source.Text;
        def length = str.Length;
        def loop(locIndex, strIndex)
        {
          if (strIndex == length)
            true
          else if (text[locIndex] == str[strIndex])
            loop(locIndex + 1, strIndex + 1)
          else
            false
        }
        loop(StartPos, 0)
      }
      else
        false
    }

    public Combine(other : Location) : Location
    {
      assert(object.ReferenceEquals(this.Source, other.Source));
      Location(
        this.Source,
        Math.Min(this.StartPos, other.StartPos),
        Math.Max(this.EndPos, other.EndPos))
    }

    public static @+(a : Location, b : Location) : Location
    {
      a.Combine(b)
    }

    public static @==(a : Location, b : string) : bool
    {
      a.Equals(b)
    }

    public static @==(a : string, b : Location) : bool
    {
      b.Equals(a)
    }

    public static @!=(a : Location, b : string) : bool
    {
      !a.Equals(b)
    }

    public static @!=(a : string, b : Location) : bool
    {
      !b.Equals(a)
    }
  }
}
