using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;
using Nitra.Runtime;

namespace Nitra.Typing
{
  [Record]
  [DebuggerDisplay("{DebugViewInline}")]
  public abstract class Context : IContext
    //where TProject: Project
  {
    public Parent  : Context { get; }
    public Parents : Seq[Context]
    {
      get
      {
        def lst = List();
        def loop(context : Context) : void
        {
          | null => ()
          | _    => lst.Add(context); loop(context.Parent);
        }

        loop(this);
        lst.Reverse();
        lst
      }
    }

    public virtual BindMany[T](path : list[Location]) : BindingResult[T]
      where T: Symbol
    {
      Parent.BindMany(path)
    }

    public virtual BindMany[T](name : Location) : BindingResult[T]
      where T: Symbol
    {
      Parent.BindMany(name)
    }

    public DebugView : string
    {
      get { $<#..$(Parents; "\r\n"; p => $"$p        $(p.GetType().Name)")#> }
    }

    public DebugViewInline : string
    {
      get { $<#..$(Parents; "  ->  ")#> }
    }
  }

  public interface IContext
  {
    Parent : Context { get; }
  }
}
