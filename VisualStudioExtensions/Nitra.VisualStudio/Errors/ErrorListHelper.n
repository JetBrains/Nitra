using Microsoft.VisualStudio;
using Microsoft.VisualStudio.Shell;
using Microsoft.VisualStudio.Shell.Interop;

using Microsoft.VisualStudio.TextManager.Interop;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.VisualStudio
{
  public class ErrorListHelper : IServiceProvider
  {
    mutable _errorListProvider : ErrorListProvider;

    public GetService(serviceType : Type) : object
    {
      Package.GetGlobalService(serviceType)
    }

    public GetErrorListProvider() : ErrorListProvider
    {
      when (_errorListProvider == null)
      {
        _errorListProvider = ErrorListProvider(this);
        _errorListProvider.ProviderName = "Provider";
        _errorListProvider.ProviderGuid = Guid.NewGuid();
      }

      _errorListProvider
    }

    public NavigateDocument(sender : object, _e : EventArgs) : void
    {
      if (sender is Task as task)
      {
        //use the helper class to handle the navigation
        OpenDocumentAndNavigateTo(task.Document, task.Line, task.Column);
      }
      else
        throw ArgumentException("sender");
    }

    public OpenDocumentAndNavigateTo(path : string, line : int, column : int) : void
    {
      def openDoc = GetService(typeof(IVsUIShellOpenDocument)) :> IVsUIShellOpenDocument;

      when (openDoc == null)
          return;

      mutable frame; // IVsWindowFrame
      mutable sp; // Microsoft.VisualStudio.OLE.Interop.IServiceProvider
      mutable hier; // IVsUIHierarchy
      mutable itemid; // uint
      mutable logicalView = VSConstants.LOGVIEWID_Code; // Guid

      when (ErrorHandler.Failed(openDoc.OpenDocumentViaProject(path, ref logicalView, out sp, out hier, out itemid, out frame)) || frame == null)
          return;

      mutable docData;
      _ = frame.GetProperty(__VSFPROPID.VSFPROPID_DocData :> int, out docData);

      // Get the VsTextBuffer
      def buffer =
        match (docData)
        {
          | x is VsTextBuffer => x //VsTextBuffer
          | bufferProvider is IVsTextBufferProvider =>
            mutable lines; // IVsTextLines
            _ = ErrorHandler.ThrowOnFailure(bufferProvider.GetTextBuffer(out lines));
            if ((lines : object) is VsTextBuffer as buffer)
              buffer
            else
            {
              assert2(false, "IVsTextLines does not implement IVsTextBuffer");
              null
            }

          | _ => null
        };

      when (buffer == null)
        return;

      // Finally, perform the navigation.
      def mgr = GetService(typeof(VsTextManagerClass)) :> IVsTextManager;
      when (mgr == null)
          return;

      _ = mgr.NavigateToLineAndColumn(buffer, ref logicalView, line, column, line, column);
    }
  }
}
