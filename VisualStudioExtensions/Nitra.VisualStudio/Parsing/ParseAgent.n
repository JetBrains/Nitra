using Nitra.Runtime;
using Nitra.Internal;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Threading;
using System.Threading.Tasks;
using System.Linq;

using NLog;

using Microsoft.VisualStudio.Text;

namespace Nitra.VisualStudio.Parsing
{
  public sealed class ParseAgent
  {
                     _syncRoot    : object = object();
             mutable _tokenSource : CancellationTokenSource;
             mutable _currentTask : Task;
    volatile mutable _lastResult  : ParseResult;
                     _logger      : Logger = LogManager.GetLogger(typeof(this).FullName);

    public Language   : Language { get; }
    public LastResult : ParseResult   { get _lastResult }

    public this([NotNull] language : Language)
    {
      Language = language;
      _logger.Trace("Language: {0}", language.Name);
    }

    public EnqueueParse([NotNull] snapshot : ITextSnapshot, fileName : string) : void
    {
      lock (_syncRoot)
      {
        _lastResult = null;
        if (_currentTask == null)
        {
          _tokenSource = CancellationTokenSource();
          def token = _tokenSource.Token;
          _currentTask = Task.Factory.StartNew(() => DoParse(snapshot, fileName, token) |> ReportResult, token);
        }
        else
        {
          _tokenSource.Cancel();
          _tokenSource = CancellationTokenSource();
          def token = _tokenSource.Token;
          _currentTask = _currentTask.ContinueWith(_ => DoParse(snapshot, fileName, token) |> ReportResult, token);
        }
      }
    }

    private DoParse(snapshot : ITextSnapshot, fileName : string, token : CancellationToken) : DoParseResult
    {
      def timer = Stopwatch.StartNew();
      def text = snapshot.GetText();
      Trace.WriteLine("snapshot.GetText() took: " + timer.Elapsed);
      def source = SourceSnapshot(text, 0, fileName);
      try
      {
        timer.Restart();
        def result = Language.Parse(source, token);
        timer.Stop();
        def info = "Parsing took: " + timer.Elapsed + "   Mb/sec:" + (text.Length / 1024.0 / 1024.0 / timer.Elapsed.TotalSeconds);
        NitraPackage.Instance.SetStatusBarText(info);
        if (token.IsCancellationRequested)
          DoParseResult.Cancelled(snapshot, fileName)
        else
          DoParseResult.Complete(snapshot, fileName, result)
      }
      catch
      {
        | _ is OperationCanceledException => DoParseResult.Cancelled(snapshot, fileName)
        | e                               => DoParseResult.Failed(snapshot, fileName, e)
      }
    }

    private ReportResult(result : DoParseResult) : void
    {
      //TODO: add exception handling
      | Complete(r) =>
        _lastResult = r;
        ParseComplete?.Invoke(this, ParseCompleteEventArgs(result.Snapshot, result.FileName, r))

      | Failed(e) => ParseFailed?.Invoke(this, ParseFailedEventArgs(result.Snapshot, result.FileName, e))
      | Cancelled => ()
    }

    [Record]
    private variant DoParseResult
    {
      public Snapshot : ITextSnapshot;
      public FileName : string;

      | Complete { result    : ParseResult    }
      | Failed   { exception : Exception }
      | Cancelled
    }

    public event ParseComplete : EventHandler[ParseCompleteEventArgs];
    public event ParseFailed   : EventHandler[ParseFailedEventArgs];
  }

  [Record]
  public sealed class ParseCompleteEventArgs : EventArgs
  {
    public Snapshot : ITextSnapshot { get; }
    public FileName : string        { get; }
    public Result   : ParseResult        { get; }
  }

  [Record]
  public sealed class ParseFailedEventArgs : EventArgs
  {
    public Snapshot  : ITextSnapshot { get; }
    public FileName  : string        { get; }
    public Exception : Exception     { get; }
  }
}
